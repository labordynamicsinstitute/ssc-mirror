{smcl}
{hline}
help for {hi:brrmean}, {hi:brrtotal}, {hi:brrratio}
{hline}

{title:Estimate means, totals, ratios, and proportions for survey data, with balance repeated replication (BRR) based standard errors}

{p 8 17}{cmd:brrmean}{space 2}{it:varlist} [{it:weight}] [{cmd:if} {it:exp}]
		[{cmd:in} {it:range}] [{cmd:,} {it:common_options}]

{p 8 17}{cmd:brrtotal} {it:varlist} [{it:weight}] [{cmd:if} {it:exp}]
		[{cmd:in} {it:range}] [{cmd:,} {it:common_options}]

{p 8 17}{cmd:brrratio} {it:varname} [{cmd:/}] {it:varname} [{it:varname}
		[{cmd:/}] {it:varname ...}]  [{it:weight}] [{cmd:if} {it:exp}]
		[{cmd:in} {it:range}] [{cmd:,} {it:common_options}]


{p}where {it:common_options} are

{p 17 17}{cmdab:brrw:eight(}{it:varlist}{cmd:)} 
    {cmdab:fay(}{it:#}{cmd:)}
    {cmdab:dof(}{it:#}{cmd:)}

{p 17 17}{cmd:by(}{it:varlist}{cmd:)}
	[{cmdab:com:plete}|{cmdab:av:ailable}]
	{cmdab:nolab:el}
	{cmdab:l:evel(}{it:#}{cmd:)} {cmd:ci} {cmd:deff} {cmd:deft} {cmd:meff}
	{cmd:meft} {cmd:obs} {cmd:size}

{p}{cmd:brrmean}, {cmd:brrratio}, and {cmd:brrtotal} typed without arguments
redisplay previous results.  Any of the following options can be used when
redisplaying results:

{p 17 17}{cmdab:l:evel(}{it:#}{cmd:)} {cmd:ci} {cmd:deff} {cmd:deft}
	{cmd:meff} {cmd:meft} {cmd:obs} {cmd:size}

{p}All of these commands allow {cmd:pweight}s; see help
{help weights}.


{title:Description}

{p}{cmd:brrmean}, {cmd:brrtotal}, and {cmd:brrratio} produce
estimates of population means, totals, ratios, and proportions.
Standard errors are calculated using a series of user-supplied replication weights, by the
balanced repeated replication (BRR) method.  This is an alternate method to
the Taylor series linearization methods used by Stata's {help svy:svy-based} commands.

{p}Estimates for multiple subpopulations can be obtained using the {cmd:by()}
option.  The {cmd:if} option will give estimates for a single population.  (Note that
with the BRR method, use of {cmd:if} or {cmd:in} producese correct estimates for 
the relevant subpopulation.)

{p}{help svytest} will operate after these commands, as it does after their svy-based 
equivalents.


{title:Options}

{p 0 4}{cmd:brrweight()} specifies the list of variables that contain the replicate weights
for the dataset.  The standard errors for the model are based on the variation in 
the estimates generated across the various weights.

{p 4 4}A set of {cmd:brrweights} is required for the analysis.  Once the brrweights are specified
they are stored as a {help char:characteristic} of the dataset and need not be respecified in subsequent command.

{p 0 4}{cmd:fay()} specifies the k value that should be used for weighting the estimates, based
on Fay's method.  The default is zero, meaning that simple averaging will be used.  As with the replicate weights,
the value for fay() is stored as a characteristic of the dataset once it is specified, and need not be
re-specified in subsequent commands.

{p 0 4}{cmd:dof()} specifies the degrees of freedom for model fit and t-statistics.  The default is
to use the number of replications.

{p 0 4}{cmd:by(}{it:varlist}{cmd:)} specifies that estimates be computed for
the subpopulations defined by different values of the variable(s) in the
{it:varlist}.

{p 0 4}{cmd:nolabel} can only be specified when {cmd:by()} is specified.
{cmd:nolabel} requests that numeric values rather than value labels be used to
label output for subpopulations.  By default, value labels are used.

{p 0 4}[{cmd:complete}|{cmd:available}] specifies how missing values
are to be handled.  {cmd:complete} specifies that only observations with
complete data should be used.  {cmd:available} specifies that all available
nonmissing values be used for each estimate.

{p 4 4}If neither {cmd:complete} nor {cmd:available} is specified,
{cmd:available} is the default when there are missing values and there are two
or more variables in the {it:varlist} (or four or more for {cmd:svyratio}).
{cmd:complete} must be specified to compute the covariance or to use
{cmd:svytest} after running the command; see help
{help svytest}.

{p 0 4}{cmd:level(}{it:#}{cmd:)} specifies the confidence level (i.e., nominal
coverage rate), in percent, for confidence intervals; see help {help level}.

{p 0 4}{cmd:ci} requests that confidence intervals be displayed.  If no
display options are specified then, by default, confidence intervals are
displayed.

{p 0 4}{cmd:deff} requests that the design-effect measure deff be displayed.
If no display options are specified then, by default, deff is displayed.

{p 0 4}{cmd:deft} requests that the design-effect measure deft be displayed.
See {hi:[R] svymean} for a discussion on deff and deft.

{p 0 4}{cmd:meff} requests that the meff measure of misspecification effects
be displayed.

{p 0 4}{cmd:meft} requests that the meft measure of misspecification effects
be displayed.  See {hi:[R] svymean} for a discussion of meff and meft.

{p 0 4}{cmd:obs} requests that the number of observations used for the
computation of the estimate be displayed for each row of estimates.

{p 0 4}{cmd:size} requests that the estimate of the (sub)population size be
displayed for each row of estimates.  The (sub)population size estimate equals
the sum of the weights for those observations used for the mean/total/ratio
estimate.


{title:Examples}

{p 8 12}{inp:. brrmean birthwgt [pw=wgt], brrw(bw*)}{p_end}
{p 8 12}{inp:. brrmean birthwgt, by(race)}{p_end}
{p 8 12}{inp:. brrmean birthwgt if race==1}

{p 8 12}{inp:. brrratio hdresult/tcresult}{p_end}


{title:Methods and formulae}

{p}Point estimates are calculated using aweights, and are identical to those produced by Stata's svy-based
commands.  The variance matrix of the estimates is formed by calculating

                   G    
		V = c * SUM [ (B - B(i))(B - B(i))' ]
                  i=1

{p}where B is the estimated coefficient vector based on the full sample weights, B(i) is the estimated coefficient vector using the i'th set of replicate weights, G is the number of replicates, and c is a constant defined as:

         1 / G            for standard BRR (i.e. fay==0), or

         1 / (G*(1-k)^2)  for Fay's method.


{title:Acknowledgements}

{p}These commands consists largely of the ado file code from official Stata's {cmd:svy_x} command, which 
underlies {cmd:svymean}, {cmd:svytotal}, and {cmd:svyratio}.  They are 
modified to calculate (co)variances differently.
I would like to thank Bobby Gutierrez at StataCorp for advice on implementation of BRR.


{title:Author}

	Nick Winter
	Cornell University
	nw53@cornell.edu

