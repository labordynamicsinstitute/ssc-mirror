<pre>
<b>help bayerhanck</b>
-------------------------------------------------------------------------------
<p>
<b><u>Title</u></b>
<p>
bayerhanck -- Test for Non-Cointegration
<p>
<b><u>Syntax</u></b>
<p>
<b>bayerhanck</b> LHSvar [<i>if</i>] [<i>in</i>] <b>,</b> rhs(RHSvarlist) [<i>options</i>]
<p>
    <i>options</i>                     Description
    -------------------------------------------------------------------------
    Model
      <b><u>l</u></b><b>ags(</b><i>#</i><b>)</b>                   use <i>#</i> for the maximum number of augmentation
                                  lags (&gt;=0), default is 1
      <b><u>t</u></b><b>rend(constant)</b>           include an unrestricted constant in model;
                                  the default
      <b><u>t</u></b><b>rend(trend)</b>              include a linear trend in the cointegrating
                                  equations and a quadratic trend in the
                                  undifferenced data
      <b><u>t</u></b><b>rend(none)</b>               do not include a trend or a constant
<p>
    Reporting
      <b><u>c</u></b><b>rit(1 | 5 | 10)</b>           Level-alpha in % for the critical value of
                                  the test to be reported
<p>
-------------------------------------------------------------------------------
<p>
<b><u>Description</u></b>
<p>
    <b>bayerhanck</b> produces a joint test-statistic for the null of
    no-cointegration based on Engle-Granger, Johansen maximum eigenvalue,
    Boswijk, and Banerjee tests (see Bayer and Hanck, 2009, for details).
<p>
<p>
<b><u>Saved results</u></b>
<p>
<p>
<b>bayerhanck</b> saves the following in <b>e()</b>:
<p>
    Scalars     
      <b>e(EJ) </b>            test statistics based on Engle-Granger and Johansen
                         tests
      <b>e(BECREJ)</b>         test statistics based on all four tests
      <b>e(CRIT_EJ) </b>       critical value for the test statistics based on
                         Engle-Granger and Johansen tests
      <b>e(CRIT_BECREJ)</b>    critical value for the test statistics based on all
                         four tests
<p>
    Matrices    
      <b>e(STAT) </b>          test statistics of all four underlying tests
      <b>e(PV) </b>            corresponding p-values
<p>
<b><u>Examples</u></b>
<p>
    <i>Example 1: (underlying tests produce conflicting results)</i>
<p>
    Setup
        <b>. webuse lutkepohl</b>
<p>
    Test for non-cointegration
        <b>. bayerhanck linvestment, rhs(lincome lconsumption)</b>
<p>
<p>
    Same as above, but use 4 lags in the underlying VECM model
        <b>. bayerhanck linvestment, rhs(lincome lconsumption) lags(4)</b>
<p>
    Same as above, but report 10% critical values instead of 5%
        <b>. bayerhanck y, rhs(i c) lags(4) crit(10)</b>
<p>
<p>
    <i>Example 2: Monte Carlo Study</i>
<p>
        <b>clear all</b>
        <b>set more off</b>
        <b>set matsize 100</b>
        <b>local rep=100</b>
        <b>mat def testPower=J(`rep',2,999)</b>
        <b>mat def testSize=J(`rep',2,999)</b>
        <b>forv z=1/`rep'</b> {
            <b>clear</b>
            <b>qui</b> {
            <b>set obs 400</b>
            <b>gen dx=rnormal()</b>
            <b>gen x=sum(dx)+50</b>
            <b>forv j=1/5</b> {
            <b>gen dz`j'=rnormal()</b>
            <b>gen z`j'=sum(dz`j')</b>
            }
            <b>gen T=_n</b>
            <b>tsset T</b>
            <b>gen u=rnormal()</b>
            <b>replace u=u+0.95*l.u if T&gt;1</b>
            <b>gen y=x+u</b>
            <b>drop if T&lt;200</b>
            <b>replace x=x+T</b>
        
            <b>di "Power Example"</b>
            <b>bayerhanck x, rhs(y) trend(trend) lags(1)</b>
            <b>mat testPower[`z',1]=`e(EJ)'</b>
            <b>mat testPower[`z',2]=`e(BECREJ)'</b>
            <b>di "Size Example"</b>
            <b>bayerhanck x, rhs(z*) trend(trend) lags(1) crit(10)</b>
            <b>mat testSize[`z',1]=`e(EJ)'</b>
            <b>mat testSize[`z',2]=`e(BECREJ)'</b>
            }
        }
        <b>ereturn list</b>
        <b>svmat testPower</b>
        <b>svmat testSize</b>
        <b>keep test*</b>
        <b>drop if testPower1==.</b>
        <b>count if testSize1&gt;e(CRIT_EJ)</b>
        <b>count if testSize2&gt;e(CRIT_BECREJ)</b>
        <b>count if testPower1&gt;e(CRIT_EJ)</b>
        <b>count if testPower2&gt;e(CRIT_BECREJ)</b>
<p>
<p>
<b><u>Also see</u></b>
<p>
    Bayer, Christian and Christoph Hanck: "Combining Non-Cointegration
             tests", METEOR RM/09/012, University of Maastricht.
<p>
<b><u>Installation Files</u></b>
        bayerhanck.ado
        NullDistr.dta
        bayerhanck.sthlp
</pre>