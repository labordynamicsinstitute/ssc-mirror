<pre>
-------------------------------------------------------------------------------
help for <b>xrewide</b> and <b>xrelong</b>                                     (Roger Newson)
-------------------------------------------------------------------------------
<p>
<p>
<b><u>Extended versions of </u></b><b><u>reshape wide</u></b><b><u> and </u></b><b><u>reshape long</u></b>
<p>
        <b>xrewide</b> [ <i>stubnames</i> ] [ ,
                     <b><u>cjv</u></b><b>alue(</b><i>charname</i><b>)</b> <b><u>vjv</u></b><b>alue(</b><i>varlist</i><b>)</b> <b><u>cjl</u></b><b>abel(</b><i>charname</i><b>)</b>
                     <b><u>vjl</u></b><b>abel(</b><i>varlist</i><b>)</b>
                     <b><u>pjv</u></b><b>alue(</b><i>string</i><b>)</b> <b><u>sjv</u></b><b>alue(</b><i>string</i><b>)</b> <b><u>pjl</u></b><b>abel(</b><i>string</i><b>)</b>
                     <b><u>sjl</u></b><b>abel(</b><i>string</i><b>)</b>
                     <b><u>xmls</u></b><b>ub lxjk(</b><i>local_macro_name</i><b>) lxkj(</b><i>local_macro_name</i><b>)</b>
                     <i>reshape_wide_options</i> ]
<p>
        <b>xrelong</b> [ <i>stubnames</i> ] [ , <b><u>jla</u></b><b>bel(</b><i>label_name</i><b>)</b> <i>reshape_long_options</i> ]
<p>
    where <i>stubnames</i> is a list of stub names as input to <b>reshape wide</b>,
    <i>label_name</i> is the name of a value label, <i>reshape_wide_options</i> is a list
    of options used by <b>reshape wide</b>, and <i>reshape_long_options</i> is a list of
    options used by <b>reshape long</b>.
<p>
<p>
<b><u>Description</u></b>
<p>
    The <b>xrewide</b> package contains 2 commands, <b>xrewide</b> and <b>xrelong</b>.  The
    <b>xrewide</b> commmand is an extended version of <b>reshape wide</b>.  It saves
    results in variable characteristics, in addition to the dataset
    characteristics saved by <b>reshape</b>.  This is done using additional options
    for assigning the values and/or value labels of the <i>j</i>-variable to
    characteristics of the corresponding generated output variables.  These
    characteristics can be useful for defining column headings if the
    reshaped output dataset is output to a table using the <b>listtab</b> package,
    which can be downloaded from SSC.  The <b>xrelong</b> commmand is an extended
    version of <b>reshape long</b>, with the option of assigning an existing value
    label to the generated variable specified by the <b>j()</b> option.  For more
    about the use of <b>xrewide</b> with <b>listtab</b> and other packages to produce
    tables, see Newson (2012).
<p>
<p>
<b><u>Options for use with </u></b><b><u>xrewide</u></b>
<p>
    <b>cjvalue(</b><i>charname</i><b>)</b> specifies thae name of a variable characteristic, to be
        assigned for the generated output variables, which will contain the
        corresponding values of the input variable specified by the <b>j()</b>
        option of <b>reshape wide</b>.
<p>
    <b>vjvalue(</b><i>varlist</i><b>)</b> specifies a subset of the input variables to be
        reshaped, whose corresponding reshaped output variables will have the
        characteristic specified by the <b>cjvalue()</b> option.  If <b>vjvalue()</b> is
        not specified, then it is set to the full set of input variables to
        be reshaped, and the characteristic specified by the <b>cjvalue()</b> option
        will be assigned to all the reshaped output variables.
<p>
    <b>cjlabel(</b><i>charname</i><b>)</b> specifies thae name of a variable characteristic, to be
        assigned for the generated output variables, which will contain the
        corresponding value labels of the input variable specified by the <b>j()</b>
        option of <b>reshape wide</b>.
<p>
    <b>vjlabel(</b><i>varlist</i><b>)</b> specifies a subset of the input variables to be
        reshaped, whose corresponding reshaped output variables will have the
        characteristic specified by the <b>cjlabel()</b> option.  If <b>vjlabel()</b> is
        not specified, then it is set to the full set of input variables to
        be reshaped, and the characteristic specified by the <b>cjlabel()</b> option
        will be assigned to all the reshaped output variables.
<p>
    <b>pjvalue(</b><i>string</i><b>)</b> specifies a prefix, to be added to the left of <b>j()</b> values
        when these are assigned to the variable characteristic specified by
        <b>cjvalue()</b>.
<p>
    <b>sjvalue(</b><i>string</i><b>)</b> specifies a suffix, to be added to the right of <b>j()</b>
        values when these are assigned to the variable characteristic
        specified by <b>cjvalue()</b>.
<p>
    <b>pjlabel(</b><i>string</i><b>)</b> specifies a prefix, to be added to the left of <b>j()</b> value
        labels when these are assigned to the variable characteristic
        specified by <b>cjlabel()</b>.
<p>
    <b>sjlabel(</b><i>string</i><b>)</b> specifies a suffix, to be added to the right of <b>j()</b> value
        labels when these are assigned to the variable characteristic
        specified by <b>cjlabel()</b>.
<p>
    <b>xmlsub</b> specifies that, in the variable characteristic specified by
        <b>cjlabel()</b>, the substrings <b>"&amp;"</b>, <b>"&lt;"</b> and <b>"&gt;"</b> will be replaced
        throughout with the XML entity references <b>"&amp;amp;"</b>, <b>"&amp;lt;"</b> and <b>"&amp;gt;"</b>,
        respectively.  This is useful if the variable characteristic is
        intended for output to a table in a document in XHTML, or in other
        XML-based languages.
<p>
    <b>lxjk(</b><i>local_macro_name</i><b>)</b> specifies the name of a local macro, to be
        assigned a list of the names of the reshaped output variables created
        by <b>xrewide</b>, sorted primarily in the order of the corresponding <b>j()</b>
        values and secondarily in the order of the corresponding input
        variables specified by the <i>stubnames</i>.
<p>
    <b>lxkj(</b><i>local_macro_name</i><b>)</b> specifies the name of a local macro, to be
        assigned a list of the names of the reshaped output variables created
        by <b>xrewide</b>, sorted primarily in the order of the corresponding input
        variables specified by the <i>stubnames</i> and secondarily in the order of
        the corresponding <b>j()</b> values.
<p>
    <i>reshape_wide_options</i> is a list of options used by <b>reshape wide</b>.
<p>
<p>
<b><u>Options for use with </u></b><b><u>xrelong</u></b>
<p>
    <b>jlabel(</b><i>label_name</i><b>)</b> specifies the name of an existing value label, to be
        assigned to the generated variable specified by the <b>j()</b> option of 
        <b>reshape long</b>.
<p>
    <i>reshape_long_options</i> is a list of options used by <b>reshape long</b>.
<p>
<p>
<b><u>Examples</u></b>
<p>
    Set-up:
<p>
        . webuse reshape1, clear
        . describe
        . list
        . reshape long inc ue, i(id) j(year)
        . describe
        . char list
        . list, sepby(id)
        . lab def yr 80 "Eighty" 81 "Eighty one" 82 "Eighty two"
        . lab val year yr
        . list, sepby(id)
<p>
    Simple examples:
<p>
        . xrewide inc ue, i(id) j(year) cjlabel(varname2) vjlabel(inc)
            cjval(varseq2) vjval(ue)
        . char list
<p>
        . xrewide, cjlab(head1) vjlab(ue)
        . char list
<p>
    Note that, if the user does not specify the variables to be reshaped
    and/or the <b>i()</b> variable list and/or the <b>j()</b> variable, then <b>xrewide</b> tries
    to find them in the latest <b>reshape</b> estimation results, which are stored
    as dataset characteristics.
<p>
    The following example demonstrates the use of <b>xrewide</b>, together with the 
    <b>chardef</b> package downloadable from SSC, to assign characteristics.  These
    characteristics will then be used by the <b>listtab</b> package, also
    downloadable from SSC, to define headings in a LaTeX <b>tabular</b> environment
    containing a table of the reshaped dataset, which can be cut and pasted
    into a LaTeX document.  Note that characteristics assigned by <b>chardef</b> to
    the input variables to be reshaped are inherited by the reshaped output
    variables generated by <b>xrewide</b>.
<p>
        . chardef id inc ue, char(varname) val("ID" "Income" "UE")
        . list, subvar
        . xrewide inc ue, i(id) j(year) cjlab(varname2) vjlab(inc)
            pjlab("\textit{") sjlab("}")
        . listtab_vars id *80 *81 *82, sub(char varname) rstyle(tabular)
            lo(h1)
        . listtab_vars id *80 *81 *82, sub(char varname2) rstyle(tabular)
            lo(h2)
        . listtab id *80 *81 *82, type rstyle(tabular)
            head("\begin{tabular}{rrrrrrr}" "`h2'" "`h1'")
            foot("\end{tabular}")
<p>
    The following sequence demonstrates the use of <b>xrelong</b> with the <b>varlabdef</b>
    package, which can be downloaded from SSC, and can create a value label
    based on a list of variables, with 1 value per variable, and value labels
    copied from the variable labels, or from other variable attributes.
<p>
    Set-up:
<p>
        . sysuse auto, clear
        . keep foreign make mpg weight price
        . rename mpg cmval1
        . rename weight cmval2
        . rename price cmval3
        . describe
<p>
    Reshape from wide to long:
<p>
        . varlabdef cmlab, vlist(cmval*) from(varlab)
        . label list cmlab
        . xrelong cmval, i(foreign make) j(carmeas) jlabel(cmlab)
        . describe
        . list, abbr(32) sepby(foreign make)
<p>
    Reshape from long to wide:
<p>
        . xrewide cmval, i(foreign make) j(carmeas) cjlab(cmeaslabel)
        . describe
        . list
<p>
    Reshape from wide to long again:
<p>
        . varlabdef cmlab2, vlist(cmval*) from(char cmeaslabel)
        . label list cmlab2
        . xrelong cmval, i(foreign make) j(carmeas) jlabel(cmlab2)
        . describe
        . list, abbr(32) sepby(foreign make)
<p>
<p>
<b><u>Saved results</u></b>
<p>
    <b>xrewide</b> and <b>xrelong</b> save the same dataset characteristics saved by
    <b>reshape wide</b> and <b>reshape long</b>, respectively.  However, the characteristic
    <b>_dta[ReS_jv]</b> is always set by <b>xrewide</b>, whether or not the user specifies
    the values explicitly in the <b>j()</b> option.  Also, the characteristic
    <b>_dta[ReS_Xij]</b> always contains a stub list complete with the <b>@</b> signs,
    whether or not the user explicitly included these <b>@</b> signs in the input
    stub names.  This makes life simpler for programmers, who might want to
    use these characteristics.
<p>
<p>
<b><u>Author</u></b>
<p>
    Roger Newson, National Heart and Lung Institute, Imperial College London,
    UK.  Email: r.newson@imperial.ac.uk
<p>
<p>
<a name="references"></a><b><u>References</u></b>
<p>
    Newson, R. B.  2012.  From resultssets to resultstables in Stata.  <i>The</i>
        <i>Stata Journal</i> 12(2): 191-213.  Download from <i>The Stata Journal</i>
        website.
<p>
<p>
<b><u>Also see</u></b>
<p>
     Manual: <b>[D] reshape</b>, <b>[P] char</b>
    On-line: help for <b>reshape</b>, <b>char</b>
             help for <b>listtab</b>, <b>chardef</b>, <b>varlabdef</b> if installed
</pre>