*! version 2.1.0  9apr2006
*! David Roodman, Center for Global Development, droodman@cgdev.org

program define xtab2_p
	version 7.0
      syntax newvarname [if] [in], [XB REsiduals DIFFerence]
	tempname touse
	mark `touse' `if' `in'
	if "`xb'`residuals'" == "xbresiduals" {
		di as err "You can only predict one statistic at a time."
		exit 198 
	}
	_predict `typlist' `varlist' if `touse'
	qui if "`e(esttype)'" == "difference" {
		sum `e(depvar') if e(sample), meanonly
		local yavg = r(mean)
		sum `varlist' if e(sample), meanonly
		replace `varlist' = `varlist' - r(mean) + `yavg'
	}
	if "`residuals'" != "" {
		qui replace `varlist' = `e(depvar)' - `varlist'
	}
	if "`difference'" != "" {
		qui replace `touse' = D.`varlist'
		qui replace `varlist' = `touse'
	}
end
