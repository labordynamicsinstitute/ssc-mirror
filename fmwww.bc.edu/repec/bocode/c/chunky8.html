<pre>
<b>help chunky8</b>
-------------------------------------------------------------------------------
<p>
<b><u>Title</u></b>
<p>
    <b>chunky8</b> -- Text file chunking utility
<p>
<p>
<b><u>Syntax</u></b>
<p>
        <b>chunky8</b> <b>using</b> <i>filename</i> [<b>,</b><i>options</i>]
<p>
    <i>options</i>                Description
    -------------------------------------------------------------------------
      <b><u>i</u></b><b>ndex(</b><i>#</i><b>)</b>              Starting line in file
      <b><u>c</u></b><b>hunk(</b><i>#</i><b>)</b>              Size of chunk in lines
      <b><u>keep</u></b><b>first</b>             Keep the first line (e.g. with variable names) of
                             the file with each chunk
      <b><u>l</u></b><b>ist</b>                  List the lines as they are being processed (used
                             for peeking at the first couple of lines or in
                             debugging)
    -------------------------------------------------------------------------
    saving options
      <b><u>s</u></b><b>aving</b>                Filename to save html
      <b>replace</b>               Replace previously saved file
<p>
    Note that if your <i>filename</i> contains embedded spaces, remember to enclose
      it in double quotes.
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>chunky8</b> is typically used in one of two ways:
<p>
    (1) When a text file is too large for <b>infiling</b>, it can be broken into
        chunks that can be imported individually.
<p>
    (2) Sometimes one needs to get a sense of what the variable names and
        structure of a text file are by pulling off just the first few lines.
<p>
<b><u>Options</u></b>
<p>
        +------+
    ----+ Main +-------------------------------------------------------------
<p>
    <b><u>i</u></b><b>ndex(</b><i>#</i><b>)</b> Thinking of a text file as having lines numbered 1 to N, index
        indicates the starting line number for the chunk
<p>
    <b><u>c</u></b><b>hunk(</b><i>#</i><b>)</b> The chunk is the number of lines to be returned.  Ideally the
        chunksize would be the largest that could be used in the subsequent 
        <b>infiling</b> process.
<p>
    <b><u>keep</u></b><b>first</b> Comma Separated Value (CSV) files frequently come with the
        variable names in the first line of the file.  For this type of file,
        the first line of names should be retained for all file chunks.
        <b>keepfirst</b> writes out the first line of the <b>using</b> file at the
        beginning of each chunk.  This allows a subsequent <b>insheet</b> to be done
        easily on each chunk.
<p>
    <b><u>l</u></b><b>ist</b> Listing the first few lines of a text file can be useful. You can
        use the <b>type</b> command but the <b>list</b> option is an alternative and can be
        set to display a single line.
<p>
        +--------+
    ----+ saving +-----------------------------------------------------------
<p>
    <b><u>s</u></b><b>aving(</b><i>"path &amp; filename"</i><b>)</b> Filename to save chunk
<p>
    <b>replace</b> Replace previously saved file
<p>
<p>
<b><u>Examples</u></b>
<p>
   .<b>chunky8 using ReallyBig.csv, index(1) chunk(10000) saving(chunk1.csv, repla</b>
<b>&gt; ce)</b>
   .<b>chunky8 using ReallyBig.csv, list</b>
   .<b>chunky8 using ReallyBig.csv, index(100001) chunk(100000) keepfirst saving(c</b>
<b>&gt; hunk2.csv, replace)</b>
<p>
<p>
<b><u>Saved results</u></b>
<p>
    <b>chunky8</b> saves the following in <b>r()</b>:
<p>
    Scalars   
      <b>r(eof)</b>         <b>e</b>nd <b>o</b>f <b>f</b>ile indicator r(eof)=1 if end of file reached
      <b>r(index)</b>       line number of the next line after the last line read,
                       i.e. the starting index for the next chunk
<p>
<p>
<b><u>Notes</u></b>
<p>
    When used on a very large file, <b>chunky8</b> would normally be employed in a
    loop that reads in and saves chunks as serially numbered files.  The
    program listing below demonstrates a typical way in which <b>chunky8</b> would
    be employed.
<p>
    <b>chunky8</b> is a deprecated version of <b>chunky</b> maintained for users who are
    using pre-Mata releases of Stata or users who require a line-indexed
    method of extracting part of a test file. The current version of <b>chunky</b>
    for Stata releases 9 and above uses a different logic, syntax and has the
    file I/O written utilizing highly efficient Mata routines. See chunky if
    installed or read description from net.
<p>
// chunky8.ado demo
// Make sure you have a minimum of 50M of memory to run this demo
<p>
// Set up a test comma separated value file
sysuse auto, clear
expand 10000
describe
outsheet using test.csv, names comma replace
<p>
// If you want to look at an example of the file's
//  variables and first few lines:
chunky8 using test.csv, list
<p>
// Set up chunksize and locals for the loop
local part 0                     // Counter for parts
local index 1                    // Keeps running line index
local chunksize 250000           // # of lines in each chunk
tempfile chunkfile              // Temporary filename for chunks
<p>
// Loop until end of file is reached
while r(eof)!=1{
    chunky8 using test.csv, ///
      index(`index') chunk(`chunksize') ///
      saving("`chunkfile'", replace) keepfirst
    if r(eof) { // break out when end of file reached
        continue, break
        }
        else {  // get starting index for next chunk
            local index `r(index)'
            }
       insheet using "`chunkfile'", clear comma names
       // keep fields you want
       keep make mpg headroom weight length displacement
       // save part of file and increment chunk count
       save test_`++part', replace
       }
<p>
// Append parts together
use test_1, clear
forvalues i=2/`part' {
    append using test_`i'
    erase test_`i'.dta
    }
describe
<p>
<b><u>Author</u></b>
<p>
    David C. Elliott,
    Nova Scotia Department of Health, Halifax, Nova Scotia, Canada
</pre>