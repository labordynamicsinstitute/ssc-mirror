<pre>
-------------------------------------------------------------------------------
help for <b>gologit2</b>
-------------------------------------------------------------------------------
<p>
<b><u>Generalized Ordered Logit Models for Ordinal Dependent Variables</u></b>
<p>
        <b>gologit2</b> <i>depvar</i> [<i>indepvars</i>] [<i>weight</i>] [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [<b>,</b> <b><u>p</u></b><b>l</b>
               <b><u>p</u></b><b>l(</b><i>varlist</i><b>)</b> <b><u>np</u></b><b>l</b> <b><u>np</u></b><b>l(</b><i>varlist</i><b>)</b> <b><u>auto</u></b><b>fit</b> <b><u>auto</u></b><b>fit(</b><i>alpha</i><b>)</b>
               <b>link(</b><i>logit/probit/cloglog/loglog/cauchit</i><b>)</b> <b>force</b> <b><u>lrf</u></b><b>orce</b> <b><u>g</u></b><b>amma</b>
               <b><u>g</u></b><b>amma(</b><i>name</i><b>)</b> <b><u>nol</u></b><b>abel</b> <b><u>mls</u></b><b>tart</b> <b><u>sto</u></b><b>re(</b><i>name</i><b>)</b> <b><u>c</u></b><b>onstraints(</b><i>clist</i><b>)</b>
               <b><u>r</u></b><b>obust</b> <b><u>cl</u></b><b>uster(</b><i>varname</i><b>)</b> <b><u>l</u></b><b>evel(</b><i>#</i><b>)</b> <b>or</b> <b>log</b> <b>v1</b> <b>svy</b> <i>svy_options</i>
               <i>maximize_options</i> ]
<p>
    where <i>svy_options</i> are
<p>
        <b><u>sub</u></b><b>pop(</b><i>subpop_spec</i><b>)</b> <b><u>nosvy</u></b><b>adjust</b> <b><u>pr</u></b><b>ob</b> <b>ci</b> <b>deff</b> <b>deft</b> <b>meff</b> <b>meft</b>
<p>
    and <i>subpop_spec</i> is
<p>
            [<i>varname</i>] [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [, <b><u>srs</u></b><b>subpop</b> ]
<p>
    <b>gologit2</b> shares the features of all estimation commands; see help est.
    <b>gologit2</b> typed without arguments redisplays previous results.  The
    following options may be given when redisplaying results:
<p>
        <b><u>g</u></b><b>amma</b> <b><u>g</u></b><b>amma(</b><i>name</i><b>)</b> <b><u>sto</u></b><b>re(</b><i>name</i><b>)</b> <b>or</b> <b><u>l</u></b><b>evel(</b><i>#</i><b>)</b> <b><u>pr</u></b><b>ob</b> <b>ci</b> <b>deff</b> <b>deft</b>
<p>
    <b>gologit2</b> works under both Stata 8.2 and Stata 9 or higher.  If using
    Stata 9, the <b>by</b>, <b>nestreg</b>, <b>stepwise</b>, <b>xi</b>, and possibly other prefix
    commands are allowed; see prefix. The <b>svy</b> prefix command is NOT currently
    supported; use the <b>svy</b> option instead.
<p>
    <b>fweight</b>s, <b>iweight</b>s, and <b>pweight</b>s are allowed; see help weights.
<p>
    The syntax of predict following <b>gologit2</b> is
<p>
        <b>predict</b> [<i>type</i>] <i>newvarname</i>(<i>s</i>) [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [<b>,</b> <i>statistic</i>
                <b><u>o</u></b><b>utcome(</b><i>outcome</i><b>)</b> ]
<p>
    where <i>statistic</i> is
<p>
        <b>p</b>        probability (specify one new variable and <b>outcome()</b> option,
                     or specify k new variables, k = # of outcomes); the
                     default
        <b>xb</b>       linear prediction (<b>outcome()</b> option required)
        <b>stdp</b>     S.E. of linear prediction (<b>outcome()</b> option required)
        <b>stddp</b>    S.E. of difference in linear predictions (<b>outcome()</b> option
                     is <b>outcome(</b><i>outcome1</i><b>,</b><i>outcome2</i><b>)</b>)
<p>
    Note that you specify one new variable with <b>xb</b>, <b>stdp</b>, and <b>stddp</b> and
    specify either one or k new variables with <b>p</b>.
<p>
    These statistics are available both in and out of sample; type "<b>predict</b>
    <i>...</i> <b>if e(sample)</b> <i>...</i>" if wanted only for the estimation sample.
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>gologit2</b> is a user-written program that estimates generalized ordered
    logit models for ordinal dependent variables. The actual values taken on
    by the dependent variable are irrelevant except that larger values are
    assumed to correspond to "higher" outcomes. Up to 20 outcomes are
    allowed. <b>gologit2</b> is inspired by Vincent Fu's <b>gologit</b> program and is
    backward compatible with it but offers several additional powerful
    options.
<p>
    A major strength of <b>gologit2</b> is that it can also estimate three special
    cases of the generalized model: the<i> proportional odds/parallel lines</i>
    <i>model</i>, the <i>partial proportional odds model</i>, and the <i>logistic regression</i>
    <i>model</i>. Hence, <b>gologit2</b> can estimate models that are less restrictive than
    the proportional odds /parallel lines models estimated by <b>ologit</b> (whose
    assumptions are often violated) but more parsimonious and interpretable
    than those estimated by a non-ordinal method, such as multinomial
    logistic regression (i.e. <b>mlogit</b>). The <b>autofit</b> option greatly simplifies
    the process of identifying partial proportional odds models that fit the
    data.
<p>
    An alternative but equivalent parameterization of the model that has
    appeared in the literature is reported when the <b>gamma</b> option is selected.
    Other key advantages of <b>gologit2</b> include support for linear constraints
    (making it possible to use <b>gologit2</b> for constrained logistic regression),
    survey data estimation, and the computation of estimated probabilities
    via the <b>predict</b> command.
<p>
    Also, if the user considers them more appropriate for their data, probit,
    complementary log-log, log-log and cauchit links can be used instead of
    logit by specifying the <b>link</b> option, e.g. <b>link(</b><i>l</i><b>)</b> for logit (the
    default), <b>link(</b><i>p</i><b>)</b> for probit, <b>link(</b><i>c</i><b>)</b> for complementary log-log, <b>link(</b><i>ll</i><b>)</b>
    for log-log, and <b>link(</b><i>ca</i><b>)</b> for cauchit.
<p>
    <b>gologit2</b> works under both Stata 8.2 and Stata 9 or higher.  Syntax is the
    same for both versions; but if you are using Stata 9 or higher, gologit2
    supports several prefix commands, including <b>by</b>, <b>nestreg</b>, <b>xi</b> and <b>sw</b>.
    Stata 9's <b>svy</b> prefix command is NOT currently supported; use the <b>svy</b>
    option instead.
<p>
    If you want to estimate marginal effects after <b>gologit2</b>, it is
    recommended that you install the most current versions of the
    user-written <b>mfx2</b> and/or <b>margeff</b> commands, both available from SSC.
    These commands are generally easier to use than <b>mfx</b> and make it possible
    to output the results using table-formatting programs like <b>outreg2</b> and
    <b>estout</b>.
<p>
    More information on the statistical theory behind <b>gologit2</b> as well as
    several worked examples and a troubleshooting FAQ can be found at 
    http://www3.nd.edu/~rwilliam/gologit2/.
<p>
<b><u>Warning &amp; Error Messages</u></b>
<p>
    Note: A trouble-shooting FAQ with additional information can be found at
    http://www3.nd.edu/~rwilliam/gologit2/tsfaq.html
<p>
    An oddity of gologit/goprobit models is that it is possible to get
    negative predicted probabilities.  McCullaph &amp; Nelder discuss this in
    Generalized Linear Models, 2nd edition, 1989, p. 155: "The usefulness of
    non-parallel regression models is limited to some extent by the fact that
    the lines must eventually intersect.  Negative fitted values are then
    unavoidable for some values of x, though perhaps not in the observed
    range.  If such intersections occur in a sufficiently remote region of
    the x-space, this flaw in the model need not be serious."
<p>
    This seems to be a fairly rare occurrence, and when it does occur there
    are often other problems with the model, e.g. the model is overly
    complicated and/or there are very small Ns for some categories of the
    dependent variable.  Combining categories or simplifying the model often
    helps. <b>gologit2</b> will give a warning message whenever any in-sample
    predicted probabilities are negative.  If it is just a few cases, it may
    not be worth worrying about, but if there are many cases you may wish to
    modify your model, data, or sample, or use a different statistical
    technique altogether.
<p>
<b><u>Options</u></b>
<p>
    <b>pl</b>, <b>npl</b>, <b>npl()</b>, <b>pl()</b>, <b>autofit</b> and <b>autofit()</b> provide alternative means for
        imposing or relaxing the proportional odds/ parallel lines
        assumption. Only one may be specified at a time.
<p>
        <b>autofit(</b><i>alpha</i><b>)</b> uses an iterative process to identify the partial
            proportional odds model that best fits the data.  <i>alpha</i> is the
            desired significance level for the tests; <i>alpha</i> must be greater
            than 0 and less than 1.  If <b>autofit</b> is specified without
            parameters, the default alpha-value is .05. Note that, the higher
            <i>alpha</i> is, the easier it is to reject the parallel lines
            assumption, and the less parsimonious the model will tend to be.
            This option can take a little while because several models may
            need to be estimated.  The use of <b>autofit</b> is highly recommended
            but the other options provide more control over the final model
            if the user wants it.
<p>
        <b>pl</b> specified without parameters constrains all independent variables
            to meet the proportional odds/ parallel lines assumption.  It
            will produce results that are equivalent to <b>ologit</b>.
<p>
        <b>npl</b> specified without parameters relaxes the proportional odds/
            parallel lines assumption for all explanatory variables.  This is
            the default option and presents results equivalent to the
            original <b>gologit.</b>
<p>
        <b>pl(</b><i>varlist</i><b>)</b> constrains the specified explanatory variables to meet
            the proportional odds/ parallel lines assumption. All other
            variable effects do not need to meet the assumption.  The
            variables specified must be a subset of the explanatory
            variables.
<p>
        <b>npl(</b><i>varlist</i><b>)</b> frees the specified explanatory variables from meeting
            the proportional odds/ parallel lines assumption.  All other
            explanatory variables are constrained to meet the assumption.
            The variables specified must be a subset of the explanatory
            variables.
<p>
    <b>link(</b><i>logit/probit/cloglog/loglog/cauchit</i><b>)</b> specifies the link function to
        be used.  The legal values are <b>link(</b><i>logit</i><b>)</b>, <b>link(</b><i>probit</i><b>)</b>,
        <b>link(</b><i>cloglog</i><b>)</b>, <b>link(</b><i>loglog</i><b>)</b>, and <b>link(</b><i>cauchit</i><b>)</b>, which can be
        abbreviated as <b>link(</b><i>l</i><b>)</b>, <b>link(</b><i>p</i><b>)</b>, <b>link(</b><i>c</i><b>)</b>, <b>link(</b><i>ll</i><b>)</b> and <b>link(</b><i>ca</i><b>)</b>.
        <b>link(</b><i>logit</i><b>)</b> is the default if the option is omitted.
<p>
        The following advice is adapted from Norusis (2005, p. 84):  Probit
        and logit models are reasonable choices when the changes in the
        cumulative probabilities are gradual. If there are abrupt changes,
        other link functions should be used. The log-log link may be a good
        model when the cumulative probabilities increase from 0 fairly slowly
        and then rapidly approach 1. If the opposite is true, namely that the
        cumulative probability for lower scores is high and the approach to 1
        is slow, the complementary log-log link may describe the data. The
        cauchit distribution has tails that are bigger than the normal
        distribution’s, hence the cauchit link may be useful when you have
        more extreme values in either direction.
<p>
        NOTE: Programs differ in the names used for these latter two links.
        Stata's loglog link corresponds to SPSS PLUM's cloglog link; and
        Stata's cloglog link is called nloglog in SPSS.
<p>
        NOTE: Post-estimation commands that work with this program may
        support some links but not others.  Check the program documentation
        to be sure it works correctly with the link you are using. For
        example, post-estimation commands that work with the original <b>gologit</b>
        will often work with this program, but only if you are using the
        logit link.
<p>
    <b>force</b> can be used to force <b>gologit2</b> to issue only warning messages in
        some situations when it would normally give a fatal error:
<p>
        By default, the dependent variable can have a maximum of 20
        categories.  A variable with more categories than that is probably a
        mistaken entry by the user, e.g. a continuous variable has been
        specified rather than an ordinal one.  But, if your dependent
        variable really is ordinal with more than 20 categories, <b>force</b> will
        let <b>gologit2</b> analyze it (although other practical limitations, such
        as small sample sizes within categories, may keep it from coming up
        with a final solution.)
<p>
        Also, variables specified in <b>npl(</b><i>varlist</i><b>)</b> and <b>pl(</b><i>varlist</i><b>)</b> are
        required to be a subset of the explanatory variables.  However,
        prefix commands like <b>sw</b> and <b>nestreg</b> estimate models that may use only
        a subset of the X variables, and those subsets may not include all
        the variable specified in the npl/pl varlists.  Using <b>force</b> will
        allow model estimation to continue in those cases.
<p>
        Obviously, you should only use <b>force</b> when you are confident that you
        are not making a mistake. <b>force</b> does not always work in Stata
        versions before 9.0.
<p>
    <b>lrforce</b> forces Stata to report a Likelihood Ratio Statistic under certain
        conditions when it ordinarily would not.  Some types of constraints
        can make a Likelihood Ratio chi-square test invalid. Hence, to be
        safe, Stata reports a Wald statistic whenever constraints are used.
        But, Likelihood Ratio statistics should be correct for the types of
        constraints imposed by the <b>pl</b> and <b>npl</b> commands.  Note that the
        <b>lrforce</b> option will be ignored when robust standard errors are
        specified either directly or indirectly, e.g.  via use of the <b>robust</b>
        or <b>svy</b> options. Use this option with caution if you specify other
        constraints since these may make a LR chi- square statistic
        inappropriate.
<p>
    <b>store(</b><i>name</i><b>)</b> causes the command <b>estimates store </b><i>name</i> to be executed when
        <b>gologit2</b> finishes.  This is useful for when you wish to estimate a
        series of models and want to save the results. See help estimates.
<p>
    <b>gamma</b> displays an alternative but equivalent parameterization of the
        partial proportional odds model used by Peterson and Harrell (1990)
        and Lall et al (2002). Under this parameterization, there is one Beta
        coefficient and M-2 Gamma coefficients for each explanatory variable,
        where M = the number of categories for Y. The gammas indicate the
        extent to which the proportional odds assumption is violated by the
        variable, i.e. when the gammas do not significantly differ from 0 the
        proportional odds assumption is met. Advantages of this
        parameterization include the fact that it is more parsimonious than
        the default layout.  In addition, by examining the test statistics
        for the Gammas, you can get a feel for which variables meet the
        proportionality assumption and which do not.
<p>
    <b>gamma(</b><i>name</i><b>)</b> causes the gamma estimates to be stored as <i>name</i>, e.g.
        <b>g(</b><i>gamma1</i><b>)</b> would store the gamma estimates under the name <i>gamma1</i>.
        This makes the gamma results easily usable with post- estimation
        table formatting commands like <b>outreg2</b> and <b>estout</b>.  Do NOT try to
        make the gamma results active and then use other post-estimation
        commands, e.g. <b>predict</b> or <b>test</b>.  Such commands either will not work
        or, if they do work, may give incorrect results.  Note that only the
        variances and standard errors of the gamma estimates are correct; all
        the covariances of the estimates are set equal to zero.
<p>
    <b>nolabel</b> causes the equations to be named eq1, eq2, etc. The default is to
        use the first 32 characters of the value labels and/or the values of
        Y as the equation labels.  Note that some characters cannot be used
        in equation names, e.g. the space ( ), the period (.), the dollar
        sign ($), and the colon(:), and will be replaced with the underscore
        (_) character.  Square brackets ([]) and parentheses will be replaced
        with curly brackets ({}). The default behavior works well when the
        value labels are short and descriptive.  It may not work well when
        value labels are very long and/or include characters that have to be
        changed. If the printout looks unattractive and/or you are getting
        strange errors, try changing the value labels of Y or else use the
        <b>nolabel</b> option.
<p>
    <b>mlstart</b> uses an alternative method for computing start values. This
        method is slower but sometimes surer. This option shouldn't be
        necessary but it can be used if the program is having trouble for
        unclear reasons or if you want to confirm that the program is working
        correctly.
<p>
    <b>v1</b> causes <b>gologit2</b> to return results in a format that is consistent with
        <b>gologit 1.0</b>.  This may be useful/ necessary for post-estimation
        commands that were written specifically for <b>gologit</b>.  However,
        post-estimation commands written for <b>gologit2</b> may not work correctly
        if <b>v1</b> is specified.  The <b>v1</b> option only works when using <b>link(logit)</b>.
<p>
    <b>log</b> displays the iteration log. By default it is suppressed.
<p>
    <b>or</b> reports the estimated coefficients transformed to relative odds
        ratios, i.e., exp(b) rather than b; see <b>[R] ologit</b> for a description
        of this concept.  Options <b>rrr</b>, <b>eform</b>, <b>irr</b> and <b>hr</b> produce identical
        results (labeled differently) and can also be used. It is up to the
        user to decide whether the exp(b) transformation makes sense given
        the link function used, e.g.  it probably doesn't make sense when
        using the probit link.
<p>
    <b>constraints(</b><i>clist</i><b>)</b> specifies the linear constraints to be applied during
        estimation.  The default is to perform unconstrained estimation.
        Constraints are defined with the { help constraint} command.
        <b>constraints(1)</b> specifies that the model is to be constrained
        according to constraint 1; <b>constraints(1-4)</b> specifies constraints 1
        through 4; <b>constraints(1-4,8)</b> specifies 1 through 4 and 8.  Keep in
        mind that the <b>pl</b>, <b>npl</b>, and <b>autofit</b> options work by generating
        across-equation constraints, which may affect how any additional
        constraints should be specified.  When using the <b>constraint</b> command,
        it is usually easiest and safest to refer to equations by their
        equation #, e.g. #1, #2, etc.
<p>
    <b>robust</b> specifies that the Huber/White/sandwich estimator of variance is
        to be used in place of the traditional calculation. <b>robust</b> combined
        with <b>cluster()</b> allows observations which are not independent within
        cluster (although they must be independent between clusters).  If you
        specify <b>pweight</b>s, <b>robust</b> is implied.
<p>
    <b>cluster(</b><i>varname</i><b>)</b> specifies that the observations are independent across
        groups (clusters) but not necessarily within groups. <i>varname</i>
        specifies to which group each observation belongs; e.g.,
        <b>cluster(personid)</b> in data with repeated observations on individuals.
        <b>cluster()</b> affects the estimated standard errors and
        variance-covariance matrix of the estimators (VCE), but not the
        estimated coefficients. <b>cluster()</b> can be used with <b>pweight</b>s to
        produce estimates for unstratified cluster-sampled data.
<p>
    <b>level(</b><i>#</i><b>)</b> specifies the confidence level in percent for the confidence
        intervals of the coefficients; see help level.
<p>
    <i>maximize_options</i> control the maximization process; see help maximize.
        You should never have to specify most of these.  However, the
        <b>difficult</b> option can sometimes be useful with models that are running
        very slowly or not converging at all.
<p>
<b><u>Additional Options for Survey Data Estimation</u></b>
<p>
    Stata 9's <b>svy</b> prefix command is not currently supported. The <b>svy</b> option
        accomplishes most of the same things.  <b>svy</b> indicates that <b>gologit2</b> is
        to pick up the <b>svy</b> settings set by <b>svyset</b> and use the robust variance
        estimator.  Thus, this option requires the data to be <b>svyset</b>; see
        help svyset. <b>svy</b> may not be supplied with <i>weight</i>s or the <b>strata()</b>,
        <b>psu()</b>, <b>fpc()</b>, or <b>cluster()</b> options.  When using svy estimation, Use
        of <b>if</b> or <b>in</b> restrictions will not produce correct variance estimates
        for subpopulations in many cases. To compute estimates for
        subpopulations, use the <b>subpop()</b> option.  A typical command using the
        svy option would look something like
<p>
        <b>. gologit2 health female black age age2, autofit svy</b>
<p>
    The following options are available when the <b>svy</b> option has been
        specified. If <b>svy</b> has not been specified, use of these options will
        produce an error.
<p>
        <b>subpop(</b><i>subpop_spec</i><b>)</b> specifies that estimates be computed for the
            single subpopulation identified in <i>subpop_spec</i>.  <i>subpop_spec</i> is
<p>
                [<i>varname</i>] [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [, <b><u>srs</u></b><b>subpop</b> ]
<p>
            Thus the subpopulation is defined by the observations for which
            <i>varname</i>!=0 that also meet the <b>if</b> and <b>in</b> conditions.  Typically,
            <i>varname</i>=1 defines the subpopulation and <i>varname</i>=0 indicates
            observations not belonging to the subpopulation.  For
            observations whose subpopulation status is uncertain, <i>varname</i>
            should be set to missing.
<p>
            <b>srssubpop</b> requests that deff and deft be computed using an
                estimate of simple-random-sampling variance for sampling
                within a subpopulation.  If <b>srssubpop</b> is not specified, deff
                and deft are computed using an estimate of
                simple-random-sampling variance for sampling from the entire
                population.  Typically, <b>srssubpop</b> would be given when
                computing subpopulation estimates by strata or by groups of
                strata.
<p>
        <b>nosvyadjust</b> specifies that the model Wald test be carried out as W/k
            distributed F(k,d), where W is the Wald test statistic, k is the
            number of terms in the model excluding the constant, d = total
            number of sampled PSUs minus total number of strata, and F(k,d)
            is an F distribution.  By default, an adjusted Wald test is
            conducted:  (d-k+1)W/(kd) distributed F(k,d-k+1).  Use of the
            <b>nosvyadjust</b> option is not recommended.
<p>
        <b>prob</b> requests that the t statistic and p-value be displayed.  The
            degrees of freedom for the t are d = total number of sampled PSUs
            minus the total number of strata (regardless of the number of
            terms in the model).  If no display options are specified then,
            by default, the t statistic and p-value are displayed.
<p>
        <b>ci</b> requests that confidence intervals be displayed.  If no display
            options are specified then, by default, confidence intervals are
            displayed.
<p>
        <b>deff</b> requests that the design-effect measure deff be displayed.
<p>
        <b>deft</b> requests that the design-effect measure deft be displayed.
<p>
        <b>meff</b> requests that the meff measure of misspecification effects be
            displayed.  This option must be specified at the time of the
            initial estimation.
<p>
        <b>meft</b> requests that the meft measure of misspecification effects be
            displayed.  This option must be specified at the time of the
            initial estimation.
<p>
<b><u>Options for predict</u></b>
<p>
    <b>p</b>, the default, calculates predicted probabilities.
<p>
        If you do not also specify the <b>outcome()</b> option, you must specify k
        new variables.  For instance, say you fitted your model by typing
        "<b>gologit2 insure age male</b>" and that <b>insure</b> takes on three values.
        Then you could type "<b>predict p1 p2 p3, p</b>" to obtain all three
        predicted probabilities.
<p>
        If you also specify the <b>outcome()</b> option, then you specify one new
        variable.  Say that <b>insure</b> took on values 1, 2, and 3.  Then typing
        "<b>predict p1, p outcome(1)</b>" would produce the same <b>p1</b> as above,
        "<b>predict p2, p outcome(2)</b>" the same <b>p2</b> as above, etc. If <b>insure</b> took
        on values 7, 22, and 93, you would specify <b>outcome(7)</b>, <b>outcome(22)</b>,
        and <b>outcome(93)</b>.  Alternatively, you could specify the outcomes by
        referring to the equation number (<b>outcome(#1)</b>, <b>outcome(#2)</b>, and
        <b>outcome(#3)</b>, or, if the variable values are labeled, you can do
        something like <b>outcome(low)</b>, <b>outcome(medium)</b>, and <b>outcome(high)</b>.
<p>
    If you do not specify <b>outcome()</b>, <b>p</b> (with one new variable specified), <b>xb</b>,
        and <b>stdp</b> assume <b>outcome(#1)</b>.  You must specify <b>outcome()</b> with the
        <b>stddp</b> option.
<p>
    <b>xb</b> calculates the linear prediction.  You should also specify the
        <b>outcome()</b> option. Default is <b>outcome(#1)</b>.
<p>
    <b>outcome()</b> specifies for which outcome the statistic is to be calculated.
        <b>equation()</b> is a synonym for <b>outcome()</b>: it does not matter which you
        use.  <b>outcome()</b> and <b>equation()</b> can be specified using (1) <b>#1</b>, <b>#2</b>,
        ..., with <b>#1</b> meaning the first category of the dependent variable, <b>#2</b>
        the second category, etc.; (2) values of the dependent variable; or
        (3) the value labels (if any) of the dependent variable.
<p>
    <b>stdp</b> calculates the standard error of the linear prediction.  You should
        also specify the <b>outcome()</b> option. Default is <b>outcome(#1)</b>.
<p>
    <b>stddp</b> calculates the standard error of the difference in two linear
        predictions.  You must specify option <b>outcome()</b> and in this case you
        specify the two particular outcomes of interest inside the
        parentheses; for example, "<b>predict sed, stddp outcome(1,3)</b>".
<p>
<b><u>Some cautions about using </u></b><b><u>gologit2</u></b><b><u> with the Stata 9 prefix commands</u></b>
<p>
    In general, <b>gologit2</b> seems to work well with Stata 9's prefix commands,
    e.g. <b>xi</b>, <b>nestreg</b>.  There are, however, some combinations of prefix
    commands and <b>gologit2</b> options that can be problematic that users should
    be aware of.
<p>
    The <b>sw</b> and <b>nestreg</b> prefix commands should work fine with the <b>pl</b> and <b>npl</b>
    options. However, they may produce error messages and/or unexpected
    results when combined with the <b>autofit</b>, <b>autofit(</b><i>alpha</i><b>)</b>, <b>npl(</b><i>varlist</i><b>)</b> or
    <b>pl(</b><i>varlist</i><b>)</b> options.  For example, the submodels estimated by <b>nestreg</b> may
    not include all the variables specified in <b>pl(</b><i>varlist</i><b>)</b>, resulting in a
    fatal error message. You can override this error by using the <b>force</b>
    option.  <b>sw</b> and <b>autofit</b> would be an especially questionable combination
    since both stepwise selection of variables and stepwise selection of
    constraints would be going on.
<p>
    Other than the above, <b>gologit2</b> will hopefully work fine in most common
    situations where prefix commands are used.  However, there are many
    possible esoteric combinations of prefix commands and <b>gologit2</b> options
    and <b>gologit2</b> is not guaranteed to be problem-free with all of them.
<p>
<p>
<b><u>Examples</u></b>
<p>
    <i>Example 1.  Proportional Odds/ Parallel Lines Assumption Violated.</i>  Long
    and Freese (2003) present data from the 1977/ 1989 General Social Survey.
    Respondents are asked to evaluate the following statement: "A working
    mother can establish just as warm and secure a relationship with her
    child as a mother who does not work." Responses were coded as 1 =
    Strongly Disagree (1SD), 2 = Disagree (2D), 3 = Agree (3A), and 4 =
    Strongly Agree (4SA).  We can do a global test of the proportional odds
    assumption by estimating a model in which no variables are constrained to
    meet the assumption and contrasting it with a model in which all are so
    constrained (the latter is equivalent to the <b>ologit</b> model).
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. gologit2 warm yr89 male white age ed prst, store(unconstrained)</b>
        <b>. gologit2 warm yr89 male white age ed prst, pl lrf</b>
            <b>store(constrained)</b>
        <b>. lrtest constrained unconstrained</b>
<p>
    The LR chi-square from the above is 49.20.  These data fail the global
    test.
<p>
    However, we can now use the <b>autofit</b> option to see whether a partial
    proportional odds model can fit the data.  In a partial proportional odds
    model, some variables meet the proportional odds assumption while others
    do not.
<p>
        <b>. gologit2 warm yr89 male white age ed prst, autofit</b>
<p>
    The results show that 4 of the 6 variables (white, age, ed, prst) meet
    the parallel lines assumption.  Only yr89 and male do not.  This model is
    less restrictive than a model estimated by <b>ologit</b> would be (whose
    assumptions are violated in this case) but much more parsimonious than a
    non-ordinal alternative such as <b>mlogit</b>.
<p>
    <i>Example 2: Alternative parameterization using the </i><i>gamma</i><i> option.</i>  Peterson
    &amp; Harrell (1990) and Lall et al (2002) present an alternative
    parameterization of the partial proportional odds model.  In this
    parameterization, gamma coefficients represent deviations from
    proportionality.  When the gammas of an explanatory variable do not
    significantly differ from 0, the parallel lines assumption for that
    variable is met. Using the <b>autofit</b> and <b>gamma</b> options, we can (a) confirm
    that Lall came up with the correct partial proportional odds model, and
    (b) replicate the results from his Table 5.
<p>
        <b>. use http://www3.nd.edu/~rwilliam/gologit2/lall.dta</b>
        <b>. gologit2 hstatus heart smoke, lrf gamma autofit</b>
<p>
    <i>Example 3: Survey data estimation.</i>  By using the <b>svy</b> option, we can
    estimate models with data that have been svyset.
<p>
        <b>. use http://www.stata-press.com/data/r8/nhanes2f.dta</b>
        <b>. gologit2 health female black age age2, svy autofit</b>
        <b>. gologit2 health black age age2, svy autofit subpop(female)</b>
<p>
    <i>Example 4.  </i><i>gologit 1.0</i><i> compatibility.</i> Some post-estimation commands -
    specifically, the <b>spost</b> routines of Long and Freese - currently work with
    the original <b>gologit</b> but not <b>gologit2</b>. That should change in the future.
    For now, you can use the <b>v1</b> parameter to make the stored results from
    <b>gologit2</b> compatible with <b>gologit 1.0</b>. (Note, however, that this may make
    the results non-compatible with post-estimation routines written for
    <b>gologit2</b>; also, you have to be using the dafualt logit link.) Using the
    working mother's data again,
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. * Use the v1 option to save internally stored results in gologit</b>
            <b>1.0 format</b>
        <b>. quietly gologit2 warm yr89 male white age ed prst, pl(yr89 male)</b>
            <b>lrf v1</b>
        <b>. * Use one of Long &amp; Freese's spost routines</b>
        <b>. prvalue, x(male=0 yr89=1 age=30) rest(mean)</b>
<p>
    <i>Example 5.  The </i><i>predict</i><i> command.</i>  In addition to the standard options
    (<b>xb, stdp, stddp</b>) the <b>predict</b> command supports the <b>pr</b> option (abbreviated
    <b>p</b>) for predicted probabilities; <b>pr</b> is the default option if nothing else
    is specified.  For example,
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. quietly gologit2 warm yr89 male white age ed prst, pl(yr89 male)</b>
            <b>lrf</b>
        <b>. predict p1 p2 p3 p4</b>
<p>
    <i>Example 6.  Constrained logistic regression.</i>  The models estimated by
    Stata's <b>logit</b> and <b>ologit</b> commands are special cases of the gologit model;
    but neither of these commands currently supports the use of linear
    constraints, such as two variables having equal effects.  <b>gologit2</b> can be
    used for this purpose.  For example,
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. recode warm (1 2 = 0)(3 4 = 1), gen(agree)</b>
        <b>. * Constrain the effects of male and white to be equal</b>
        <b>. constraint 1 male = white</b>
        <b>. gologit2 agree yr89 male white age ed prst, lrf store(constrained)</b>
            <b>c(1)</b>
<p>
    <i>Example 7.  Other link functions.</i>  By default, and as its name implies.
    <b>gologit2</b> uses the logit link. If you prefer, however, you can specify
    probit, complementary log log, log log, or cauchit links.  For example,
    to estimate a goprobit model,
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. gologit2 warm yr89 male white age ed prst, link(p)</b>
<p>
    <i>Example 8.  Prefix commands.</i>  If you are using Stata 9 or higher,
    <b>gologit2</b> supports many of the prefix commands. For example,
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. sw, pe(.05): gologit2 warm yr89 male</b>
        <b>. xi: gologit2 warm yr89 i.male</b>
        <b>. nestreg: gologit2 warm (yr89 male white age) (ed prst)</b>
<p>
    <i>Example 9.  Post-estimation table formatting commands.</i>  Here is an
    example of how you could use <b>outreg2</b> to format the results from multiple
    models.  In this example I use both the regular and the gamma results but
    most people would probably choose one or the other.  The store option
    stores the regular results while the g option stores the results from the
    gamma parameterization.
<p>
        <b>. use http://www.indiana.edu/~jslsoc/stata/spex_data/ordwarm2.dta,</b>
            <b>clear</b>
        <b>. * Unconstrained model</b>
        <b>. gologit2 warm yr89 male white age ed prst, npl lrf store(gologit)</b>
            <b>g(gologit_g)</b>
        <b>. * Autofit model</b>
        <b>. gologit2 warm yr89 male white age ed prst, autofit lrf</b>
            <b>store(gologit2) g(gologit2_g)</b>
        <b>. * Use outreg2 to output the regular results in a single table</b>
        <b>. outreg2 [gologit gologit2] using regular, replace onecol long nor2</b>
            <b>seeout</b>
        <b>. * Use outreg2 to output the gamma results in a single table</b>
        <b>. outreg2 [gologit_g gologit2_g] using gamma, replace onecol long</b>
            <b>nor2 seeout</b>
<p>
<p>
<b><u>Author</u></b>
<p>
     Richard Williams
     Notre Dame Department of Sociology
     Richard.A.Williams.5@ND.Edu
     http://www3.nd.edu/~rwilliam/gologit2/
<p>
<p>
<b><u>Acknowledgements</u></b>
<p>
     Vincent Kang Fu of the Utah Department of Sociology wrote <b>gologit 1.0</b> and
     graciously allowed Richard Williams to incorporate parts of its source
     code and documentation in <b>gologit2</b>.
<p>
     The documentation for Stata 8.2's <b>mlogit</b> command and the program <b>mlogit_p</b>
     were major aids in developing the <b>gologit2</b> documentation and in adding
     support for the <b>predict</b> command.  Much of the code is adapted from <i>Maximum</i>
     <i>Likelihood Estimation with Stata, Second Edition</i>, by William Gould,
     Jeffrey Pitblado and William Sribney.
<p>
     Sarah Mustillo, Dan Powers, J. Scott Long, Nick Cox, Kit Baum and Joseph
     Hilbe provided stimulating and helpful comments. Jeff Pitblado was
     extremely helpful in updating the program to use Stata 9's new features.
<p>
<b><u>References</u></b>
<p>
     Fu, Vincent. 1998. "Estimating Generalized Ordered Logit Models." Stata
     Technical Bulletin 8:160-164.
<p>
     Lall, R., S.J. Walters, K. Morgan, and MRC CFAS Co-operative Institute of
     Public Health.  2002. "A Review of Ordinal Regression Models Applied on
     Health-Related Quality of Life Assessments." Statistical Methods in
     Medical Research 11:49-67.
<p>
     Long, J. Scott and Jeremy Freese.  2006.  "Regression Models for
     Categorical Dependent Variables Using Stata, 2nd Edition." College
     Station, Texas: Stata Press.
<p>
     Norusis, Marija.  2005.  "SPSS 13.0 Advanced Statistical Procedures
     Companion." Upper Saddle River, New Jersey: Prentice Hall.
<p>
     Peterson, Bercedis and Frank E. Harrell Jr.  1990.  "Partial Proportional
     Odds Models for Ordinal Response Variables." Applied Statistics
     39(2):205-217.
<p>
<b><u>Suggested citation if using </u></b><b><u>gologit2</u></b><b><u> in published work </u></b>
<p>
     <b>gologit2</b> is not an official Stata command. It is a free contribution to
     the research community, like a paper. Please cite it as such.
<p>
     Williams, Richard.  2006.  "Generalized Ordered Logit/ Partial
     Proportional Odds Models for Ordinal Dependent Variables." The Stata
     Journal 6(1):58-82.
<p>
     The above document provides more detailed explanations and examples and is
     recommended reading.  A pre-publication version that includes information
     on updates to the program since the article was published is available at 
     http://www3.nd.edu/~rwilliam/gologit2/gologit2.pdf.  The published article
     is available for free at 
     http://www.stata-journal.com/article.html?article=st0097.
<p>
<p>
<b><u>Also see</u></b>
<p>
    Online:  help for estcom, postest, constraint, ologit, svy, svyologit; if
</pre>