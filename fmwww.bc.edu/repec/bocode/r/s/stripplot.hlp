{smcl}
{* 5july2004/28nov2005/24jun2007/26july2007/26aug2007/8nov2007/30nov2007/27feb2008/7nov2008/22apr2009/2may2009/15may2009/30nov2009/8dec2009/4feb2010/19feb2010/15mar2010/26apr2010/21may2010/2dec2010/23mar2011/6apr2011/30aug2011}{...}
{hline}
help for {hi:stripplot}
{hline}

{title:Strip plots: oneway dot plots}

{p 8 17 2}
{cmd:stripplot}
{it:varlist} 
[{cmd:if} {it:exp}] 
[{cmd:in} {it:range}] 
[ 
{cmd:,}
{cmdab:vert:ical} 
{cmdab:w:idth(}{it:#}{cmd:)}
{c -(}
{cmd:floor}
{c |}
{cmdab:ceil:ing} 
{c )-}
{cmdab:st:ack}
{cmdab:h:eight(}{it:#}{cmd:)}
{c -(} 
{cmdab:ce:ntre}
{c |} 
{cmdab:ce:nter}
{c )-} 
{cmdab:sep:arate(}{it:varname}{cmd:)} 
{c -(} 
{cmd:bar}[{cmd:(}{it:bar_options}{cmd:)}] 
{c |} 
{cmd:box}[{cmd:(}{it:box_options}{cmd:)}] 
{c )-} 
{cmd:iqr}[{cmd:(}{it:#}{cmd:)}] 
{cmdab:pct:ile(}{it:#}{cmd:)} 
{cmdab:wh:iskers(}{it:rspike_options}{cmd:)} 
{cmd:boffset(}{it:#}{cmd:)}
{cmd:variablelabels} 
{cmd:plot(}{it:plot}{cmd:)}
{cmd:addplot(}{it:plot}{cmd:)}
{it:graph_options} ]

{p 8 17 2}
{cmd:stripplot}
{it:varname} 
[{cmd:if} {it:exp}] 
[{cmd:in} {it:range}] 
[ 
{cmd:,}
{cmdab:vert:ical} 
{cmdab:w:idth(}{it:#}{cmd:)} 
{c -(}
{cmd:floor}
{c |}
{cmdab:ceil:ing} 
{c )-}
{cmdab:st:ack}
{cmdab:h:eight(}{it:#}{cmd:)}
{c -(} 
{cmdab:ce:ntre}
{c |} 
{cmdab:ce:nter}
{c )-}
{cmdab:o:ver(}{it:groupvar}{cmd:)}
{cmdab:sep:arate(}{it:varname}{cmd:)} 
{c -(} 
{cmd:bar}[{cmd:(}{it:bar_options}{cmd:)}] 
{c |} 
{cmd:box}[{cmd:(}{it:box_options}{cmd:)}] 
{c )-} 
{cmd:iqr}[{cmd:(}{it:#}{cmd:)}] 
{cmdab:pct:ile(}{it:#}{cmd:)} 
{cmdab:wh:iskers(}{it:rspike_options}{cmd:)} 
{cmd:boffset(}{it:#}{cmd:)}
{cmd:plot(}{it:plot}{cmd:)} 
{cmd:addplot(}{it:plot}{cmd:)} 
{it:graph_options} 
]


{title:Description}

{p 4 4 2}{cmd:stripplot} plots data as a series of marks against a
single magnitude axis. By default this axis is horizontal. With the
option {cmd:vertical} it is vertical. Optionally, data points may be
jittered or stacked into histogram- or {cmd:dotplot}-like displays, and
either bars showing means and confidence intervals, or boxes showing
medians and quartiles, may be added. 


{title:Remarks}

{it:General and bibliographic remarks}

{p 4 4 2}There is not a sharp distinction in the literature or in
software implementations between {it:dot plots} and {it:strip plots}.
Commonly, but with many exceptions, a dot plot is drawn as a
pointillist analogue of a histogram. Sometimes, dot plot is used as the
name when data points are plotted in a line, or at most a narrow strip,
against a magnitude axis. Strip plot implementations, as here, usually
allow stacking options, so that dot plots may be drawn as one choice. 

{p 4 4 2}Such plots under these and yet other names go back at least as
far as Langren (1644): see Tufte (1997, p.15) and in much more detail 
Friendly {it:et al.} (2010). 
Sasieni and Royston (1996) and Wilkinson (1999) give general discussions 
and several further references of historical interest. 
Monkhouse and Wilkinson (1952) used the term {it:dispersion diagrams}. 
Pearson (1956) gives several examples.  
Dickinson (1963) used the term {it:dispersal graphs}. 
Box {it:et al.} (1978) used the term {it:dot diagrams}. 
Chambers {it:et al.} (1983), Becker {it: et al.} (1988) and
Cleveland (1994) used the term {it:one-dimensional scatter plots}, 
as did Lee and Tu (1997) and Reimann {it: et al.} (2008). 
Ryan {it:et al.} (1985) discuss their Minitab implementation as {it:dotplots}. 
Cleveland (1985) used the term {it:point graphs}. 
The term {it:oneway plots} appears to have been introduced by Computing 
Resource Center (1985). 
Feinstein (2002, p.67) uses the term {it:one-way graphs}.  
The term {it:strip plots} (or {it:strip charts}) (e.g. Dalgaard 2002; 
Venables and Ripley 2002; Robbins 2005; Faraway 2005; 
Maindonald and Braun 2007) appears traceable to work
by J.W. and P.A. Tukey (1990).  
The term {it:dit plots} appears in Ellison (1993, 2001). 
The term {it:linear plots} appears in Hay (1996) and that of {it:line plots} 
in Klemel{c a:} (2009) and Schenemeyer and Drew (2011). 

{p 4 4 2}Tufte (1974), Berry (1996), Cobb (1998), Griffiths {it:et al.}
(1998), Bland (2000), Wild and Seber (2000), Robbins (2005), Young {it:et al.} (2006),  
Morgenthaler (2007), Warton (2008) and Keen (2010) show many interesting examples of strip plots. 

{p 4 4 2}Hybrid dot-box plots were used by Monkhouse and Wilkinson
(1952), Gregory (1963), Matthews (1981), Wilkinson (1992, 2005),  Wild
and Seber (2000), Ellison (2001), Quinn and Keough (2002) and Young
{it:et al.} (2006). Box plots in widely current forms are best known
through the work of Tukey (1972, 1977). Similar ideas go back much
further.  Cox (2009) gives various references. Bibby (1986, pp.56, 59)
gave even earlier references to their use by A.L. Bowley in his lectures
about 1897 and to his recommendation (Bowley, 1910, p.62; 1952, p.73) to use minimum
and maximum and 10, 25, 50, 75 and 90% points as a basis for graphical
summary. Keen (2010) also discusses several variants of box plots. 

{p 4 4 2}Dot charts (also sometimes called dot plots) in the sense 
of Cleveland (1984, 1994), as implemented in {help graph dot}, are quite
distinct. 

{p 4 4 2}See also Cox (2004) for a general discussion of graphing distributions
in Stata; Cox (2007) for an implementation of stem-and-leaf plots that bears
some resemblance to what is possible with {cmd:stripplot}; and Cox (2009) on 
how to draw box plots using {help twoway}. 

{it:A note for experimental design people} 

{p 4 4 2}There is no connection between {cmd:stripplot} and the 
strip plots discussed in design of experiments. 

{it:A comparison between} {cmd:stripplot}{it:,} {cmd:gr7, oneway} {it:and} {cmd:dotplot}

{p 4 8 2}{cmd:stripplot} may have either horizontal or vertical magnitude
axis.  With {cmd:gr7, oneway} the magnitude axis is always horizontal.  With
{cmd:dotplot} the magnitude axis is always vertical. 

{p 4 8 2}{cmd:stripplot} and {cmd:dotplot} put descriptive text on the axes. 
{cmd:gr7, oneway} puts descriptive text under each line of marks.

{p 4 8 2}{cmd:stripplot} and {cmd:dotplot}
allow any marker symbol to be used for the data marks. 
{cmd:gr7, oneway} always shows data marks as short vertical bars,
unless {cmd:jitter()} is specified. 

{p 4 8 2}{cmd:stripplot} and {cmd:dotplot} interpret
{cmd:jitter()} in the same way as does {cmd:scatter}. 
{cmd:gr7, oneway} interprets {cmd:jitter()} as replacing short
vertical bars by sets of dots.

{p 4 8 2}{cmd:stripplot} and {cmd:dotplot} allow tuning of {cmd:xlabel()}.
{cmd:gr7, oneway} does not allow such tuning: the minimum and maximum are
always shown.  Similarly, {cmd:stripplot} and {cmd:dotplot} allow the use of
{cmd:xline()} and {cmd:yline()}. 

{p 4 8 2}
{cmd:dotplot} uses only one colour in the body of the graph. 
{cmd:stripplot} allows several colours in the body of the graph with its 
{cmd:separate()} option. 
{cmd:gr7, oneway} uses several colours with several variables. 

{p 4 8 2}There is no equivalent with {cmd:stripplot} or {cmd:dotplot} to
{cmd:gr7, oneway rescale}, which  stretches each set of data marks to extend
over the whole horizontal range of the graph.  Naturally, users could 
standardise a bunch of variables in some way before calling {cmd:stripplot} 
or {cmd:dotplot}. 

{p 4 8 2}{cmd:stripplot} and {cmd:dotplot} with option
{cmd:over(}{it:groupvar}{cmd:)} do not require data to be sorted by 
{it:groupvar}. The equivalent {cmd:gr7, oneway by(}{it:groupvar}{cmd:)} 
does require this. 

{p 4 8 2}{cmd:stripplot} allows the option 
{cmd:by(}{it:byvar}{cmd:)}, producing separate graph panels according 
to the groups of {it:byvar}.
{cmd:dotplot} does not allow the option {cmd:by()}. 
{cmd:gr7, oneway} allows the option 
{cmd:by(}{it:byvar}{cmd:)}, producing separate displays within a 
single panel. It does not take the values of {it:byvar}
literally: displays for values 1, 2 and 4 will appear equally spaced. 

{p 4 8 2}{cmd:stripplot} with the {cmd:stack} option produces a variant on
{cmd:dotplot}. There is by default no binning of data: compare 
{cmd:dotplot, nogroup}. Binning may be accomplished with the {cmd:width()} 
option so that
classes are defined by {cmd:round(}{it:varname}{cmd:/}{it:width}) 
or optionally by 
{it:width} {cmd:* floor(}{it:varname/width}{cmd:)} or 
{it:width} {cmd:* ceil(}{it:varname/width}{cmd:)}:
contrast {cmd:dotplot, ny()}. Conversely, stacking may in 
effect be suppressed in {cmd:dotplot} by setting {cmd:nx()} sufficiently 
large. 

{p 4 8 2}{cmd:stripplot} has options for showing bars as confidence intervals 
and boxes showing medians and quartiles. 
{cmd:gr7, oneway box} shows Tukey-style box plots.
{cmd:dotplot} allows the showing of mean +/- SD or median and quartiles by
horizontal lines. 


{title:Options}

{p 4 8 2}{cmd:vertical} specifies that the magnitude axis should be
vertical. 

{p 4 8 2}{cmd:width(}{it:#}{cmd:)} specifies that values are to be rounded in
classes of specified width. Classes are defined by default by 
{cmd:round(}{it:varname}{cmd:,}{it:width}{cmd:)}. See also the 
{cmd:floor} and {cmd:ceiling} options just below. 

{p 8 8 2}{cmd:floor} or {cmd:ceiling} in conjunction with {cmd:width()} 
specifies rounding by {it:width} {cmd:* floor(}{it:varname/width}{cmd:)}
or {it:width} {cmd:* ceil(}{it:varname/width}{cmd:)} respectively. Only 
one may be specified. (These options are included to give some users the 
minute control they may desire, but if either option produces a marked
difference in your plot, you may be rounding too much.)  

{p 4 8 2}{cmd:stack} specifies that data points with identical values are to
be stacked, as in {cmd:dotplot}, except that by default there is no binning of
data.

{p 8 8 2}{cmd:height(}{it:#}{cmd:)} controls the amount of graph space
taken up by stacked data points under the {cmd:stack} option above. The default
is 0.8.  This option will not by itself change the appearance of a plot for a
single variable. Note that the height may need to be much smaller
or much larger than 1 with {cmd:over()}, given that the latter takes values
literally. For example, if your classes are 0(45)360, 36 might be 
a suitable height. 

{p 8 8 2}{cmd:centre} or {cmd:center} centres or centers markers
for each variable or group on a hidden line.

{p 4 8 2}{cmd:over(}{it:groupvar}{cmd:)} specifies that values of {it:varname} are
to be shown separately by groups defined by {it:groupvar}. This option may only be
specified with a single variable.  If {cmd:stack} is also specified, then note
that distinct values of any numeric {it:groupvar} are assumed to differ by at
least 1. Tuning {cmd:height()} or the prior use of {cmd:egen, group() label} will
fix any problems. See help on {help egen} if desired. 

{p 8 8 2}Note that {cmd:by()} is also available as an alternative or complement
to {cmd:over()}. See the examples for detail on how {cmd:over()} and {cmd:by()}
could be used to show data subdivided by a cross-combination of categories. 

{p 4 8 2}{cmd:separate()} specifies that data points be shown separately 
according to the distinct classes of the variable specified. Commonly, but 
not necessarily, this option will be specified together with {cmd:stack}. 
Note that this option has no effect on any error bar or box plot calculations. 

{p 4 8 2}{cmd:bar} specifies that bars be added showing means and 
confidence intervals. Bar information is calculated using {cmd:ci}. 
{cmd:bar(}{it:bar_options}{cmd:)} may be used to specify details 
of the means and confidence intervals. {it:bar_options} are 

{p 8 8 2}Various options of {help ci}: 
{cmdab:l:evel()}, 
{cmdab:p:oisson}, 
{cmdab:b:inomial},   
{cmdab:exa:ct}, 
{cmdab:wa:ld}, 
{cmdab:a:gresti}, 
{cmdab:w:ilson}, 
{cmdab:j:effreys} and 
{cmdab:e:xposure()}. For example, {cmd:bar(binomial jeffreys)} specifies
those options of {cmd:ci}. 

{p 8 8 2}{cmd:mean(}{it:scatter_options}{cmd:)} may be used to control
the rendering of the symbol for the mean. 
For example, {cmd:bar(mean(mcolor(red) ms(sh)))} specifies the use
of red small hollow squares. 

{p 8 8 2}Options of {help twoway rcap} may be used to control the 
appearance of the bar. For example, {cmd:bar(lcolor(red))} specifies
red as the bar colour. 

{p 8 8 2}These kinds of options may be combined. 

{p 4 8 2}{cmd:box} specifies that boxes be added showing medians and quartiles.
Box information is calculated using {cmd:egen, median()} and 
{cmd:egen, pctile()}.  {cmd:box(}{it:box_options}{cmd:)} may be used to specify options of
{help twoway rbar} to control the appearance of the box. For example,
{cmd:box(bfcolor(eltgreen))} specifies {cmd:eltgreen} as the box fill colour.
The defaults are {cmd:bcolor(none) barwidth(0.4)}. Note that the length of each box 
is the interquartile range or IQR. 

{p 8 8 2}{cmd:iqr}[{cmd:(}{it:#}{cmd:)}] specifies that spikes are to be added to boxes 
that extend as far as the largest or smallest value within {it:#} IQR of the upper or 
lower quartile. Plain {cmd:iqr} without argument yields a default of 1.5 for {it:#}. 

{p 8 8 2}{cmd:pctile(}{it:#}{cmd:)} specifies that spikes are to be added to boxes that 
extend as far as the {it:#} and 100 - {it:#} percentiles. 

{p 8 8 2}{cmd:whiskers()} specifies options of {help twoway rspike} that may be used to 
modify the appearance of spikes added to boxes. 

{p 8 8 2}{cmd:iqr}, {cmd:iqr()}, {cmd:pctile()} and {cmd:whiskers()} have no effect without 
{cmd:box} or {cmd:box()}. {cmd:iqr} or {cmd:iqr()} may not be combined with {cmd:pctile()}. 

{p 4 8 2}{cmd:bar}[{cmd:()}] and {cmd:box}[{cmd:()}] may not be combined. 

{p 4 8 2}{cmd:boffset()} may be used to control the position of bars or boxes. 
By default, bars are positioned 0.2 unit to the left of (or below) the base line 
for strips, and boxes are positioned under the the base line for strips. 
Negative arguments specify positions to the left or below of the base line and 
positive arguments specify positions to the right or above. 

{p 4 8 2}{cmd:variablelabels} specifies that multiple variables be labelled by 
their variable labels. The default is to use variable names. 

{p 4 8 2}{cmd:plot(}{it:plot}{cmd:)} provides a way to add other plots to the 
generated graph; see help {help plot_option} (Stata 8 only). 

{p 4 8 2}{cmd:addplot(}{it:plot}{cmd:)} provides a way to add other plots to the
generated graph; see help {help addplot_option} (Stata 9 up). 

{p 4 8 2}{it:graph_options} are options of {help scatter}, including 
{cmd:by()}, on which see {help by_option}. Note that {cmd:by(, total)} is not 
supported with bars or boxes. {cmd:jitter()} is often helpful. 


{title:Examples} 

{p 4 8 2}(Stata's auto data){p_end}
{p 4 8 2}{cmd:. sysuse auto, clear}{p_end}
{p 4 8 2}{cmd:. stripplot mpg}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, aspect(0.05)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) by(foreign)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) vertical}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) vertical stack}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) vertical stack h(0.4)}

{p 4 8 2}{cmd:. gen pipe = "|"}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, ms(none) mlabpos(0) mlabel(pipe) mlabsize(*2) stack}{p_end}
{p 4 8 2}{cmd:. stripplot price, over(rep78) ms(none) mla(pipe) mlabpos(0)}{p_end}
{p 4 8 2}{cmd:. stripplot price, over(rep78) w(200) stack h(0.4)}

{p 4 8 2}(5 here is empirical: adjust for your variable){p_end}
{p 4 8 2}{cmd:. gen price1 = price - 5}{p_end}
{p 4 8 2}{cmd:. gen price2 = price + 5}{p_end}
{p 4 8 2}{cmd:. stripplot price, over(rep78) box ms(none) addplot(rbar price1 price2 rep78, horizontal barw(0.2) bcolor(gs6))}

{p 4 8 2}{cmd:. stripplot mpg, over(rep78) stack h(0.5) bar(lcolor(red))}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box(bfcolor(eltgreen)) boffset(-0.3)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box boffset(-0.3)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box(bfcolor(eltgreen) barw(0.2)) boffset(-0.2) stack h(0.5)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box(bfcolor(black) blcolor(white) barw(0.2)) boffset(-0.2) stack h(0.5)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box(bfcolor(black) blcolor(white) barw(0.2)) iqr boffset(-0.2) stack h(0.5)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) box(bfcolor(black) blcolor(white) barw(0.2)) pctile(10) whiskers(recast(rbar) bcolor(black) barw(0.02)) boffset(-0.2) stack h(0.5)}

{p 4 8 2}{cmd:. gen digit = mod(mpg, 10)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, stack vertical mla(digit) mlabpos(0) ms(i) over(foreign) height(0.2) yla(, ang(h)) xla(, noticks)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, stack vertical mla(digit) mlabpos(0) ms(i) by(foreign) yla(, ang(h))}

{p 4 8 2}{cmd:. stripplot mpg, over(rep78) separate(foreign) stack}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, by(rep78) separate(foreign) stack}

{p 4 8 2}{cmd:. gen rep78_1 = rep78 - 0.1}{p_end}
{p 4 8 2}{cmd:. egen mean = mean(mpg), by(foreign rep78)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78) by(foreign, compact) addplot(scatter rep78_1 mean, ms(T)) stack}

{p 4 8 2}{cmd:. clonevar rep78_2 = rep78}{p_end}
{p 4 8 2}{cmd:. replace rep78_2 = cond(foreign, rep78 + 0.15, rep78 - 0.15)}{p_end}
{p 4 8 2}{cmd:. stripplot mpg, over(rep78_2) separate(foreign) yla(1/5) jitter(1 1)}

{p 4 8 2}(Challenger shuttle O-ring damage){p_end}
{p 4 8 2}{cmd:. logit damage temperature}{p_end}
{p 4 8 2}{cmd:. predict pre}{p_end}
{p 4 8 2}{cmd:. stripplot damage, over(temperature) stack ms(sh) height(0.4) addplot(mspline pre temperature, bands(20))}

{p 4 8 2}(Stata's blood pressure data){p_end}
{p 4 8 2}{cmd:. sysuse bplong, clear}{p_end}
{p 4 8 2}{cmd:. egen group = group(age sex), label}{p_end}
{p 4 8 2}{cmd:. stripplot bp*, bar over(when) by(group, compact col(1) note("")) ysc(reverse) subtitle(, pos(9) ring(1) nobexpand bcolor(none) placement(e)) ytitle("") xtitle(Blood pressure (mm Hg))}{p_end} 


{title:Acknowledgments}

{p 4 4 2}
Philip Ender helpfully identified a bug. 
William Dupont offered encouragement. 
Kit Baum nudged me into implementing {cmd:separate()}. 
Maarten Buis made a useful suggestion about this help. 
Ron{c a'}n Conroy suggested adding whiskers. He also found two bugs. 
Marc Kaulisch asked a question which led to more emphasis on the use of {cmd:by()} and the blood pressure example.
David Airey found another bug. 
Oliver Jones asked a question which led to an example of the use of {cmd:twoway rbar} to mimic pipe or barcode symbols. 
Fredrik Norstr{c o:}m found yet another bug. 


{title:Author}

{p 4 4 2}Nicholas J. Cox, Durham University, U.K.{break} 
n.j.cox@durham.ac.uk


{title:References} 

{p 4 8 2}Becker, R.A., J.M. Chambers, and A.R. Wilks. 1988. 
{it:The new S language: A programming environment for data analysis and graphics.} 
Pacific Grove, CA: Wadsworth and Brooks/Cole. 

{p 4 8 2}Berry, D.A. 1996. {it:Statistics: a Bayesian perspective.} 
Belmont, CA: Duxbury. 

{p 4 8 2}Bibby, J. 1986. 
{it:Notes towards a history of teaching statistics.} 
Edinburgh: John Bibby (Books). 

{p 4 8 2}Bland, M. 2000. 
{it:An introduction to medical statistics.}
Oxford: Oxford University Press. 

{p 4 8 2}Bowley, A.L. 1910. 
{it:An elementary manual of statistics.} 
London: Macdonald and Evans. (seventh edition 1952) 

{p 4 8 2}Box, G.E.P., W.G. Hunter and J.S. Hunter. 1978. 
{it: Statistics for experimenters: an introduction to design, data analysis, and model building.}
New York: John Wiley. (second edition 2005)

{p 4 8 2}Chambers, J.M., W.S. Cleveland, B. Kleiner and P.A. Tukey. 1983. 
{it:Graphical methods for data analysis.} Belmont, CA: Wadsworth. 

{p 4 8 2}Cleveland, W.S. 1984. Graphical methods for data presentation: full
scale breaks, dot charts, and multibased logging. 
{it:American Statistician} 38: 270{c -}80.

{p 4 8 2}Cleveland, W.S. 1985. {it:Elements of graphing data.} 
Monterey, CA: Wadsworth. 

{p 4 8 2}Cleveland, W.S. 1994. {it:Elements of graphing data.} 
Summit, NJ: Hobart Press. 

{p 4 8 2}Cobb, G.W. 1998. 
{it:Introduction to design and analysis of experiments.} 
New York: Springer. 

{p 4 8 2}Cox, N.J. 2004. 
Speaking Stata: Graphing distributions. 
{it:Stata Journal} 4(1): 66{c -}88. 
 
{p 4 8 2}Cox, N.J. 2007. 
Speaking Stata: Turning over a new leaf. 
{it:Stata Journal} 7(3): 413{c -}433. 

{p 4 8 2}Cox, N.J. 2009. 
Speaking Stata: Creating and varying box plots. 
{it:Stata Journal} 9(3): 478{c -}496. 

{p 4 8 2}Computing Resource Center. 1985. {it:STATA/Graphics user's guide.} 
Los Angeles, CA: Computing Resource Center. 

{p 4 8 2}Dalgaard, P. 2002. {it:Introductory statistics with R.} 
New York: Springer. 

{p 4 8 2}Dickinson, G.C. 1963. 
{it:Statistical mapping and the presentation of statistics.} 
London: Edward Arnold. (second edition 1973) 

{p 4 8 2}Ellison, A.M. 1993. 
Exploratory data analysis and graphic display. 
In Scheiner, S.M. and J. Gurevitch (eds) 
{it:Design and analysis of ecological experiments.} 
New York: Chapman & Hall, 14{c -}45. 

{p 4 8 2}Ellison, A.M. 2001. 
Exploratory data analysis and graphic display. 
In Scheiner, S.M. and J. Gurevitch (eds) 
{it:Design and analysis of ecological experiments.} 
New York: Oxford University Press, 37{c -}62. 

{p 4 8 2}Faraway, J.J. 2005. {it:Linear models with R.} 
Boca Raton, FL:Chapman and Hall/CRC. 

{p 4 8 2}Feinstein, A.R. 2002. {it:Principles of medical statistics.} 
Boca Raton, FL: Chapman and Hall/CRC. 

{p 4 8 2}Friendly, M., P. Valero-Mora and J.I. Ulargui. 2010.
The first (known) statistical graph: Michael Florent van Langren and the "secret" of longitude.
{it:American Statistician} 64: 174{c -}184. (supplementary materials online) 

{p 4 8 2}Gregory, S. 1963. {it:Statistical methods and the geographer.} 
London: Longmans. (later editions 1968, 1973, 1978; publisher later Longman)

{p 4 8 2}Griffiths, D., W.D. Stirling and K.L. Weldon. 1998. 
{it:Understanding data: principles and practice of statistics.} 
Brisbane: John Wiley. 

{p 4 8 2}Hay, I. 1996. 
{it:Communicating in geography and the environmental sciences.}
Melbourne: Oxford University Press. (later editions 2002, 2006) 

{p 4 8 2}Keen, K.J. 2010. 
{it:Graphics for statistics and data analysis with R.} 
Boca Raton, FL: CRC Press. 

{p 4 8 2}Klemel{c a:}, J. 2009. 
{it:Smoothing of multivariate data: Density estimation and visualization.} 
Hoboken, NJ: John Wiley. 

{p 4 8 2}Langren, Michael Florent van. 1644. 
{it:La verdadera longitud por mar y tierra.} Antwerp. 

{p 4 8 2}Lee, J.J. and Z.N. Tu. 1997. 
A versatile one-dimensional distribution plot: the BLiP plot. 
{it:American Statistician}
51: 353{c -}358. 

{p 4 8 2}Maindonald, J.H. and W.J. Braun. 2007. 
{it:Data analysis and graphics using R {c -} an example-based approach.} 
Cambridge: Cambridge University Press. 

{p 4 8 2}Matthews, J.A. 1981. 
{it:Quantitative and statistical approaches to geography: A practical manual.} 
Oxford: Pergamon. 

{p 4 8 2}Monkhouse, F.J. and H.R. Wilkinson. 1952. 
{it:Maps and diagrams: Their compilation and construction.} 
London: Methuen. (later editions 1963, 1971)

{p 4 8 2}Morgenthaler, S. 2007. 
{it:Introduction {c a'g} la statistique}. 
Lausanne: Presses polytechniques et universitaires romandes. 

{p 4 8 2}Pearson, E.S. 1956.
Some aspects of the geometry of statistics: the use of visual 
presentation in understanding the theory and application of mathematical 
statistics. 
{it:Journal of the Royal Statistical Society} 
A 119: 125{c -}146.

{p 4 8 2}Quinn, G.P. and M.J. Keough. 2002. 
{it:Experimental design and data analysis for biologists.} 
Cambridge: Cambridge University Press. 

{p 4 8 2}Reimann, C., P. Filzmoser, R.G. Garrett and R. Dutter. 2008. 
{it:Statistical data analysis explained: applied environmental statistics with R.}
Chichester: John Wiley. 

{p 4 8 2}Robbins, N.B. 2005. 
{it:Creating more effective graphs.} 
Hoboken, NJ: John Wiley.

{p 4 8 2}Ryan, B.F., B.L. Joiner and T.A. Ryan. 1985. 
{it:Minitab handbook.} 
Boston, MA: Duxbury. 

{p 4 8 2}Sasieni, P.D. and P. Royston. 1996. 
Dotplots. 
{it:Applied Statistics} 45: 219{c -}234.

{p 4 8 2}Schenemeyer, J.H. and L.J. Drew. 2011. 
{it:Statistics for earth and environmental scientists.}
Hoboken, NJ: John Wiley.
 
{p 4 8 2}Tufte, E.R. 1974. 
{it:Data analysis for politics and policy.} 
Englewood Cliffs, NJ: Prentice-Hall. 

{p 4 8 2}Tufte, E.R. 1997. 
{it:Visual explanations: images and quantities, evidence and narrative.} 
Cheshire, CT: Graphics Press. 

{p 4 8 2}Tukey, J.W. 1972.
Some graphic and semi-graphic displays.
In Bancroft, T.A. and Brown, S.A. (eds)
{it:Statistical papers in honor of George W. Snedecor.}
Ames, IA: Iowa State University Press, 293{c -}316.
(also accessible at {browse "http://www.edwardtufte.com/tufte/tukey":http://www.edwardtufte.com/tufte/tukey})

{p 4 8 2}Tukey, J.W. 1977. 
{it:Exploratory data analysis.} 
Reading, MA: Addison-Wesley. 

{p 4 8 2}Tukey, J.W. and P.A. Tukey. 1990. Strips displaying 
empirical distributions: I. Textured dot strips. Bellcore Technical Memorandum. 

{p 4 8 2}Venables, W.N. and B.D. Ripley. 2002. 
{it:Modern applied statistics with S.} New York: Springer. 

{p 4 8 2}Warton, D.I. 2008. 
Raw data graphing: an informative but under-utilized tool for the analysis of multivariate abundances.
{it:Austral Ecology} 33: 290{c -}300. 

{p 4 8 2}Wild, C.J. and G.A.F. Seber. 2000. 
{it:Chance encounters: a first course in data analysis and inference.} 
New York: John Wiley. 

{p 4 8 2}Wilkinson, L. 1992. Graphical displays. 
{it:Statistical Methods in Medical Research} 1: 3{c -}25. 

{p 4 8 2}Wilkinson, L. 1999. Dot plots. {it:American Statistician} 
53: 276{c -}281. 

{p 4 8 2}Wilkinson, L. 2005. {it:The language of graphics.} 
New York: Springer. 

{p 4 8 2}Young, F.W., P.M. Valero-Mora and M. Friendly. 2006. 
{it:Visual statistics: Seeing data with interactive graphics.} 
Hoboken, NJ: John Wiley. 


{title:Also see}

{p 4 13 2} 
On-line: help for {help dotplot}, {help gr7oneway}, {help histogram}, 
{help beamplot} (if installed) 

