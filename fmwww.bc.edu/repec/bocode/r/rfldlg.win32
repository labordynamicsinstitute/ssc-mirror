POSITION . . 500 350
DIALOG main, title("rfl 2.2 - Recent file list") tabtitle("Main")
BEGIN
// Windows-Version
// Version 2.2, 10 Nov 2004
// Begin groupbox for datasets, descriptions, memory

	GROUPBOX gb_Database    5   10  480  150,	label(`"Select the desired file from the drop-down-list or choose to open another file"')
	GROUPBOX gb_lists      +3   19  255   27, label("")
	TEXT tx_liste          10   25   20   12, left label("List")
	RADIO rb_listrecent   +25    @   85    @,	first label(`"Most recent"')	option("getlast5")	 onclickon("script show_rb_g1")	//onclickoff("script hide_rb_g1")
	RADIO rb_listall      +95    @   30    @, middle label(`"All"') 			option("getallfiles") onclickon("script show_rb_g2")	 //onclickoff("script hide_rb_g2") nomemory
	RADIO rb_listdesc     +40    @   85    @,	last	label(`"by description"')		  option("getlast5") onclickon("script show_rb_g3")	//onclickoff("script hide_rb_g3") nomemory

	GROUPBOX gb_otherno 	 285   19  197   27, label("")
	CHECKBOX cb_otherDB	 +10   25  100   20, label("Other data file") onclickon("program fi_dtaopen_show") onclickoff("program fi_dtaopen_hide")
	CHECKBOX cb_noDB     +105	  @   80    @, label("No data file")	  onclickon("program no_db_checkon")	 onclickoff("program no_db_checkoff") nomemory

	COMBOBOX co_listrecent 10   50  467  102, label("Select the desired file") dropdownlist 	                contents(lstlast5)		onselchange("program fi_sel_chg") nomemory
	COMBOBOX co_listall     @    @    @    @, label("Select the desired file") dropdownlist 	                contents(listall)		onselchange("program fi_sel_chg") nomemory
	COMBOBOX co_listdesc    @    @    @    @, label("Select the desired file by description") dropdownlist 	 contents(lstdesc)	   onselchange("program fi_sel_chg") nomemory
	FILE fi_otherDB         @    @    @   20, label("Choose ...") dialogtitle("Select a data file")	  filter("Stata Data|*.dta")		onchange("script sc_after_fi_fop") nomemory

	TEXT tx_desc           10   75  467   20, left // Description
	// Beschränkung auf 80 Zeichen, da die Combolists auf 80 Zeichen beschränkt sind
	// Listeneinträge mit Zeichenlängen >80 erzeugen den Fehler r(1004): command too long
	EDIT ed_desc            @    @    @    @, label("Enter a description (<=244 Chars)") max(244)

	TEXT tx_filesize       10  104  330   12, left label("An error occured - you should read here the size of the dataset")
	TEXT tx_setmem       +330    @   70    @, right label("Set memory to")
	SPINNER sp_mem        +76    @   45   20, label("Memory to be allocated") 		option(memory) max(1000) onchange("program setmemtext")
	TEXT tx_setmemMB      +47    @   16   12, left label("MB")

	GROUPBOX gb_marked     10  +21   30   25, label("")
	TEXT tx_marked         +5   +5   20   12, left label("")
	BUTTON bu_chgdesc     134    @  100   20, label("Change description") 			onpush("script chgdesc")
	BUTTON bu_chgdn       134    @    @    @, label("Done")								onpush("program chgdescdone")

	BUTTON bu_dellistentry +110  @  120   20, label("Remove/Rename entry") 			onpush("program show_gb_delren")
	BUTTON bu_showname	 +130   @  100   20, label("Complete info") 					onpush("program showname")

// Always hidden in this group:
	EDIT ed_hid            20  270  467   15, label("Zur Übergabe zwischen Textfeld description und edit description") max(244) nomemory
	EDIT ed_hidfo           @    @    @    @, label("Zur Übergabe der description, die für eine neue Datei geschrieben wird") max(244) nomemory
	EDIT ed_hisize          @    @    @    @, label("Size of data file")
	CHECKBOX cb_hidedesclist 1   1    1    1, label("Hide descriptions list") nomemory default(0)

// Begin groupbox for logfiles
	GROUPBOX gb_Logfile     5  170  480  115, label(`"Log file to use:"')
	RADIO rb_log1default   10  +17  467   12, first	  label(`"(Log-file not yet chosen)"')  onclickon("script sc_knownlog")
	RADIO rb_log2last       @  +17    @    @, middle   label(`"<no last log file yet>"') onclickon("script sc_rb_l2")
	RADIO rb_log3nolog      @  +17    @    @, middle   label(`"No Log-file"') onclickon("script nolog")
	RADIO rb_log4other      @  +17    @    @, last		label(`"Other Log-file"')		 onclickon("script logopen")
	FILE	fi_logopen        @  +20    @   20, label("Choose ...") dialogtitle("Select the desired log file") filter("Formatted log (*.smcl)|*.smcl|Log (*.log)|*.log|All files (*.*)|*.*") onchange("program setlogname")

// Always hidden in this group:
	EDIT ed_hil            10  270  467   15, label("Hidden log") nomemory
	EDIT ed_hill            @  +15    @    @, label("Hidden log last used with this file") nomemory
// end groupbox for logfiles

// Begin other visible controls
	CHECKBOX cb_dsave  		@  285   47   14, label(`"Save"') option("saved") nomemory
	CHECKBOX cb_dclose  	 +50	 -2  467    @, label(`"Close data in memory"') option("closed") nomemory
	CHECKBOX cb_lclose      @  +16    @    @, label(`"Close (and save) open log"') option("closel") nomemory
	CHECKBOX cb_cclose      @  +16    @    @, label(`"Close (and save) open cmdlog"') option("closec") nomemory
// end other visible controls

// Begin other hidden controls
	EDIT ed_scri            5  +15    @   15, nomemory max(3000)
	CHECKBOX cb_runsetmemtext 5 300  15    @, label("Allow setmemtext to run") default(0) nomemory
	CHECKBOX cb_runstata    +15   @   @	   @, label("Do not run -stata- commands before the checkbox is set to 1, ie upon startup") default(0) nomemory
// end other hidden controls

// Begin groupbox for deleting and renaming entries
	GROUPBOX gb_ren_del     5  170  480  115, label(`"What do you want to do with the selected list entry?"')
	RADIO rb_delete 	     +5  +17  125    @,	first label("Remove")
	RADIO rb_rename         @  +17    @    @, last label("Rename") 					onclickon("program show_rb_rename_on")	onclickoff("program show_rb_rename_off") nomemory
	FILE fi_rename          @  +20  467   20, label("Choose ...") dialogtitle("Select the new location of the file")	  filter("Stata Data|*.dta")
	BUTTON bu_fi_delete   134  +40   50   15, label("Submit") onpush("program del_ren_entry")
	BUTTON bu_fi_cancel   +60    @  +10   15, label("Show logs") onpush("program del_ren_cancel")

// This one is only used at start-up to set the initial values
	CHECKBOX cb_startdialog 1    1    1    1, label("Must be the 1st line. Hide some controls and start the dialog") nomemory onclickon("script startdialog") onclickoff("script startdialog") default(0)

END
