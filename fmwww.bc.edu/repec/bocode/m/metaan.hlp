{smcl}
{* 20Apr2009}{...}
{hline}
help for {hi:metaan}
{hline}

{title:Title}

{p2colset 5 15 17 2}{...}
{p2col :{hi:metaan} {hline 2}}Module for performing fixed- or random-effects meta-analyses{p_end}
{p2colreset}{...}


{title:Syntax}

{p 4 8 2}
{cmd:metaan}
{it:varname1}
{it:varname2}
{ifin}
[{cmd:,} {it:{help collapse##options:options}}]

{p 4 4 2}
where

{p 6 6 2}
{it:varvame1} the study effect sizes.

{p 6 6 2}
{it:varvame2} the study effect variation, with standard error used as default.

{synoptset 20 tabbed}{...}
{marker options}{...}
{synopthdr}
{synoptline}
{syntab :Options}
{synopt :{opt fe}}Fixed-effect model
{p_end}
{synopt :{opt dl}}DerSimonian-Laird random-effects model
{p_end}
{synopt :{opt bdl}}Bootstrapped DerSimonian-Laird random-effects model
{p_end}
{synopt :{opt ml}}Maximum likelihood random-effects model
{p_end}
{synopt :{opt reml}}Restricted maximum likelihood random-effects model
{p_end}
{synopt :{opt pl}}Profile likelihood random-effects model
{p_end}
{synopt :{opt pe}}Permutations random-effects model
{p_end}
{synopt :{opt sa}}Sensitivity analysis model
{p_end}
{synopt :{opt varc}}Variances provided instead of the standard errors/deviations
{p_end}
{synopt :{opth label(varname)}}Study label variable(s)
{p_end}
{synopt :{opt forest}}Forest plot
{p_end}
{synopt :{opth forestw(#)}}Forest plot with adjusted weight ratios
{p_end}
{synopt :{opth reps(#)}}Number of bootstrap replications
{p_end}
{synopt :{opth seed(#)}}Seed number
{p_end}
{synopt :{opth sens(#)}}Sensitivity analysis pre-set heterogeneity (I^2) level
{p_end}
{synopt :{opth plplot(string)}}Likelihood plot for the mu or tau^2 estimate in the maximum-likelihood models (ml, pl, reml)
{p_end}

{title:Description}

{p 4 4 2}
The {cmd:metaan} command performs a meta-analysis on a set of studies and calculates the overall effect and a confidence interval for
the effect. The command also displays various heterogeneity measures: Cochrane's Q, I-squared (0-100% with larger scores indicating heterogenity)
, H-squared (zero in the case of homogeneity) and the between-study variance estimate. Cochrane's Q is the same across all methods, but the
between-study variance estimate (and hence I-squared and H-squared) can vary between the {opt dl} and {opt ml} methods. Only one method option must
be selected. For calculating the effects and variance of the effects, for a group of studies, from various statistical parameters please see
{cmd:{help metaeff}}. More details and examples have been provided in accompanying paper, published in the Stata Journal (http://www.stata-journal.com/article.html?article=st0201).

{title:Options}

{dlgtab:Model}

{phang}
{opt fe} Fixed-model that assumes there is no heterogeneity between the studies. The model assumes
that within-study variances may differ, but that there is homogeneity of effect size across allstudies.
Often the homogeneity assumption is unlikely and variation in the true effect across studies is to be expected.
Therefore, caution is required when using this model. Reported heterogeneity measures are estimated using the {opt dl} model.

{phang}
{opt dl} DerSimonian-Laird (DL), the most commonly used random-effects model. Models heterogeneity between the studies i.e. assumes that the true
effect can be different for each study. The method assumes that the individual study true effects are distributed with a variance tau^2, around an
"overall" true effect, but makes no assumptions about the form of the distribution of either the within- or between-study effects.
Reported heterogeneity measures are estimated using the {opt dl} model.

{phang}
{opt bdl} Bootstrapped DerSimonian-Laird, similar approach to DL but better performing. Uses a non-parametric bootstrap to estimate the between-study variance and
other heterogeneity parameters. Reported heterogeneity measures are estimated using the {opt bdl} model.

{phang}
{opt ml} Maximum likelihood random-effects model. Makes the additional assumption (necessary to derive the log-likelihood function, and also true for
{opt reml} and {opt pl} below) that both the within-study and between-study effects have Normal distributions. It solves the log-likelihood function
iteratively to produce an estimate of the between-study variance. However, the method does not always converge while in some cases the between-study
variance estimate is negative and set to zero (in which case the model is reduced to the {opt fe} model). Estimates are reported as missing in the
event of non-convergence. Reported heterogeneity measures are estimated using the {opt ml} model.

{phang}
{opt reml} Restricted maximum-likelihood random-effects model. Similar method to {opt ml} and using the same assumptions. The log-likelihood
function is maximized iteratively to provide estimates as in {opt ml}. However, under {opt reml} only the part of the likelihood function which is location
invariant is maximized (i.e. maximizing the portion of the likelihood that does not involve mu, if estimating tau^2, and vice versa).
The method does not always converge while in some cases the between-study variance estimate is negative and set to zero (in which case the model is
reduced to the {opt fe} model). Estimates are reported as missing in the event of non-convergence. Reported heterogeneity measures are estimated using
the {opt reml} model.

{phang}
{opt pl} Profile likelihood random-effects model. Profile likelihood uses the same likelihood function as {opt ml}, but takes into account the
uncertainty associated with the between-study variance estimate when calculating an overall effect, by using nested iterations to converge to an
maximum. The confidence intervals provided by the method are asymmetric and hence so is the the diamond in the forest plot. However, the
method does not always converge. Values that were not computed are reported as missing. Reported heterogeneity measures are estimated using the
{opt ml} model (the effect and tau^2 estimates are the same, only the confidence interevals are re-estimated) but also provides a confidence interval
for the between-study variance estimate.

{phang}
{opt pe} Permutations random-effects model. A non-parametric random-effects method, which can be described in three steps. First, in line with a Null
hypothesis that all true study effects are zero and observed effects are due to random variation, a dataset of all possible combinations of observed
study outcomes is created by permuting the sign of each observed effect. Then the {opt dl} method is used to compute an overall effect for each
combination. Finally, the resulting distribution of overall effect sizes is used to derive a confidence interval for the observed overall effect.
The confidence interval provided by the method is asymmetric and hence so is the diamond in the forest plot. Reported heterogeneity measures are
estimated using the {opt dl} model.

{phang}
{opt sa} Sensitivity analysis model. Allows sensitivity analyses to be performed by varying the level of heterogeneity, with I^2 taking values in the
[1,100) range. Undetected heterogeneity is the norm rather than the exception and we encourage users to test the sensitivity of their results in the
presence of moderate (50%) and high (80-90%) levels of heterogeneity (please see Kontopantelis et al, 2013).
Reported heterogeneity measures are based on the preset I^2 level.


{dlgtab:Variable information}

{phang}
{opt varc} Informs the program that the study effect variation variable ({it:varname2}) holds variance values. If this option is
omitted the program assumes the variable contains standard error values (the default)

{phang}
{opth label(varname)} Selects labels for the studies.
Up to two variables can be selected and converted to strings. If two variables are selected they will be separated by a comma. Usually,
the author names and the year of study are selected as labels. The final string is truncated to 20 characters.

{dlgtab:Bootstrapped DerSimonian-Laird options}

{phang}
{opth reps(#)} Integer number of repetition for the bootstrapped DerSimonian-Laird method. Fewer than 100 repetitions are not permitted.

{phang}
{opth seed(#)} Seed number to be used in the bootstrapped DerSimonian-Laird method.

{dlgtab:Sensitivity analysis options}

{phang}
{opth sens(#)} Preset heterogeneity level, with I^2 taking values in the [0,100) range. The default value is 80%.

{dlgtab:Graph}

{p 4 4 2}
Only one graph output is allowed in each execution

{phang}
{opt forest} Requests a forest plot. The weights from the specified analysis are used for plotting symbol sizes ({opth pe} uses {opth dl} weights).

{phang}
{opth forestw(#)} Requests a forest plot with adjusted weight ratios for better display. The value can be in the [1,50] range.
For example if the largest to smallest weight ratio is 60 and the graph looks awkward the user can use this command to improve
the appearance, by requesting the weight to be rescaled to a largest/smallest weight ratio of 30. It should be noted that only
the weight squares in the plot are affected and not the model. The confidence intervals in the plot are unaffected.

{phang}
{opth plplot(string)} Requests a plot of the likelihood function for the mu or tau^2 estimates of the {opt ml}, {opt pl} or {opt reml} models.
Option {opt plplot(mu)} fixes mu to its model estimate, in the likelihood function, and creates a two way plot of tau^2 vs the likelihood function.
Option {opt plplot(tsq)} fixes tau^2 to its model estimate, in the likelihood function, and creates a two way plot of mu vs the likelihood function.


{title:Remarks}

{phang}
For a detailed description of the methods see Brockwell & Gorndon (methods {opt fe}, {opt dl}, {opt pl}, {opt ml}) and
Follmann & Proschan ({opt pe}). Method performance investigated by Kontopantelis & Reeves and Brockwell & Gorndon. Performance of
the bootstrapped DerSimonian-Laird ({opt bdl}) investigated by Kontopantelis, Springate and Reeves.


{title:Examples}

{p 4 8 2}
{cmd:. metaan eff SEeff, ml}

{p 4 8 2}
{cmd:. metaan eff SEeff, pl forest}

{p 4 8 2}
{cmd:. metaan eff effvar, varc pe}

{p 4 8 2}
{cmd:. metaan eff effvar, varc fe forestw(20)}

{p 4 8 2}
{cmd:. metaan eff effvar, bdl reps(10000) seed(123) label(study)}

{p 4 8 2}
{cmd:. metaan eff effvar, sa sens(50) label(study)}


{title:Authors}

{p 4 4 2}
Evangelos Kontopantelis, Centre for Biostatistics, Institute of Population Health, University of Manchester, e.kontopantelis@manchester.ac.uk

{p 4 4 2}
David Reeves, Centre for Biostatistics, Institute of Population Health, University of Manchester


{title:References}

{phang}
Kontopantelis and Reeves D. 2010.
{it:metaan: Random-effects meta-analysis}.
The Stata Journal. http://www.stata-journal.com/article.html?article=st0201

{phang}
Kontopantelis, E., Springate D. and Reeves D. 2013.
{it:A re-analysis of the Cochrane Library data: the dangers of unobserved heterogeneity in meta-analyses}.
PLoS ONE.

{phang}
Kontopantelis, E. and Reeves D. 2010.
{it:The Robustness of Statistical Methods for Meta-Analysis when Study Effects are Non-Normally Distributed: A Simulation Study}.
Statistical Methods in Medical Research.

{phang}
Kontopantelis, E. and Reeves D. 2009.
{it:A Meta-Analysis add-in for Microsoft Excel}.
Journal of Statistical Software.

{phang}
Mittlbock, M. and Heinzl H. 2006.
{it:A Simulation Study Comparing Properties of Heterogeneity Measures in Meta-Analyses}.
Statistics in Medicine.

{phang}
Higgins, J.P. and Thompson S.G. 2002.
{it:Quantifying Heterogeneity in a Meta-Analysis}.
Statistics in Medicine.

{phang}
Brockwell, S.E. and Gordon I.R. 2001.
{it:A Comparison of Statistical Methods for Meta-Analysis}.
Statistics in Medicine.

{phang}
Follmann, D.A. and Proschan M.A. 1999.
{it:Valid Inference in Random Effects Meta-Analysis}.
Biometrics.


{title:Also see}

{p 4 4 2}
STB: STB-44 sbe24

{p 4 4 2}
help for {help metaeff}, {help metan7} (if installed)

{p 4 4 2}
{help metannt} (if installed), {help meta} (if installed)

{p 4 4 2}
{help metacum} (if installed), {help metareg} (if installed)

{p 4 4 2}
{help metabias} (if installed), {help metatrim} (if installed)

{p 4 4 2}
{help metainf} (if installed), {help galbr} (if installed)

{p 4 4 2}
{help metafunnel} (if installed)

