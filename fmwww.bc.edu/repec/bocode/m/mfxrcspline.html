<pre>
<p>
help for <b>mfxrcspline</b>
-------------------------------------------------------------------------------
<p>
<b><u>Title</u></b>
<p>
    <b>mfxrcspline</b> -- Displays the marginal effect of a restricted cubic spline.
<p>
<b><u>Syntax</u></b>
<p>
        <b>mfxrcspline</b> [<i>if</i>] [<i>in</i>] <b>,</b> [ <b>at(</b><i>var </i><b>=</b><i> # </i>[<i>var </i><b>=</b><i> # </i>[...]]<b>)</b> <b>link(</b><i>linkname</i><b>)</b>
               <b><u>cust</u></b><b>omdydxb(</b><i>dydxb_specification</i><b>)</b> <b><u>sh</u></b><b>owknots</b>
               <b>ciopts(</b><i>rarea_options</i><b>)</b> <b>noci</b> <b>level(</b><i>#</i><b>)</b> <b>lineopts(</b><i>line_options</i><b>)</b>
               <b>addplot(</b><i>plot</i><b>)</b> <b><u>gen</u></b><b>erate(</b><i>newvar1 </i>[<i>newvar2 newvar3</i>]<b>)</b> ]
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>mfxrcspline</b> displays a graph of the marginal effect of a restricted cubic
    spline. In other words, it shows for each value of the spline variable
    how much the expected value of your explained variable changes for a unit
    change in the spline variable. It is the first derivative of the curve
    shown by adjustrcspline.
<p>
    <b>mfxrcspline</b> will automatically compute the appropriate marginal effects
    (i.e. detect the appropriate link function) for the following commands:
    <b>regress</b>, <b>logit</b>, <b>logistic</b>, <b>betafit</b>, <b>probit</b>, <b>poisson</b>, <b>cloglog</b>, and <b>glm</b>
    (with the exception of the negative binomial link function). For other
    commands users can specify the appropriate link function using the <b>link()</b>
    option. With the <b>link()</b> option you can specify the dentity, logit,
    probit, log, log-complement, log-log, complementary log-log, reciprocal,
    power, and the odds power links. If your model uses another link function
    you can manually specify the first derivative of the inverse link
    function (with respect to the linear predictor) in the <b>customdydxb()</b>
    option.
<p>
<b><u>Options</u></b>
<p>
    <b>at(</b><i>var </i><b>=</b><i> # </i>[<i>var </i><b>=</b><i> # </i>[...]]<b>)</b> specifies the values of the other explanatory
        variables (and offset or exposure) at which the marginal effects are
        calculated. The default is to set all variables at their mean.
<p>
        The only exception is the default when the<b> exposure()</b> option is
        specified in the estimation command. In that case the default is to
        set the exposure equal to the exp(mean(offset)).
<p>
    <b>link(</b><i>linkname</i><b>)</b> specifies the link function used for computing the
        marginal effects. The link functions that can be specified are:
        identity, logit, probit, log, logcomplement, loglog, cloglog,
        reciprocal, power #, opower #. These link functions are described in
        <b>[R] glm</b> and (Hardin and Hilbe 2001).
<p>
    <b>customdydxb(</b><i>inv_link_specification</i><b>)</b> specifies a user specified first
        derivative of the inverse link funtion. The syntax is the same as in 
        predictnl, in particular the linear predictor can be specified with
        <b>xb()</b>.
<p>
    <b>showknots</b> specifies that the positions of the knots be shown on the graph
        by vertical lines.
<p>
    <b>ciopts(</b><i>rarea_options</i><b>)</b> specifies options of twoway rarea to tune the
        display of the confidence interval.
<p>
    <b>noci</b> suppresses the display of the confidence interval.
<p>
    <b>level(</b><i>#</i><b>)</b> specifies a confidence level to use for confidence intervals.
        See help on level.
<p>
    <b>lineopts(</b><i>line_options</i><b>)</b> specifies options of twoway line to tune the
        display of the point estimates of the marginal effect. These options
        can also be added directly, which can be convenient to change for
        instance the legend, axis titles, etc.
<p>
    <b>addplot(</b><i>plot</i><b>)</b> provides a way to add other plots to the generated graph.
        See help on addplot_option.
<p>
    <b><u>gen</u></b><b>erate(</b><i>newvar1 </i>[<i>newvar2 newvar3</i>]<b>)</b> specifies that the values of the
        marginal effects and optionally the lower and upper bounds of the
        confidence interval be saved in <i>newvar1</i>, <i>newvar2</i> and <i>newvar3</i>
        respectively.
<p>
<p>
<b><u>Examples</u></b>
<p>
<p>
<p>
<b>    sysuse nlsw88, clear</b>
<b>    recode grade 0/5=5</b>
<b>        </b>
<b>    mkspline2 grades = grade, cubic nknots(3)</b>
<b>    logit never_married grades*</b>
<b>        </b>
<b>    adjustrcspline, name(__ex_adjustrcspline1)  </b>
<b>    mfxrcspline, name(__ex_mfxrcspline1) </b>
<p>
    (click to run)
<p>
<p>
<p>
<b>    sysuse uslifeexp, clear</b>
<b>    </b>
<b>    mkspline2 ys = year, cubic</b>
<b>    reg le ys* if year != 1918</b>
<b>        </b>
<b>    adjustrcspline if year != 1918,                             ///</b>
<b>        addplot(scatter le year if year != 1918, msymbol(Oh) || ///</b>
<b>                scatter le year if year == 1918, msymbol(X) )   ///</b>
<b>        ytitle("life expectancy")                               ///</b>
<b>        name(__ex_adjustrcspline 2)                              /// </b>
<b>        note("1918 was excluded from the computations because of the Spanish fl</b>
<b>&gt; u")</b>
<b>    </b>
<b>    mfxrcspline if year != 1918, name(__ex_mfxrcspline2)</b>
<p>
    (click to run)
<p>
<p>
<p>
<b>    sysuse nlsw88, clear</b>
<b>    recode grade 0/5=5</b>
<b>        </b>
<b>    mkspline2 grades = grade, cubic nknots(3)</b>
<b>    logit never_married grades*</b>
<b>        </b>
<b>    local mu "1/(1+exp(-1*xb()))"</b>
<b>    local dydxb "`mu'*(1-`mu')"</b>
<b>    adjustrcspline, customdydxb("`dydxb'")</b>
<p>
    (click to run)
<p>
<p>
<p>
<b>    sysuse nlsw88, clear</b>
<b>    recode grade 0/5=5</b>
<b>        </b>
<b>    mkspline2 grades = grade, cubic nknots(3)</b>
<b>    glm never_married grades* south, link(cloglog) family(binomial)</b>
<b>    </b>
<b>    mfxrcspline, at(south=0)</b>
<p>
    (click to run)
<p>
<p>
<p>
<b>    sysuse cancer, clear</b>
<b>    gen long id = _n</b>
<b>    stset studytime, failure(died) id(id)</b>
<b>        </b>
<b>    stsplit t, every(1)</b>
<p>
<b>    mkspline2 ts=t, cubic nknots(3)</b>
<b>    xi: streg i.drug age ts*, dist(exp)</b>
<b>        </b>
<b>    mfxrcspline , at(_Idrug_2=0 _Idrug_3=0) ///</b>
<b>                  link("log")               ///</b>
<b>                  noci                      ///</b>
<b>                  ytitle(d hazard / d time)</b>
<p>
    (click to run)
<p>
<p>
<b><u>Author</u></b>
<p>
    Maarten L. Buis
    Universitaet Tuebingen
    Institut fuer Soziologie
    maarten.buis@ifsoz.uni-tuebingen.de
<p>
<p>
<b><u>Suggested citation if using mfxrcspline in published work</u></b>
<p>
    <b>mfxrcspline</b> is not an official Stata command. It is a free contribution
    to the research community, like a paper. Please cite it as such.
<p>
    Buis, Maarten L.  2009. "POSTRCSPLINE: Stata module containing
    post-estimation commands for models using a restricted cubic spline" 
    http://ideas.repec.org/c/boc/bocode/s456928.html
<p>
<p>
<b><u>Also see</u></b>
<p>
<p>
    If installed: <b>mkspline2</b>, <b>adjustrcspline</b>, <b>rcspline</b>
</pre>