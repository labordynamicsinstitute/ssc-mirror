<pre>
-------------------------------------------------------------------------------
help for <b>mclest</b>                                                  John Hendrickx
-------------------------------------------------------------------------------
<p>
<b><u>Stata macros for multinomial conditional logit models</u></b>
<p>
<i>MCL</i> stands for <i><u>M</u></i><i>ultinomial </i><i><u>C</u></i><i>onditional </i><i><u>L</u></i><i>ogit</i> model. A conditional logit program
is used to estimate a multinomial logistic model. This produces the same
coefficients and standard errors as a regular multinomial logit program but has
the advantage that it provides great flexibility for imposing constraints on
the dependent variable.  mclgen restructures the data so the model can be
estimated by clogit, <b>mclest</b> estimates the model using <b>clogit</b>.
<p>
In addition, <b>mclest</b> can estimate two special models:  <i>stereotyped ordered</i>
<i>regression</i> (SOR) and Goodman's <i>row and columns model</i> 2 (RC2). Both models
estimate a scaling metric for the dependent variable; the RC2 model estimates a
scaling metric for a categorical independent variable as well.
<p>
<b><u>Syntax:</u></b>
<p>
        <b>mclest</b> <i>varlist</i> [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [<i>weight</i>] [, <b>sor(</b><i>varlist</i><b>)</b> <b>soriter(</b><i>#</i><b>)</b>
                           <b>sortol(</b><i>#</i><b>)</b> <b>rc2(</b><i>varname</i><b>)</b> <b>eqrc2(</b><i>varname</i><b>)</b> <b>muby(</b><i>varlist</i><b>)</b>
                           <b>nonorm</b> <b>debug</b> ]
<p>
<i>varlist</i> contains a model specification. The main effects of the <i>response factor</i>
specified in <b>mclgen</b> correspond with the intercept in a multinomial logit model.
Interactions of the <i>response factor</i> with independent variables correspond with
the effects of these variables. Because the response factor is on the right
hand side of the model specification, it is a simple matter to impose
restrictions. The dichotomous dependent variable and the stratification
variable are automically passed on from mclgen to <b>mclest</b> and do not have to be
specified.
<p>
<b><u>Options</u></b>
<p>
These options are used to request the special nonlinear models <i>Sterotyped</i>
<i>Ordered Regression</i> (SOR) and/or the <i>Row and Columns model 2</i> (RC2).
<p>
<b>sor(</b><i>varlist</i><b>)</b> specifies a list of variables for which the SOR model should be
    estimated. Note that at least two variables should be specified, unless
    either the <b>rc2</b> or <b>eqrc2</b> option is being used.
<p>
<b>soriter(</b><i>#</i><b>)</b> specifies the maximum number of iterations for estimating a SOR or
    RC2 model. The default value is 20.
<p>
<b>sortol(</b><i>#</i><b>)</b> specifies the convergence criterion for estimating a SOR or RC2
    model. The default value is .0001.
<p>
<b>rc2(</b><i>varname</i><b>)</b> specifies a categorical independent variable for the RC2 model.
    The eqrc2 option will be ignored if the <b>rc2</b> option is specified.
<p>
<b>eqrc2(</b><i>varname</i><b>)</b> specifies a categorical independent variable for the EQRC2
    model. The <b>rc2</b> option may not be used together with the eqrc2 option.
<p>
<b>muby(</b><i>varlist</i><b>)</b> specifies one or more variables which affect the association
    between the <b>rc2 </b>or <b>eqrc2</b> variable and the dependent variable. Ignored if
    not used in conjunction with the <b>rc2</b> or <b>eqrc2</b> option.
<p>
<b>nonorm</b> prevents the mclest program from estimating a normalized solution if a
    SOR and/or RC2 model has been requested.
<p>
<b>debug</b> prints intermediate results of <b>clogit</b>. This can be used to determine the
    source of error if something goes wrong. The default is <b>nodebug</b>.
<p>
<b>mclest</b> passes the following arguments on to clogit unaltered:
<p>
    <b>weight</b>, <b>if</b>, <b>in</b>
<p>
See the Stata documentation on clogit for further details on these options.
<p>
<b><u>Usage</u></b>
<p>
For basic use, <b>mclest</b> will be specified with a list of dummy variables
representing the main effects of the <i>response factor</i> and interactions of the
<i>response factor</i> with independent variables.
<p>
The main effects of the independent variables should not be specified;
unfortunately xi does not provide this option. As a result, clogit will report
that the main effects have been "omitted due to no within-group variance". This
has no further consquences for the estimates.
<p>
In the following example, the variable <b>occ</b> (respondent's occupation, 5
categories) is the <i>response factor</i>. It is specified in the mclgen command to
transform the data into a <i>person/choice</i> file. <b>mclgen </b>reports "(3352
observations created)", each of the 838 cases has been duplicated 4 times so
that there are now 5 records for each respondent. The cases are indexed by <b>occ</b>
and <b>__strata</b>. <b>occ</b> indicates response options 1 to 5, <b>__strata</b> indicates
respondents 1 to 838. The variable <b>__didep</b> indicates which record corresponds
with the respondent's choice.
<p>
The model is specified as a main effect of <b>occ</b> and interactions of <b>occ</b> with the
independent variables <b>educ</b> and <b>black</b>. The model could be specified as xi:
mclest i.occ*educ i.occ*black instead. This produces the same estimates but a
different order of the estimates.
<p>
* Using mcl to estimate a multinomial logit model use logan
<p>
mlogit occ educ black, base(1)
<p>
mclgen occ
xi: mclest i.occ i.occ|educ i.occ|black
<p>
The coefficients of this model are the same as those found using mlogit. For
models like this, <b>mclest</b> basicly just specifies the <b>__didep</b> and <b>__strata</b>
variables for you in a clogit command. The model could also be estimated as:
<p>
xi: clogit __didep i.occ i.occ|educ i.occ|black, strata(__strata)
<p>
This model can be estimated equivalently (and more easily) with <b>mlogit</b>. The
advantage of mcl models lies in the ability to easily specify different
<i>response functions</i> for different independent variables. A response function
refers to the type of parameterization applied to the response factor.
<p>
Using <b>xi</b>, the first category of <b>occ</b> is treated as the reference category. In
that case, the model is equivalent to using mlogit ..., base(1). Using xi3 or 
desmat, other paramterizations, or contrasts, can be applied to the response
factor, obtaining other response functions. Equality constraints can be imposed
on two categories of the response factor by adding the dummies for those two
categories. A parameter can be fixed to zero by dropping the dummy for that
cateory. A linear constraint can be imposed by treating the response factor as
a continuous variable. In an mcl model, such restrictions can be imposed on the
indendent variables on a variable by variable basis.
<p>
For example, to estimate an <i>adjacent logit</i> (Agresti 1990: 318) model with
<b>mclest</b>, use the <i>backward difference contrast</i> with either xi3 or desmat. Both
<b>xi3</b> and <b>desmat</b> are available from the ssc archives. To estimate and adjacent
logit model with <b>xi3</b>, use:
<p>
xi3: mclest b.occ b.occ*educ b.occ*black
<p>
Or with <b>desmat</b>:
<p>
char occ[pzat] dif(b)
desmat: mclest occ occ.@educ occ.@black
<p>
Another conceivable application might be to impose a <i>linear constraint</i> on the
response factor for the effects of <b>educ</b> but to use the usual constraints (first
category is reference) for <b>black</b>. This could be done as follows:
<p>
gen occ_ed=occ*educ
xi: mclest i.occ occ_ed i.occ*black
<p>
<b><u>Mobility models</u></b>
<p>
With <b>mclest</b>, it is easy to impose any constraint you might wish on the response
factor and to impose a different constraint for each independent (dummy)
variable if necessary. One application of this is to specify <i>loglinear model</i>
<i>for square tables</i>, also known as <i>mobility models</i>, as multinomial logistic
models (cf. Logan 1983, Breen 1994).
<p>
Mobility models lie in the space between a model of independence and a
saturated loglinear model. Special constraints are imposed on the second degree
loglinear parameters in order to test for a particular pattern of association
and to enhance interpretablity by reducing the number of parameters. Hout
(1983) and Goodman (1984) contain overviews of commonly used mobility models.
<p>
When treated as multinomial logit models, mobility models utilize a different
<i>response function</i>, i.e. a different set of restrictions on the response factor,
for different levels of the "other" variable.  For example, in a
quasi-independence model for father's occupation by son's occupation, son's
occupation would be specified as the response factor. For category <i>i</i> of
father's occupation, the response function would be category <i>i</i> of son's
occupation versus the other categories. The model would be specified as:
<p>
mclgen occ
gen d1=(focc==1)*(occ==1)
gen d2=(focc==2)*(occ==2)
gen d3=(focc==3)*(occ==3)
gen d4=(focc==4)*(occ==4)
gen d5=(focc==5)*(occ==5)
xi: mclest i.occ d* i.occ|black i.occ|educ
<p>
The specification <b>(occ==</b><i>i</i><b>)</b> represents the reponse function for a particular
dummy variable. This dummy variable will produce the logit of landing in
category <i>i</i> versus some other category. So <b>d1</b> indicates the effect of father's
occupation being equal to 1 on the logit for the son being in category 1 versus
some other category.
<p>
As it turns out, once the data have been transformed into a <i>person choice file</i>,
mobility models can be specified in an <b>mcl</b> model in the same way they would be
in a loglinear model. So the much more compact specification:
<p>
gen diag=(focc==occ)*focc
xi: mclest i.occ i.diag i.occ|black i.occ|educ
<p>
Could be used as well. Examples for estimating a number of mobility models are
in the file <b>mobility.do</b> in the <b>desmat</b> package.  Use net get desmat to download
these ancillary files
<p>
<b><u>Stereotyped Ordered Regression</u></b>
<p>
<b>mclest</b> is also able to estimate certain special designs incorporating both
linear and multiplicative effects. One of these is the <i>Stereotyped Ordered</i>
<i>Regression model</i> (Anderson 1984, DiPrete 1990). The SOR model is an alternative
to the <i>proportional odds model</i> estimated by ologit. The SOR model estimates a
scaling metric for the response factor based on the effects of independent
variables. The model has <i>J</i>-1 intercept parameters for a response factor with <i>J</i>
categories, just like an unordered multinomial logit model. However, it has a
single <b>beta</b> parameter for each independent variable, together with <i>J</i>-2
independent <i>scale values</i> <b>phi[j]</b> for the response factor.
<p>
Two restrictions must be placed on the scaling metric in order to identify the
model. <b>mclest</b> sets the value for the first category to 0 and the value for the
last category to 1 while estimating the model. For the final estimates, the
scaling metric is also normalized, with a mean of 0 and a sum of squares of 1.
<p>
The SOR model can be specified as:
<p>
log(P(Y==q)/P(Y==r)) = a[q]-a[r] +
            (phi[q]-phi[r])(b[1]X[1]+b[2]X[2]+ ... +b[K]X[K])
<p>
<p>
Where
    <b>Y</b> is the response factor with categories <i>j</i>=1 to <i>J</i>,
    <b>q</b> and <b>r</b> are any two categories of <b>Y</b>,
    <b>a[j]</b> represents the intercept parameters with suitable restrictions,
    <b>phi[j]</b> represents the scaling metric with suitable restrictions,
    <b>X[k]</b> represents independent variables with <i>k</i>=1 to <i>K</i>, and
    <b>b[k]</b> represents parameters of the independent variables.
<p>
Compare this to a standard multinomial logistic model:
<p>
log(P(Y==q)/P(Y==r)) = a[q]-a[r] +
            (b[q1]-b[r1])X[1]+(b[q2]-b[r2])X[2]+ ... +(b[qK]-b[rK])X[K])
<p>
<p>
In a multinomial model, the difference between <b>b[qk]</b> and <b>b[rk]</b> show how the
<b>logit(q/r)</b> is affected by <b>X[k]</b>. In the SOR model, the degree of this effect
equals <b>(phi[q]-phi[r])b[k]X[k]</b>. The SOR model forces the effect on the logit
for any two outcomes to be proportional for all independent variables, with the
magnitude of the effect being determined by the <b>b[k]</b> parameters.
<p>
A SOR model can be requested by specifying a varlist in the <b>sor</b> option. A SOR
model with only one <b>X[k]</b> variable would be trivial and equivalent to standard
multinomial model since it contains the same number of parameters. A simple SOR
model with two variables could be specified as:
<p>
use logan
mclgen occ
<p>
xi: mclest i.occ, sor(educ black)
<p>
This model will contain 9 parameters: 4 intercept parameters, 3 independent
<b>phi[j]</b> parameters, and 2 <b>b[k]</b> parameters. This is only slightly 3 less than for
an unrestricted multinomial model. However, the parsimony of a SOR model does
increase as the number of <b>X[k]</b> variables increase.
<p>
The SOR model contains both linear and multiplicative elements. To estimate it,
<b>mclest</b> iteratively estimates MCL models, first taking the <b>phi[j]</b> scaling metric
as given and estimating the <b>b[k]</b> parameters, then taking the <b>b[k]</b> parameters as
given and estimating the <b>phi[j]</b> parameters. This continues until the change in
log likelihood between successive MCL models is less than the value specified
in the <b>sortol</b> option (defalut .0001) or the maximum number of iterations
specified in the <b>soriter</b> option is exceeded (default 20).
<p>
As a result of this estimation procedure, no standard errors can be given for
the <b>phi[j]</b> parameters and standard errors for the remaining parameters are
conditional, given the scaling metric. In addition, the model degrees of
freedom reported by <b>clogit</b> are not correct since the estimates for the scaling
metric are not taken into account. See the <b>Model fit information</b> at the end of
the output for the correct number of degrees of freedom. <b>e(df_m)</b> in the saved
results also contains the correct degrees of freedom.
<p>
<b><u>Row and Columns model 2</u></b>
<p>
A second special model that can be estimated by <b>mclest</b> is Goodman's (1979) Row
and Columns model 2. Originally developed for frequency tables, the RC2 model
estimates scaling metrics for both the dependent variable and one of the
independent variables. The association between the two variables can then be
expressed through a single parameter <b>mu</b>.  The scaling metric for the dependent
variable is <b>phi[j]</b> as in the SOR model, the scaling metric for the independent
variable is <b>sigma[v]</b>. Two restrictions must be imposed on <b>phi[j]</b> and <b>sigma[v]</b>
to identify the model. During estimation, <b>mclest</b> sets <b>phi</b>[1]=<b>sigma</b>[1]=0 and
<b>phi</b>[<b>J</b>]=<b>sigma</b>[<b>V</b>]=1. The final estimates are also given for normalized scale
values, where mean(<b>phi</b>[<b>j</b>])=<b>mean</b>(sigma[<b>v</b>])=0 and SS(<b>phi</b>[<b>j</b>])=SS(<b>sigma</b>[<b>v</b>])=1.
<p>
A model containing an RC2 effect could be specified as:
<p>
logit(q/r) = a[q]-a[r] + (phi[q]-phi[r])mu*sigma[v]
<p>
This model can be extended with standard and/or SOR effectsof independent
variables. Indeed, the RC2 effects can be seen as the SOR effects of a
categorical variable, scaled by <b>mu</b>*<b>sigma[v]</b>.
<p>
A variation of the RC2 model is the EQual Row and Columns model 2 (EQRC2),
which as the name suggests uses the same scale for the dependent variable and
the categorical independent.
<p>
logit(q/r) = a[q]-a[r] + (phi[q]-phi[r])mu*phi[v]
<p>
Another variation implemented in <b>mclest</b> allows the association mu between the
dependent and independent variable to vary by one or more other variables.
<p>
logit(q/r) = a[q]-a[r] + (phi[q]-phi[r])(mu[0]+mu[t]X[t])*phi[v]
<p>
An overall association parameter mu[0] is estimated, together with mu[t]
parameters indicating how the association changes for each independent variable
X[t], t=1 to T.
<p>
An RC2 model is requested by specifying a varname in the <b>rc2</b> option. At
present, only one variable can be used for the RC2 effect. Similarly, an EQRC2
model can be requested by specifying a varname in the <b>eqrc2</b> option. The <b>rc2</b> and
<b>eqrc2</b> options are mutually exclusive. To let the overall association vary by
one or more independent variables, specify a varlist in the <b>muby</b> option.
<p>
The following example estimates a quasi RC2 model for father's occupation,
including both effects for identical categories (diag) and an rc2 effect. The
overall association mu between father's occupation and respondent's occupation
is allowed to vary by race. Further more, race and education are included in
the model as covariates using a SOR effect.
<p>
use logan
mclgen occ
<p>
gen diag=(focc==occ)*focc
xi: mclest i.occ i.diag, sor(educ black) rc2(focc) muby(black)
<p>
Models containing RC2 or EQRC2 effects are estimated by iteratively running MCL
models, as is the case for SOR models. Convergence criterion and maximum
iterations are determined by the <b>sortol</b> and <b>soriter</b> options. As with SOR
models, no standard errors are available for the sigma[v] metric and other
standard errors are conditional on the <b>phi[j]</b> and <b>sigma[v]</b> estimates. See the
<b>Model fit information</b> at the end of the output for the correct number of model
degrees of freedom.
<p>
For tabular data, a separate rc2 program is also available from the ssc
archives. Note that <b>mclest</b> can estimate these models in many cases. The
differences are that <b>mclest</b> will not estimate the main effects of the row
variable and that interactions between the row variable and a grouping variable
must be included in <b>rc2</b> for the models to be equivalent. An advantage of <b>rc2</b> is
that it does not require a restructuring of the data by <b>mclgen</b>.
<p>
<b><u>Saved results</u></b>
<p>
In addition to the results saved by clogit, <b>mclest</b> saves the following
matrices:
<p>
<b>e(phi)</b>
    the phi scale with the first category fixed to 0, the last to 1
<p>
<b>e(phi_n)</b>
    the phi scale with mean 0 and sum of squares 1
<p>
<b>e(df_m)</b>
    the model degrees of freedom adjusted for the sigma and phi parameters
<p>
If the <b>rc2</b> or <b>eqrc2</b> option has been used, <b>mclest</b> also saves:
<p>
<b>e(sig)</b>
    the sigma scale with the first category fixed to 0, the last to 1
<p>
<b>e(sig_n)</b>
    the sigma scale with mean 0 and sum of squares 1
<p>
<b><u>References</u></b>
<p>
Anderson, J.A. (1984). Regression and Ordered Categorical Variables.  <i>Journal</i>
    <i>of the Royal Statistical Society</i>, Series B 46: 1-30.
<p>
Breen, Richard. (1994). Individual Level Models for Mobility Tables and Other
    Cross-Classifications. <i>Sociological Methods &amp; Research</i> 33:  147-173.
<p>
DiPrete, Thomas A. (1990). Adding Covariates to Loglinear Models for the Study
    of Social Mobility. <i>American Sociological Review</i> 55: 757-773.
<p>
Goodman, Leo A. (1979). Multiplicative models for the analysis of occupational
    mobility tables and other kinds of cross-classification tables. <i>American</i>
    <i>Journal of Sociology</i> 84: 804-819.
<p>
Hendrickx, John, Ganzeboom, Harry B.G. (1998). Occupational Status Attainment
    in the Netherlands, 1920-1990. A Multinomial Logistic Analysis. <i>European</i>
    <i>Sociological Review</i> 14: 387-403.
<p>
Hout, Michael. (1983). <i>Mobility Tables</i>. Sage Publication 07-031.
<p>
Logan, John A. (1983). A Multivariate Model for Mobility Tables.  <i>American</i>
    <i>Journal of Sociology</i> 89: 324-349.
<p>
Xie, Yu (2003). Association Model. In the <i>Encyclopedia of Social Science</i>
    <i>Research Methods</i>, edited by Michael Lewis-Beck, Alan Bryman and Tim Futing
    Liao. Thousand Oaks, Ca: Sage (2003).
<p>
http://www-personal.umich.edu/~yuxie/Research/Assoc-program.html
<p>
Direct comments to: John Hendrickx
<p>
<b>mclest</b> is available at SSC-IDEAS.  Use ssc <b>install mcl</b> to obtain the latest
version.
<p>
The packages rc2, desmat, xi3, are also available from the ssc archives.
<p>
<b><u>Also see</u></b>
On-line: help for mclgen, clogit, mlogit, ologit desmat, desrep, xi, xi3, rc2
</pre>