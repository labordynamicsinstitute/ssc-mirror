<pre>
-------------------------------------------------------------------------------
help for <b>descsave</b>                                                (Roger Newson)
-------------------------------------------------------------------------------
<p>
<p>
<b><u>Save descriptive attributes of variables to a do-file and/or a Stata dataset</u></b>
<p>
        <b>descsave</b> [<i>varlist</i>] [<b>using</b> <i>filename</i>] [ <b>,</b>
               <b><u>do</u></b><b>file(</b><i>dofilename</i> [<b>, replace</b>]<b>)</b>
               <b><u>li</u></b><b>st(</b> [<i>varlist</i>] [<i>if</i>] [<i>in</i>] [ , [<i>list_options</i>] ] <b>)</b>
               <b><u>sa</u></b><b>ving(</b><i>datafilename</i> [<b>, replace</b>]<b>)</b>
               <b>no</b><b><u>re</u></b><b>store</b> <b>fast</b> <b><u>fl</u></b><b>ist(</b><i>global_macro_name</i><b>)</b>
               <b><u>ch</u></b><b>arlist(</b><i>characteristic_list</i><b>)</b> <b><u>idn</u></b><b>um(</b><i>#</i><b>)</b> <b><u>ids</u></b><b>tr(</b><i>string</i><b>)</b>
               <b><u>ren</u></b><b>ame(</b><i>oldvarname_1 newvarname_1 ... oldvarname_n newvarname_n</i><b>)</b>
               <b><u>gs</u></b><b>ort(</b><i>gsort_list</i><b>)</b> <b><u>ke</u></b><b>ep(</b><i>varlist</i><b>)</b> ]
<p>
    where <i>list_options</i> is a list of options accepted by the <b>list</b> command,
    <i>characteristic_list</i> is a list of <i>characteristic_name</i>s and/or asterisks
    (<b>*</b>) separated by spaces, and <i>gsort_list</i> is a list of one or more elements
    of the form
<p>
        [<b>+</b>|<b>-</b>]<i>varname</i>
<p>
    as used by the <b>gsort</b> command.
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>descsave</b> is an extended version of <b>describe</b>, which lists descriptive
    attributes for a list of variables in a dataset given by <i>varlist</i>, or for
    all variables in the dataset if <i>varlist</i> is not specified.  The dataset is
    the current dataset in memory, unless <b>using</b> is used to specify a dataset
    in a file.  The descriptive attributes are variable names, storage types,
    display formats, value labels and variable labels (as output by 
    <b>describe</b>), and also (optionally) a list of characteristics specified by
    the <b>charlist()</b> option.  <b>descsave</b> creates an output Stata dataset (or
    resultsset) with one observation per variable and data on these
    descriptive attributes.  This dataset may be listed using the <b>list()</b>
    option and/or saved to a file using the <b>saving()</b> option and/or written to
    the memory using the <b>norestore</b> or <b>fast</b> option, overwriting any existing
    dataset.  The file specified by <b>dofile()</b> is a do-file, containing
    commands which can be run to reconstruct the descriptive attributes of
    the variables, assuming that variables of the same names have been
    created and are numeric or character as appropriate.  <b>descsave</b> can be
    used together with <b>outsheet</b> to create a definitive generic spreadsheet
    version of the current dataset, together with a Stata do-file to
    reconstruct the descriptive attributes of the variables after the
    spreadsheet has been input using <b>insheet</b>.
<p>
<b><u>Options</u></b>
<p>
    These options fall into the following 2 groups:
<p>
   Option group          Description
   --------------------------------------------------------------------------
   <i>outdest_opts</i>          Output-destination options for the do-file and/or
                           resultsset
   <i>conspec_opts</i>          Content-specifying options for the resultsset
   --------------------------------------------------------------------------
<p>
<p>
<p>
<a name="outdest_opts"></a><b><u>Output-destination options</u></b>
<p>
    <b>dofile(</b><i>dofilename</i> [<b>, replace</b>]<b>)</b> specifies an output Stata do-file, with
        commands to reconstruct the variable descriptive attributes (storage
        types, display formats, value labels, variable labels and selected
        characteristics), assuming that variables with those names already
        exist and are numeric or string-valued as appropriate.  If <b>replace</b> is
        specified, then any existing file of the same name is overwritten.
<p>
    <b>list(</b><i>varlist</i> [<b>if</b> <i>exp</i>] [<b>in</b> <i>range</i>] [, <i>list_options</i> ] <b>)</b> specifies a list of
        variables in the output dataset, which will be listed to the Stata
        log by <b>descsave</b>.  The user may optionally also specify <b>if</b> or <b>in</b>
        clauses to list subsets of variables, or change the display style
        using a list of <i>list_options</i> allowed as options by the <b>list</b> command.
        If the <b>rename()</b> option is specified (see below), then any variable
        names specified by the <b>list()</b> option must be the new names.  If the
        <b>list()</b> option is absent, then nothing is listed.
<p>
    <b>saving(</b><i>datafilename</i> [<b>, replace</b>]<b>)</b> specifies an output file containing a
        Stata dataset, with one observation per variable, and data on the
        descriptive attributes of the variable. If <b>replace</b> is specified, then
        any existing file of the same name is overwritten.
<p>
    <b>norestore</b> specifies that the output dataset will be written to the
        memory, overwriting any pre-existing dataset.  This option is
        automatically set if <b>fast</b> is specified.  Otherwise, if <b>norestore</b> is
        not specified, then the pre-existing dataset is restored in the
        memory after the execution of <b>descsave</b>.
<p>
    <b>fast</b> is a stronger version of <b>norestore</b>, intended for use by programmers.
        It specifies that the pre-existing dataset in the memory will not be
        restored, even if the user presses Break during the execution of
        <b>descsave</b>.  If <b>norestore</b> is specified and <b>fast</b> is absent, then
        <b>descsave</b> will go to extra work so that it can restore the original
        data if the user presses Break.
<p>
    Note that the user must specify at least one of the 5 options <b>dofile()</b>,
        <b>list()</b>, <b>saving()</b>, <b>norestore</b> and <b>fast</b>.
<p>
    <b>flist(</b><i>global_macro_name</i><b>)</b> specifies the name of a global macro, containing
        a filename list (possibly empty).  If <b>saving()</b> is also specified,
        then <b>descsave</b> will append the <i>filename</i> specified in the <b>saving()</b>
        option to the value of the global macro specified in <b>flist()</b>.  This
        enables the user to build a list of filenames in a global macro,
        containing the output of a sequence of estimation result sets saved
        by <b>descsave</b>.  These files may later be concatenated using <b>append</b>, or
        using <b>dsconcat</b> if installed.
<p>
<p>
<a name="conspec_opts"></a><b><u>Content-specifying ooptions</u></b>
<p>
    <b>charlist(</b><i>characteristic_list</i><b>)</b> specifies a list of characteristic names
        and/or asterisks (<b>*</b>), separated by spaces.  The characteristics
        specified will be reconstructed by the do-file specified by <b>dofile()</b>
        (if specified), and be written to variables in the output dataset.
        If a characteristic has length greater than the maximum length for a
        string variable, which is 244 in Version 9 of Stata (see help for 
        data_types), then it will be truncated to that maximum length in the
        output dataset and/or do-file.  (This is not expected to cause
        problems very often.) <b>descsave</b> expands the <i>characteristic_list</i> by
        replacing each asterisk <b>*</b> with a list of the names of all
        characteristics of all variables in the <i>varlist</i>, and then contracts
        the <i>characteristic_list</i> by removing the rightmost occurrences of all
        duplicate characteristic names.  Therefore, <b>charlist(*)</b> specifies a
        list of all characteristics belonging to all variables in the
        <i>varlist</i>, and <b>charlist(omit missing *)</b> specifies a list of the same
        characteristics, with <b>omit</b> appearing first and <b>missing</b> appearing
        second.  In the second case, the output variable <b>char1</b> will contain
        the <b>omit</b> characteristics, and the output variable <b>char2</b> will contain
        the <b>missing</b> characteristic.  (See <b>Output dataset created by descsave</b>
        below for details on output variables.)
<p>
    <b>idnum(</b><i>#</i><b>)</b> specifies an ID number for the output dataset.  It is used to
        create a numeric variable <b>idnum</b> in the output dataset, with that
        value for all observations.  This is useful if the output dataset is
        concatenated with other <b>descsave</b> output datasets using <b>append</b>, or
        using <b>dsconcat</b> if installed.
<p>
    <b>idstr(</b><i>#</i><b>)</b> specifies an ID string for the output dataset.  It is used to
        create a string variable <b>idstr</b> in the output dataset, with that value
        for all observations. (An output dataset may have <b>idnum</b>, <b>idstr</b>, both
        or neither.)
<p>
    <b>rename(</b><i>oldvarname_1 newvarname_1 ... oldvarname_n newvarname_n</i><b>)</b> specifies
        a list of pairs of variable names. The first variable name of each
        pair specifies a variable in the output dataset, which is renamed to
        the second variable name of the pair.  (See <b>Output dataset created by</b>
        <b>descsave</b> below for details on output variables.)
<p>
    <b>gsort(</b><i>gsort_list</i><b>)</b> specifies a generalized sorting order (as recognised by
        <b>gsort</b>) for the observations in the output dataset.  If <b>gsort()</b> is not
        specified, then the output dataset will be sorted by the single
        variable <b>order</b>.  If <b>rename()</b> is specified, then <b>gsort()</b> must use the
        new names.
<p>
    <b>keep(</b><i>varlist</i><b>)</b> specifies a list of variables to be kept in the output
        dataset.  If <b>keep()</b> is not specified, then the output dataset
        contains all the variables listed in the next section.  If <b>rename()</b>
        is specified, then <b>keep()</b> must use the new names.
<p>
<p>
<b><u>Output dataset created by </u></b><b><u>descsave</u></b>
<p>
    The output dataset (or resultsset) created by <b>descsave</b> has one
    observation per variable in the <i>varlist</i>.  If the <b>rename()</b> option is not
    specified, then it contains the following variables:
<p>
   Default name    Description
   --------------------------------------------------------------------------
   <b>idnum</b>           Numeric dataset ID
   <b>idstr</b>           String dataset ID
   <b>order</b>           Variable order
   <b>name</b>            Variable name
   <b>type</b>            Storage type
   <b>format</b>          Display format
   <b>vallab</b>          Value label
   <b>varlab</b>          Variable label
   <b>char</b><i>n</i>           char[<i>characteristic_name</i>]
   --------------------------------------------------------------------------
<p>
<p>
    The variable <b>order</b> contains the sequential order of the variable in the
    input <i>varlist</i> specified for <b>descsave</b>, or the order of that variable in
    the dataset, if the user does not specify an input <i>varlist</i>.  The
    variables <b>idnum</b> or <b>idstr</b> are only present if requested in the options of
    the same names.  There is one <b>char</b><i>n</i> variable for each <i>characteristic_name</i>
    in the list specified by the <b>charlist()</b> option. The variable <b>char</b><i>n</i>
    specifies the <i>n</i>th characteristic specified in the <b>charlist()</b> option
    (truncated if necessary to the maximum length for a string variable under
    the current version of Stata). All of these variables can be renamed
    using the <b>rename()</b> option, or used by the <b>gsort()</b> option to specify the
    sorting order.  If the <b>keep()</b> option is used, then the output dataset
    will contain only the specified subset of these variables.
<p>
<p>
<b><u>Remarks</u></b>
<p>
    <b>descsave</b> can be used together with <b>outsheet</b> and <b>insheet</b> to construct a
    definitive generic spreadsheet version of the data. This is useful if the
    user needs either to convert the data to distant past versions of Stata
    not produced by <b>saveold</b>, or to return to the data decades into the
    future, when all proprietary software has evolved beyond recognition. The
    do-file specified by <b>dofile()</b> can be used to reconstruct variable
    attributes after inputting the definitive version of the data using 
    <b>insheet</b>, assuming that the variables are still numeric or string-valued,
    as specified in the original Stata data.  (The user may need to use 
    <b>destring</b> after using <b>insheet</b>, if some of the numeric variables in the
    definitive generic spreadsheet are formatted in nonstandard ways.) The
    output do-file can also be translated manually into other software
    languages if the user wants to use the data under other software
    platforms. However, <b>descsave</b> can also be used with the <b>parmest</b> and 
    <b>factext</b> packages (see help for <b>parmby</b>, <b>parmest</b> or <b>factext</b> if installed).
    Typically, the user uses <b>descsave</b> to save to a do-file the attributes of
    variables representing categorical factors, generates dummy variables for
    these categorical factors using <b>tabulate</b> or <b>xi</b>, enters these dummy
    variables into a regression analysis, saves the results of the regression
    to a dataset using <b>parmby</b> or <b>parmest</b>, and then reconstructs the
    categorical factors from the variable <b>label</b> in the <b>parmest</b> output dataset
    using the <b>factext</b> package.
<p>
<p>
<b><u>Examples</u></b>
<p>
                . descsave, list(,)
<p>
                . descsave make mpg weight, list(name varlab vallab, clean
                    noobs)
<p>
                . descsave, list(, subvar noobs sepa(0) abbrev(32)) char(omit)
<p>
                . descsave, do(auto.do, replace)
<p>
                . descsave, saving(autodesc.dta, replace)
<p>
                . descsave, list(, noobs abb(32)) do(auto.do, replace)
                    saving(autodesc.dta, replace) rename(varlab variable_label
                    format variable_format)
<p>
                . descsave using auto2, list(,)
<p>
                . descsave model mpg price using auto2, list(,)
                    saving(auto2desc, replace)
<p>
                . descsave, norestore
<p>
    The following example will work in the <b>auto</b> data. The first part creates
    a generic text spreadsheet in <b>auto.txt</b>, with a program to reconstruct the
    variable attributes in <b>auto.do</b>. The second part reconstructs the <b>auto</b>
    data from <b>auto.txt</b>, using <b>auto.do</b>.
<p>
                . descsave, do(auto.do, replace) sa(autodesc.dta, replace)
                    charlist(omit *)
                . outsheet using auto.txt, nolabel replace
<p>
                . insheet using auto.txt, clear
                . run auto.do
                . describe
<p>
    The following example will work in the <b>auto</b> data if the packages <b>parmest</b>,
    <b>factext</b> and <b>eclplot</b> are installed.  All of these packages can be
    downloaded from SSC.
<p>
                . tab foreign, gene(type_) nolabel
                . qui descsave foreign, do(foreign.do, replace)
                . parmby "regress mpg type_*, noconst robust", label norestore
                . factext foreign, do(foreign.do)
                . eclplot estimate min95 max95 foreign, xscal(range(-1 2))
                    xlab(0 1)
<p>
    The following advanced example will work under Stata 8 or above in the
    <b>auto</b> data if the <b>dsconcat</b> and <b>xcollapse</b> packages are installed. Both
    packages can be downloaded from SSC.  The example creates a dataset with
    1 observation for each of a list of variables and data on their names and
    median values, using <b>xcollapse</b> and <b>dsconcat</b>, and then uses <b>merge</b> to merge
    in a dataset created by <b>descsave</b>, with 1 observation per variable and
    data on the variable names, variable labels and display formats.
<p>
                . tempfile tf0
                . descsave price mpg headroom trunk weight length turn
                    displacement gear_ratio, saving(`tf0', replace) gsort(name)
                    keep(order name varlab format)
                . global tflist ""
                . local i1=0
                . foreach X of var price mpg headroom trunk weight length turn
                    displacement gear_ratio {
                .  local i1=`i1'+1
                .  tempfile tf`i1'
                .  xcollapse (median) med=`X', idstr("`X'") nidstr(name)
                    saving(`tf`i1'', replace) flist(tflist)
                . }
                . dsconcat $tflist
                . sort name
                . lab var med "Median value"
                . merge name using `tf0'
                . sort order
                . list order name varlab med
<p>
<p>
<b><u>Author</u></b>
<p>
    Roger Newson, Imperial College London, UK.  Email:
    r.newson@imperial.ac.uk
<p>
<p>
<b><u>Also see</u></b>
<p>
     Manual: <b>[D] describe</b>, <b>[D] destring</b>,<b> [D] gsort</b>, <b>[D] insheet</b>, <b>[D] label</b>,
             <b>[D] outsheet</b>
             <b>[R] tabulate</b>, <b>[R] xi</b>
             <b>[U] 12.8 Characteristics</b>
             <b>[P] char</b>
    On-line: help for <b>append</b>, <b>char</b>, <b>describe</b>, <b>destring</b>, <b>gsort</b>, <b>insheet</b>, 
             <b>label</b>, <b>outsheet</b>, <b>saveold</b>, <b>tabulate</b>, <b>xi</b>
             help for <b>dsconcat</b>, <b>eclplot</b>, <b>factext</b>, <b>parmby</b>, <b>parmest</b>, <b>xcollapse</b>
             if installed
</pre>