<pre>
-------------------------------------------------------------------------------
help for <b>estwrite</b>
-------------------------------------------------------------------------------
<p>
<b><u>Command to backup model estimates on disk</u></b>
<p>
<p>
        <b>estwrite</b> [<i>namelist</i> <b>using</b>] <i>filename</i> [<b>,</b> <b>id(</b><i>varname</i><b>)</b> <b>alt</b> <b>estsave</b> <b><u>r</u></b><b>eplace</b>
               <b><u>a</u></b><b>ppend</b> ]
<p>
        <b>estread</b> [<i>namelist</i> <b>using</b>] <i>filename</i> [<b>,</b> <b>id(</b><i>varname</i><b>)</b> <b>estsave</b> <b><u>d</u></b><b>escribe</b> ]
<p>
<p>
    where <i>namelist</i> is a name, a list of names, or <b>_all</b>; the <b>*</b> and <b>?</b> wildcards
    are allowed. A name may also be <b>.</b>, meaning the current (active)
    estimates.
<p>
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>estwrite</b> exports models previously fitted and stored by <b>estimates store</b>
    to <i>filename</i> on disk.  <i>namelist</i> provides the names of the stored
    estimation sets to be exported. You may use the <b>*</b> and <b>?</b> wildcards in
    <i>namelist</i>. The results estimated last may be indicated by a period (<b>.</b>),
    even if they have not yet been stored. If no model is specified, <b>estwrite</b>
    uses the model fitted last.
<p>
    <b>estread</b> reimports the saved estimation sets from <i>filename</i>. Specify
    <i>namelist</i> to select the estimation sets to be imported. The default is to
    import all saved estimation sets.
<p>
    In both cases, if <i>filename</i> is specified without an extension, <b>.sters</b> is
    assumed.
<p>
    <b>estwrite</b> uses the technology behind <b>estimates save</b> to save the estimation
    sets, but has the following advantages over <b>estimates save</b>:
<p>
        (1) Multiple sets may be stored in one file. (A similar feature has
            been added to <b>estimates save</b> in Stata 11. However, <b>estwrite</b>
            supports named sets and allows storing or retrieving multiple
            sets in one call.)
<p>
        (2) The <b>e(sample)</b> (estimation sample) may be stored and recovered
            using the <b>id()</b> option.
<p>
<p>
        (3) <b>e()</b>-returns that have a name starting with an underscore (e.g.
            <b>e(_abc)</b>) are preserved.
<p>
    <b>estwrite</b> can also be used in Stata 8 and Stata 9, where the <b>estimates</b>
    <b>save</b> command is not available.
<p>
         o  In Stata 9 (or if option <b>alt</b> is specified), <b>estwrite</b> uses its
            own Mata-based algorithm to store the sets (using the same file
            format as with the default method).
<p>
         o  In Stata 8 (or if option <b>estsave</b> is specified), the estimates
            are stored in a Stata dataset using Michael Blasnik's <b>estsave</b>
            command, which must be installed on the system in this case
            (see ssc describe estsave). <b>.dta</b> is assumed, if <i>filename</i> is
            specified without an extension.
<p>
<p>
<b><u>Technical note</u></b>
<p>
    In Stata 8 and Stata 9 (or if option <b>alt</b> or option <b>estsave</b> is specified)
    a restored model might differ from the original model in terms of the
    model's representation in memory (although the represented information is
    the same). This has to due with the fact that some of Stata's estimation
    commands (such as, e.g., <b>regress</b>) post their results in <b>e()</b> in a special
    way that cannot be reproduced by <b>ereturn post</b>, which is used by <b>estwrite</b>
    to restore the results in Stata 8 and Stata 9.
<p>
    It may not be possible to replay the full standard output of such a model
    after importing the model by <b>estread</b> (for example, typing <b>regress</b> without
    arguments returns error on an imported model that was fitted by <b>regress</b>).
    However, all results are, in fact, preserved and post-estimation commands
    such as <b>test</b> or <b>estimates table</b> should work as usual.
<p>
<p>
<b><u>Options</u></b>
<p>
    <b>id(</b><i>varname</i><b>)</b> specifies a unique identifier for the observations and causes
        the <b>e(sample)</b> to be stored with the estimates. <b>id()</b> specified with
        <b>estread</b> reimports such estimates.
<p>
    <b>estsave</b> specifies that the estimates be stored in a Stata dataset using
        Michael Blasnik's <b>estsave</b> command. <b>estsave</b> is default in Stata 8.
<p>
    <b>alt</b> causes the estimation sets to be stored without making use of the
        technology behind <b>estimates save</b>. <b>alt</b> is usually a bit faster than
        the default method and also results in somewhat smaller file sizes. A
        disadvantage is, however, that full output may not be replayed for
        some commands after reimporting the models (see the technical note
        above). Specify the <b>alt</b> option in Stata 10 if you want to be able to
        restore the saved models in Stata 9. Similarly, specify <b>alt</b> in Stata
        11 if you want to be able to restore the saved models in Stata 10 or
        Stata 9. <b>alt</b> is default the Stata 9.
<p>
    <b>replace</b> permits <b>estwrite</b> to overwrite an existing file.
<p>
    <b>append</b> causes <b>estwrite</b> to add the estimation sets to an existing file.
        Make sure to use unique names for the added estimation sets. Models
        saved with or without the <b>alt</b> option may be combined in a single
        file. The <b>id()</b> option has to be specified if the file to append to
        was created with <b>id()</b> option (and the values of the ID variables have
        to coincide). Similarly, the <b>id()</b> option is not allowed if the file
        to append to was created without <b>id()</b> option.
<p>
    <b>describe</b> causes <b>estread</b> to display information on the contents of
        <i>filename</i> without importing the estimation sets.
<p>
<p>
<b><u>Returned results</u></b>
<p>
    <b>estwrite</b> and <b>estread</b> return in <b>r(names)</b> the names of the
        exported/imported estimation sets.
<p>
<p>
<b><u>Examples</u></b>
<p>
    Basic usage:
<p>
        . sysuse auto
        (1978 Automobile Data)
<p>
        . regress mpg price weight
        (output omitted)
<p>
        . estimates store model1, title(Model 1)
        
        . logit foreign mpg weight
        (output omitted)
<p>
        . estimates store model2, title(Model 2)
        
        . estwrite * using mymodels
        (saving model1)
        (saving model2)
        (file mymodels.sters saved)
<p>
        . estimates clear
        
        . estread mymodels
        
        -------------------------------------------------------
                name | command      depvar       npar  title
        -------------+-----------------------------------------
              model1 | regress      mpg             3  <i>Model 1</i>
              model2 | logit        foreign         3  <i>Model 2</i>
        -------------------------------------------------------
<p>
<p>
    Add models to an existing file:
<p>
        . regress mpg price weight foreign displ
        (output omitted)
<p>
        . estimates store model3, title(Extended Model 1)
        
        . estwrite model3 using mymodels, append
        (appending model3)
        (file mymodels.sters saved)
<p>
        . estimates clear
        
        . estread mymodels
        
        ---------------------------------------------------------------
                name | command      depvar       npar  title
        -------------+-------------------------------------------------
              model1 | regress      mpg             3  <i>Model 1</i>
              model2 | logit        foreign         3  <i>Model 2</i>
              model3 | regress      mpg             5  <i>Extended Model 1</i>
        ---------------------------------------------------------------
<p>
<p>
    Preserve the <b>e(sample)</b>:
<p>
        . sysuse auto
        (1978 Automobile Data)
<p>
        . count
           74
        
        . mlogit rep78 mpg price weight if rep78&gt;=3
        (output omitted)
<p>
        . count if e(sample)
           59
        
        . estwrite . using mymodels, replace id(make)
        (saving active estimates)
        (file mymodels.sters saved)
<p>
        . estimates clear
        
        . estread mymodels, id(make)
        
        -------------------------------------------------------
                name | command      depvar       npar  title
        -------------+-----------------------------------------
                   . | mlogit       rep78           8  
        -------------------------------------------------------
<p>
        . count if e(sample)
           59
        
<p>
<b><u>Author</u></b>
<p>
    Ben Jann, ETH Zurich, jannb@ethz.ch
<p>
    Thanks for citing this software as follows:
<p>
        Jann, B. 2005. estwrite: Stata module to store estimation results on
        disk. Available from
        http://ideas.repec.org/c/boc/bocode/s450201.html.
<p>
<p>
<b><u>Also see</u></b>
<p>
    Manual:  <b>[R] estimates</b>
<p>
</pre>