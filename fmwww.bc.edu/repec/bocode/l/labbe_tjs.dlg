/* Dialog (Version 1.0.0) by: T. J. Steichen, steicht@rjrt.com 

laabe      VERSION 1.1 May 2001   

L'abbe Graph
------------

Syntax:  labbe varlist [if exp] [in range] [weight] , [ nowt percent
              or(#) rr(#) rd(#) rrn(#) null graph options ]
 
To install in User Statistics menu via Stata commands:
  . window menu append item "stUserStatistics" "Metan-based L'abbe Graph (&labbe)" "db labbe"
  . window menu refresh
To permanently install, place the commands in your -profile.do- file.

*/

VERSION 8.0

INCLUDE _std_small
INCLUDE header

HELP hlp1, view("help metan")
RESET res1, label("Reset")

DIALOG main, label("labbe 1.1 - Metan-based L'abbe Graph") tabtitle("Main")
BEGIN
  TEXT     tx_binary    10    5  330      .,            ///
     label("Vars for Counts a, b, c, d, in that order")
  VARLIST  vl_binary     @   _ss   @      .,            ///
     label("Vars for a, b, c, d")

/*  end vars  */

  CHECKBOX cb_nowt      10   50  130      .,            ///
     label("Symbols not sized")                         ///
     option("nowt")

  CHECKBOX cb_percent   10   70  130      .,            ///
     label("Event Rates in %")                          ///
     option("percent")
  
  CHECKBOX cb_null      10  100  130      .,            ///
     label("Draw p1=p2 line")                           ///
     option("null")

/*  end switches  */

  GROUPBOX gb_lines    170   48  175    108,            ///
     label("Draw Lines at:")

  CHECKBOX cb_or       190  +20   80      .,            ///
     label("OR:    (>0)")                               ///
     onclickon(main.ed_or.enable)                       ///
     onclickoff(main.ed_or.disable)
  EDIT     ed_or       275    @   50      .,            ///
     label("or")                                        ///
     numonly                                            ///
     option("or")

  CHECKBOX cb_rd       190  +20   80      .,            ///
     label("RD:  (-1,1)")                               ///
     onclickon(main.ed_rd.enable)                       ///
     onclickoff(main.ed_rd.disable)
  EDIT     ed_rd       275    @   50      .,            ///
     label("rd")                                        ///
     numonly                                            ///
     option("rd")

  CHECKBOX cb_rr       190  +20   80      .,            ///
     label("RR:    (>0)")                               ///
     onclickon(main.ed_rr.enable)                       ///
     onclickoff(main.ed_rr.disable)
  EDIT     ed_rr       275    @   50      .,            ///
     label("rr")                                        ///
     numonly                                            ///
     option("rr")

  CHECKBOX cb_rrn      190  +20   80      .,            ///
     label("RRN: (>0)")                                 ///
     onclickon(main.ed_rrn.enable)                      ///
     onclickoff(main.ed_rrn.disable)
  EDIT     ed_rrn      275    @   50      .,            ///
     label("rrn")                                       ///
     numonly                                            ///
     option("rrn")

/*  end sets  */

  GROUPBOX gb_gopts7    10 155  330   _ht1h,            ///
     label("Allowed Graph7 Options:")
  EDIT     ed_gopts7    15 175  320       .,            ///
     label("Graph7 Options")

/*  end Graph7 opts  */

END

INCLUDE ifin
INCLUDE weights_a

SCRIPT vars_on
BEGIN
  main.tx_effect.enable
  main.vl_effect.enable
END

SCRIPT vars_off
BEGIN
  main.tx_effect.disable
  main.vl_effect.disable
END


PROGRAM command
BEGIN
  put "labbe "
  varlist main.vl_binary
  INCLUDE _weights_pr
  INCLUDE _ifin_pr
  beginoptions
    option main.cb_nowt
    option main.cb_percent
    option main.cb_null
    optionarg main.ed_or
    optionarg main.ed_rd
    optionarg main.ed_rr
    optionarg main.ed_rrn
    put main.ed_gopts7
  endoptions
END
