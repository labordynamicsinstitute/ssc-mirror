*! 1.0 - 29 May 2022
*!
*! this is a subprogram that is part of the shell wrapper package -inshell-
*! it is meant to process files generated by shell commands that produce output
*! containing ANSI escape sequences. This program removes that formatting,
*! generating a new, "cleaned" file for -inshell- to parse

capture program drop _inshell_deansify

program define _inshell_deansify, rclass

  syntax, file(string) [ error ]
  version 9

  tempname  deansify   deansify_nosmcl
  tempfile  deansifyf  deansifyf_nosmcl
  quietly file open `deansify' using "`deansifyf'", write replace
  local ln = 0
  tempname filen
  quietly file open `filen' using "`file'", read
  if "`error'" != "" {
    quietly file open `deansify_nosmcl' using "`deansifyf_nosmcl'", write replace
    file write `deansify' "{err}{...}" _n
  }
  file read `filen' line
  while r(eof) == 0 {
    if strlen("`macval(line)'") != 0 {
      local ++ln
      local line2 = subinstr(ustrregexra("`macval(line)'", "\x1b\[([0-9]{1,2}(;[0-9]{1,2})*)?m" , ""),  char(10), " ", .)
      file write `deansify' `"`macval(line2)'"' _n
      if "`error'" != "" file write `deansify_nosmcl' `"`macval(line2)'"' _n
    }
    file read `filen' line
  }

  capture file close `filen'
  capture file close `deansify'
  local outf "`c(tmpdir)'inshell_deansified_`=now()'.txt"
  copy "`deansifyf'" "`outf'"
  return local outfile "`outf'"

  if "`error'" != "" {
    capture quietly file close `deansify_nosmcl'
    local outf_nosmcl "`c(tmpdir)'inshell_deansified_`=now()'_nosmcl.txt"
    capture quietly copy "`deansifyf_nosmcl'" "`outf_nosmcl'"
    return local outfile_nosmcl "`outf_nosmcl'"
  }

end
