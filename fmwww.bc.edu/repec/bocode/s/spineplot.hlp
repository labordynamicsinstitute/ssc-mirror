{smcl}
{* 5nov2007}{...}
{hline}
help for {hi:spineplot}
{hline}

{title:Spine plots for two-way categorical data} 

{p 8 17 2} 
{cmd:spineplot} 
{it:yvar} {it:xvar} 
[{it:weight}]
[{cmd:if} {it:exp}] 
[{cmd:in} {it:range}]
[ 
{cmd:,}
{cmd:bar1(}{it:twoway_bar_options}{cmd:)} ... 
{cmd:bar20(}{it:twoway_bar_options}{cmd:)} 
{cmd:barall(}{it:twoway_bar_options}{cmd:)}
{cmdab:miss:ing}
{cmdab:perc:ent}
{cmd:text(}{it:textvar} [{cmd:,} {it:marker_label_options}]{cmd:)} 
{it:twoway_options}
]


{title:Description}

{p 4 4 2}
{cmd:spineplot} produces a spine plot for two-way categorical data.  The
fractional breakdown of the categories of the first-named variable
{it:yvar} is shown for each category of the second-named variable
{it:xvar}. Stacked bars are drawn with vertical extent showing
fraction({it:yvar} category {c |} {it:xvar} category) and horizontal
extent showing fraction in {it:xvar} category. Thus the areas of tiles
formed represent the frequencies, or more generally totals, for each
cross-combination of {it:yvar} and {it:xvar}. 

{p 4 4 2}
{cmd:fweight}s and {cmd:aweight}s may be specified. 


{title:Remarks} 

{p 4 4 2}
The name "spine plot" is due to Hummel (1996). The term is not yet
widespread but appears already to be variously understood.  Textbooks
and monographs with examples of spine plots and related plots include
Friendly (2000), Venables and Ripley (2002), Robbins (2005), Unwin et
al. (2006) and Young et al. (2006).  Among several papers, Hofmann's
(2000) discussion is clear, concise and well-illustrated. 

{p 4 4 2}
Some literature treats spine plots, as understood here, under the
heading of mosaic plots, variously with and without also using the term
spine plot.  The original definition of spine plots appears to have
allowed at most vertical subdivision (e.g. by highlighting) into two
categories, but this stipulation is also widely ignored as being unduly
restrictive.  The Stata implementation here under the name
{cmd:spineplot} thus implies a broad interpretation of the term.
Conversely, this implementation does not purport to be a general mosaic
plot program. 

{p 4 4 2}
Mosaic plots have been re-invented several times under different names.
Hartigan and Kleiner (1981, 1984) introduced, or re-introduced, them
into mainstream statistics. Friendly (2002) cites earlier examples,
including the work of Georg von Mayr (1877), Karl G. Karsten (1923) and
Erwin J. Raisz (1934).  Hofmann (2007) discusses a mosaic by Francis A.
Walker (1874).  Other early examples are those of Willard C. Brinton
(1914, quoting earlier work) and Berend G. Escher (1934). 

{p 4 4 2}
Most implementations of mosaic plots omit axes and numerical scales and
convey a recursive subdivision according to what may be several
categorical variables by a hierarchy of gaps of various sizes. As the
plot here is restricted to two variables, this Stata implementation
keeps axes and numerical scales as defaults.  The distinction between
categories is conveyed by bar boundaries rather than explicit gaps. 

{p 4 4 2}
A key principle behind any kind of mosaic plot is that a categorical
classification of independent variables would yield tiles that align
consistently. Thus departures from independence, or relationships between
variables, will be shown by failure of alignment. 

{p 4 4 2}
The restriction to two variables is more apparent than real.  Composite
variables may be created by cross-combination of two or more categorical
variables. The {help egen} functions {cmd:group()} and {cmd:axis()} may
be useful for this purpose. {cmd:axis()} is in the {cmd:egenmore}
package from SSC and must have been installed previously. Compare also
what Wilkinson (2005) calls "region trees" (and his references). 

{p 4 4 2}
The program works by calculating cumulative frequencies. The plot is then
produced by overlaying distinct graphs, each a call to 
{cmd:twoway bar, bartype(spanning)} for one category of {it:yvar}. By
default, each bar is shown with {cmd:blcolor(bg) blw(medium)}, which
should be sufficient to outline each bar distinctly but delicately.  By
default also, the categories of {it:yvar} will be distinguished
according to the graph scheme you are using.  With the default
{cmd:s2color} scheme the effect is reminiscent of tinned fruit salad,
which may be fine for exploratory work.  For a publishable graph you
might want to reach for something more subdued, such as various grey
scales.

{p 4 4 2} 
Options {cmd:bar1()} to {cmd:bar20()} are provided to allow overriding
the defaults on up to 20 categories, the first, second, etc., shown.
The limit of 20 is plucked out of the air as more than any user should
really want. The option {cmd:barall()} is available to override the
defaults for all bars. Any {cmd:bar}? option always overrides
{cmd:barall()}. Thus if you wanted thicker {cmd:blwidth()} on all bars
you could specify {cmd:barall(blwidth(thick))}. If you wanted to
highlight the first category only you could specify
{cmd:bar1(blwidth(thick))}. 

{p 4 4 2} 
Other defaults include {cmd:legend(col(1) pos(3))}.  At least with
{cmd:s2color} a legend on the right implies an approximately square plot
region which can look quite good. A legend is supplied partly because 
there is no guarantee that all {it:yvar} categories will be represented
for extreme categories of {it:xvar}. However, it will often be possible 
and tasteful to omit the legend and show categories as axis label text. 
An example is given below. 

{p 4 4 2}
Note the possibility of using {cmd:plotregion(margin(zero))} to 
place axes alongside the plot region. 

{p 4 4 2}
As with scatter plots, a response variable is usually better shown on
the {it:y} axis.  If one variable is binary, it is often better to plot that
on the {it:y} axis. Naturally, there can be some tension between these 
suggestions. 
For example, in the auto data, {cmd:foreign} is arguably a predictor 
of {cmd:rep78} rather than vice versa, but I suggest that 
{cmd:spineplot foreign rep78} is more congenial than 
{cmd:spineplot rep78 foreign}. 

{p 4 4 2}
The user may need to experiment with different sort orders 
for the categorical variables. {cmd:egen, axis()} may again 
be useful here. 


{title:Options} 

{p 4 8 2} 
{cmd:bar1(}{it:twoway_bar_options}{cmd:)} ...
{cmd:bar20(}{it:twoway_bar_options}{cmd:)} allow specification of the
appearance of the bars for each category of {it:yvar} using options of
{help twoway bar}. 

{p 4 8 2}
{cmd:barall(}{it:twoway_bar_options}{cmd:)} allows specification of the
appearance of the bars for all categories of {it:yvar} using options of
{help twoway bar}. 

{p 4 8 2}
{cmd:missing} specifies that any missing values of either of the
variables specified should also be included within their own categories.
The default is to omit them. 

{p 4 8 2}
{cmd:percent} specifies labelling in terms of percents. The default is
labelling in terms of fractions. 

{p 4 8 2}
{cmd:text(}{it:textvar} [{cmd:,} {it:marker_label_options}]{cmd:)}
specifies a variable to be shown as text at the centre of 
each tile. {it:textvar} may be a numeric or string variable. It should
contain identical values for all observations in each
cross-combination of {it:yvar} and {it:xvar}. A simple example is the 
frequency of each cross-combination. To show nothing in 
particular tiles, use a variable with missing values, either numeric 
missing or empty strings, for those tiles. 
A numeric variable with fractional part will typically look best converted 
to string as (e.g.) {cmd:string(}{it:residual}{cmd:, "%4.3f")}. 
Choice of tile colours so that text is readable is the user's
responsibility. {cmd:text()} may also include 
{help marker_label_options:marker label options} tuning the display. 

{p 4 8 2}
{it:twoway_options} refers to options of {help twoway}.  Note that by
default there are two {it:x} axes, {cmd:axis(1)} on top and {cmd:axis(2)} on
bottom, and two {it:y} axes, {cmd:axis(1)} on right and {cmd:axis(2)} on left. 


{title:Examples}

{p 4 8 2}{cmd:. sysuse auto}{p_end}
{p 4 8 2}{cmd:. spineplot foreign rep78}{p_end}
{p 4 8 2}{cmd:. spineplot foreign rep78, xti(frequency, axis(1)) xla(0(10)60, axis(1)) xmti(1/69, axis(1))}{p_end}
{p 4 8 2}{cmd:. spineplot rep78 foreign}

{p 4 8 2}{cmd:. set scheme s1color}{p_end}
{p 4 8 2}{cmd:. bysort foreign rep78: gen freq = _N}{p_end}
{p 4 8 2}{cmd:. spineplot foreign rep78, text(freq, mlabsize(*1.4)) bar1(color(gs14)) bar2(color(gs10))}{p_end}
{p 4 8 2}{cmd:. spineplot foreign rep78, text(freq, mlabsize(*1.4)) bar1(color(gs14)) bar2(color(gs10)) legend(off) yla(0.1 "Domestic" 0.9 "Foreign", noticks axis(1))}{p_end}


{title:Author}

{p 4 4 2}Nicholas J. Cox, Durham University{break} 
n.j.cox@durham.ac.uk

	 
{title:Acknowledgments} 

{p 4 4 2}
Matthias Schonlau, Scott Merryman and Maarten Buis provoked this program
through challenging Statalist postings, which re-awakened a
long-standing thought that someone, perhaps me, should implement spine
plots in Stata. A suggestion from Peter Jepsen led to the {cmd:text()}
option. Private emails from Matthias Schonlau 
and Antony Unwin highlighted different senses of spine plots and the 
importance of sort order. Vince Wiggins originally told me about the undocumented 
{cmd:bartype(spanning)} option.


{title:References}

{p 4 8 2}
Anderson, M.J. 2001. 
Francis Amasa Walker. 
In Heyde, C.C. and Seneta, E. (eds)
{it:Statisticians of the centuries.} 
New York: Springer, 216{c -}218. 

{p 4 8 2}
Brinton, W.C. 1914. 
{it:Graphic methods for presenting facts.} 
New York: Engineering Magazine Company. 

{p 4 8 2}
Escher, B.G. 1934. 
{it:De methodes der grafische voorstelling.} 
Amsterdam: Wereldbibliotheek. [first edition 1924] 

{p 4 8 2}
Friendly, M. 2000.  
{it:Visualizing categorical data.} 
Cary, NC: SAS Institute. 

{p 4 8 2} 
Friendly, M. 2002. 
A brief history of the mosaic display.  
{it:Journal of Computational and Graphical Statistics} 
11: 89{c -}107. 

{p 4 8 2}
Hartigan, J.A. and Kleiner, B. 1981. 
Mosaics for contingency tables. 
In Eddy, W.F. (ed.) 
{it:Computer science and statistics: Proceedings of the 13th symposium on the interface.} 
New York: Springer, 268{c -}273. 

{p 4 8 2}
Hartigan, J.A. and Kleiner, B. 1984. 
A mosaic of television ratings. 
{it:American Statistician} 38: 32{c -}35. 

{p 4 8 2}
Hertz, S. 2001. 
Georg von Mayr.
In Heyde, C.C. and Seneta, E. (eds)
{it:Statisticians of the centuries.} 
New York: Springer, 219{c -}222. 

{p 4 8 2}
Hofmann, H. 2000. 
Exploring categorical data: interactive mosaic plots. 
{it:Metrika} 51: 11{c -}26. 

{p 4 8 2}
Hofmann, H. 2007. 
Interview with a centennial chart. 
{it:Chance} 20(2): 26{c -}35. 

{p 4 8 2} 
Hummel, J. 1996.
Linked bar charts: analysing categorical data graphically.
{it:Computational Statistics} 11: 23{c -}33.

{p 4 8 2}
Karsten, K.G. 1923. 
{it:Charts and graphs: An introduction to graphic methods in the control and analysis of statistics.} 
New York: Prentice-Hall. 

{p 4 8 2}
Mayr, G. von. 1877. 
{it:Die Gesetzm{c a:}ssigkeit im Gesellschaftleben.} 
M{c u:}nchen: Oldenbourg. 

{p 4 8 2}
Raisz, E.J. 1934. 
The rectangular statistical cartogram. 
{it:Geographical Review} 24: 292{c -}296. 

{p 4 8 2}
Robbins, N.B. 2005. 
{it:Creating more effective graphs.} 
Hoboken, NJ: John Wiley. 

{p 4 8 2}
Robinson, A.H. 1970. 
Erwin Josephus Raisz, 1893{c -}1968. 
{it:Annals, Association of American Geographers} 
60: 189{c -}193. 

{p 4 8 2}
Unwin, A., Theus, M. and Hofmann, H. 2006.  
{it:Graphics of large datasets: Visualizing a million.}
New York: Springer. 

{p 4 8 2}
Venables, W.N. and Ripley, B.D. 2002. 
{it:Modern applied statistics with S.} 
New York: Springer. 

{p 4 8 2}
Walker, F.A. 1874.
{it:Statistical atlas of the United States based on the results of the ninth census 1870.}
New York: Census Office. 

{p 4 8 2}
Wilkinson, L. 2005. 
{it:The grammar of graphics.}
New York: Springer. 

{p 4 8 2}
Young, F.W., Valero-Mora, P.M. and Friendly, M. 2006.
{it:Visual statistics: Seeing data with interactive graphics.}
Hoboken, NJ: John Wiley. 


{title:Also see}

{p 4 13 2}On-line:  help for {help histogram}, 
help for {help catplot} (if installed), 
help for {help tabplot} (if installed), 
help for {help egenmore} (if installed), 
help for {help vreverse} (if installed) 

