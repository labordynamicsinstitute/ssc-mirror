<pre>
-------------------------------------------------------------------------------
help for <b>ivpois</b>
-------------------------------------------------------------------------------
<p>
<b><u>IV/GMM Poisson regression</u></b>
<p>
<b><u>Syntax</u></b>
<p>
      <b>ivpois</b> [<i>varlist</i>] [<i>if</i>] [<i>in</i>] [<b>,</b> <b>exog(</b><i>varlist</i><b>)</b> <b>endog(</b><i>varlist</i><b>)</b> <i>other</i>
                <i>options</i>]
<p>
<a name="contents"></a>        +--------------------+
    ----+  Table of Contents +-----------------------------------------------
<p>
  General description of estimator
  Examples
  Description of options
  Remarks and saved results
  References
  Acknowledgements
  Citation of <b>ivpois</b>
  Author information
<p>
<a name="description"></a>        +-------------+
    ----+ Description +------------------------------------------------------
<p>
Using the Mata function optimize available in Stata 10, <b>ivpois</b> implements a
Generalized Method of Moments (GMM) estimator (due to Mullahy, 1997) of Poisson
regression that allows additional exogenous variables that have no direct
impact on the dependent variable to be specified, and endogenous variables to
be instrumented by excluded instruments (see ivreg2 or rd if installed, and
references therein; ssc install ivreg2 and ssc install rd to install), hence
the acronym for Instrumental Variables (IV) in its name (see Baum et al. 2007
or Nichols 2007 for more on IV).
<p>
Standard errors are estimated by the asymptotic approximation outlined by
Hansen (1982), requiring "large" samples, though bootstrapped standard errors
may outperform these in many situations (the latter are obtained by prefixing
the command with bootstrap:).  If clustering of errors is suspected, the
cluster option may be supplied to bootstrap.
<p>
Variables specified in the <b>exog(</b><i>varlist</i><b>)</b> option and not in the primary <i>varlist</i>
are used as excluded instruments that are correlated with the endogenous
variables in <b>endog(</b><i>varlist</i><b>)</b> and not the error term.
<p>
Note that Poisson regression assumes <b>E[y|X]=exp(Xb)</b> to get a consistent
estimate of <b>b</b>, so it is appropriate for a wide variety of models where the
dependent variable is nonnegative (zero or positive), not just where the
dependent variable measures counts of events.  Wherever you might be inclined
to take the logarithm of a nonnegative dependent variable <b>y</b> and use ivregress,
<b>ivpois</b> offers an alternative that includes in the estimation observations where
<b>y</b> is zero.
<p>
Assuming <b>E[y|X]=exp(Xb)</b>, one can assume either an additive error or a
multiplicative error, which produce different versions of the moment
conditions.  The model used by <b>ivpois</b> assumes a multiplicative error.  There is
no explicit support for panel models and cluster-robust SEs are supplied by 
bootstrap, using the <b><u>cl</u></b><b>uster(</b><i>varlist</i><b>)</b> option.
<p>
On the moment conditions, the additive form for the error posits that
<b>y=exp(xb)+u</b> and gives moment conditions of the form <b>Z'(y-exp(xb))=0</b>, whereas
the multiplicative form posits <b>y=exp(xb)u</b> and gives moment conditions of the
form <b>Z'(y-exp(Xb)/exp(Xb)))=Z'(y*exp(-Xb)-1))=0</b> for instruments Z satisfying
<b>E(Z'u)=0</b> (where Z includes all exogenous variables, both included and excluded
instruments).  Angrist (2001) shows that in a model with endogenous binary
treatment and a binary instrument, the latter procedure (assuming a
multiplicative error) estimates a proportional local average treatment effect
(LATE) parameter in models with no covariates.  The latter is also more
intuitively appealing and congruent with poisson and glm, and the assumption
can be rewritten <b>y=exp(xb)u=exp(xb)*exp(v)=exp(xb+v)</b> so <b>ln(y)=xb+v</b> assuming <b>y&gt;0</b>
to provide the natural link to OLS.  Windmeijer (2006) has a very useful
discussion and further related models.
<p>
<a name="examples"></a>        +----------+
    ----+ Examples +---------------------------------------------------------
<p>
In each example, you can cut and paste the entire block of code to the Command
window, or click on commands one by one to run.
<p>
-------------------------------------------------------------------------------
There is no theoretical model to support the next set of commands; they merely
illustrate syntax.  You will need to install estout from SSC to run the <b>esttab</b>
command.
-------------------------------------------------------------------------------
      est clear
      sysuse auto, clear
      poisson mpg disp wei, r
      est sto pois
      ivpois mpg wei, exog(turn) endog(disp)
      est sto endog
      ivpois mpg disp wei, exog(turn)
      est sto excl
      ivpois mpg disp wei
      est sto noexcl
      g manuf=word(make,1)
      bs, cl(manuf): ivpois mpg wei, exog(turn) endog(disp)
      est sto clustbs
      esttab *, nogaps se mti
<p>
-------------------------------------------------------------------------------
Comparison of poisson to <b>ivpois</b> with an exposure variable and a small sample.
-------------------------------------------------------------------------------
      est clear
      webuse dollhill3, clear
      tab agecat, gen(a)
      drop a4 a5
      poisson deaths smokes a?, exposure(pyears) r
      est sto p
      bs: poisson deaths smokes a?, exposure(pyears)
      est sto bsp
      ivpois deaths smokes a?, exposure(pyears)
      est sto gmm
      bs: ivpois deaths smokes a?, exposure(pyears)
      est sto bsgmm
      esttab *, nogaps se mti
<p>
-------------------------------------------------------------------------------
The following three examples offer a comparison of linear regression of ln(y)
on X to Poisson regression of y on X, and each model has some real economic
content.  You will need to install ivreg2 from SSC to run the <b>ivreg2</b> command.
-------------------------------------------------------------------------------
    An example from Card (1995):
      use http://fmwww.bc.edu/ec-p/data/wooldridge/card, clear
      loc x "exper* smsa* south mar black reg662-reg669"
      ivreg2 lw `x' (educ=nearc4)
      ivpois wage `x', endog(educ) exog(nearc4)
<p>
    An example from Mullahy (1997) where ivreg2 reports no evidence of a weak
        instruments problem:
      use http://fmwww.bc.edu/RePEc/bocode/i/ivp_bwt.dta, clear
      g lnbw=ln(bw)
      loc x "parity white male"
      loc z "edfwhite edmwhite incwhite cigtax88"
      ivreg2 lnbw `x' (cigspreg=`z')
      ivpois bw `x', endog(cigspreg) exog(`z')
<p>
    An example from Mullahy (1997) where ivreg2 reports evidence of a weak
        instruments problem:
      use http://fmwww.bc.edu/RePEc/bocode/i/ivp_cig.dta, clear
      g lnc=ln(cigpacks)
      loc x "pcigs79 rest79 income age qage educ qeduc famsize white"
      loc z "ageeduc cage ceduc pcigs78 restock"
      ivreg2 lnc `x' (k210=`z')
      ivpois cigpacks `x', endog(k210) exog(`z')
<p>
-------------------------------------------------------------------------------
An alternative Generalized Linear Model (glm) approach, due to Hardin,
Schmiediche, and Carroll (2003), is designed to address endogeneity due to
measurement error. Type findit qvf to install. The following example, loosely
based on the qvf help file, favors the GMM approach:
-------------------------------------------------------------------------------
      clear all
      set obs 1000
      gen x1 = uniform()
      gen x2 = uniform()
      gen x3 = uniform()
      gen err = invnorm(uniform())
      gen y = exp(1+2*x1+3*x2+4*x3+err)
      gen t3 = .8*x3 + .6*invnorm(uniform())
      qvf y x1 x2 x3 (x1 x2 t3), link(log) fam(poisson)
      est sto qvf
      bs: qvf y x1 x2 x3 (x1 x2 t3), link(log) fam(poisson)
      est sto bsqvf
      ivpois y x1 x2, endog(x3) exog(t3)
      est sto gmm
      bs: ivpois y x1 x2, endog(x3) exog(t3)
      est sto bsgmm
      cap ssc inst estout
      esttab *, nogaps se mti
<p>
<a name="options"></a>        +-----------------+
    ----+ Options summary +--------------------------------------------------
<p>
    <b>exog(</b><i>varlist</i><b>)</b> specifies a list of exogenous variables, possibly included
        in the primary <i>varlist</i>.  Exogenous variables not included in the
        primary <i>varlist</i> are considered<b> excluded instruments</b>.
<p>
    <b>endog(</b><i>varlist</i><b>)</b> specifies a list of endogenous variables, possibly
        included in the primary <i>varlist</i>.  Endogenous variables not included
        in the primary <i>varlist</i> are added to that list.
<p>
    <b>exposure(</b><i>varname_e</i><b>)</b>, <b>offset(</b><i>varname_o</i><b>)</b>, <b>constraints(</b><i>constraints</i><b>)</b>,
        <b>collinear</b>; see <b>[R] estimation options</b>.
<p>
    <b>from(</b><i>matrix</i><b>)</b> specifies a row matrix of initial values for optimize to
        use.
<p>
    <b>level(</b><i>#</i><b>)</b>; see <b>[R] estimation options</b>.
<p>
    Other <b>options</b> can be supplied to a bootstrap prefix, including <b>reps(n)</b>
        requesting a number of repetitions other than the default of 50 and
        the <b><u>cl</u></b><b>uster(</b><i>varlist</i><b>)</b> option. In practice, the number of bootstrap
        replications should probably be much larger than 50, and convergence
        should be examined, though simulations show that for a correctly
        specified model, 50 are sufficient for good performance.
<p>
<a name="macros"></a>        +---------------------------+
    ----+ Remarks and saved results +----------------------------------------
<p>
The command saves the following results in <b>e()</b>:
<p>
Scalars
   <b>e(N)</b>          Number of observations used in estimation
<p>
Macros
   <b>e(cmd)</b>        <b>ivpois</b>
   <b>e(version)</b>    Version number 
   <b>e(depvar)</b>     Name of dependent variable
<p>
Matrices
   <b>e(b)</b>          Coefficient vector
   <b>e(V)</b>          VCE estimate
<p>
Functions
   <b>e(sample)</b>     Marks estimation sample
<p>
<p>
<a name="refs"></a><b><u>References</u></b>
<p>
On the GMM approach, see:
<p>
    Angrist, Joshua D. 2001. "Estimation of limited dependent variable models
        with dummy endogenous regressors: simple strategies for empirical
        practice." <i>Journal of Business and Economic Statistics,</i> 19:2-16.
<p>
    Hansen, Lars P. 1982. "Large Sample Properties of Generalized Methods of
        Moments Estimators." <i>Econometrica,</i> 50:1029-1054.
<p>
    Mullahy, John. 1997. "Instrumental-Variable Estimation of Count Data
        Models: Applications to Models of Cigarette Smoking Behavior." <i>The</i>
        <i>Review of Economics and Statistics,</i> 79(4):586-593.
<p>
    Windmeijer, Frank. 2006. "GMM for Panel Count Data Models." Discussion
        Paper No. 06/591, Department of Economics, University of Bristol.
<p>
On IV methods, see:
<p>
    Baum, Christopher F., Mark E. Schaffer, and Steven Stillman. 2007.
        "Enhanced routines for instrumental variables/generalized method of
        moments estimation and testing." Stata Journal 7(4):465-506.
<p>
    Nichols, Austin. 2007. "Causal inference with observational data." Stata
        Journal 7(4):507-541.
<p>
For the glm-style approach, see:
<p>
    Hardin, James W., Henrik Schmiediche, and Raymond J. Carroll. 2003.
        "Instrumental variables, bootstrapping, and generalized linear
        models." <i>The Stata Journal</i> 3(4): 351-360.  See also
        http://www.stata.com/merror/.
<p>
The example using earnings as the outcome:
<p>
    Card, David E. 1995. "Using Geographic Variation in College Proximity to
        Estimate the Return to Schooling" in<i> Aspects of Labour Economics:</i>
        <i>Essays in Honour of John Vanderkamp,</i> edited by Louis Christofides, E.
        Kenneth Grant and Robert Swindinsky.  University of Toronto Press.
        See also NBER WP 4483.
<p>
<a name="acknow"></a><b><u>Acknowledgements</u></b>
<p>
The central code was promulgated by Bill Gould at a Seminar in DC on November
2, 2007.  That Mata code was written by David Drukker at an earlier date, and
reads as follows:
<p>
    <b>m=((1/rows(Z)):*Z'((y:*exp(-X*b') :- 1)))'</b>
    <b>crit=(m*W*m')</b>
<p>
Type viewsource ivpois.ado to see that code<i> in situ</i> (four lines into the
section that begins <b>mata:</b>, where all the Mata code appears).
<p>
Thanks to John Mullahy for sharing the data used in Mullahy (1997), and for
writing that paper. Thanks to Mark Schaffer for pointing out that using the
zero vector as an initial value can result in failure of optimize in some
cases, and suggesting a return to using the estimated coefficients from poisson
as initial values.  Thanks to Henry Schneider for asking for an exposure
option.  Thanks to John Zedlewski for asking for a speed improvment using the
d1 evaluatortype of optimize and asymptotic standard errors.
<p>
<a name="citation"></a><b><u>Citation of </u></b><b><u>ivpois</u></b>
<p>
<b>ivpois</b> is not an official Stata command. It is a free contribution to the
research community, like a paper. Please cite it as such:
<p>
    Nichols, Austin. 2007.  ivpois: Stata module for IV/GMM Poisson
        regression.  http://ideas.repec.org/c/boc/bocode/s456890.html
<p>
<b><u>Author</u></b>
<p>
    Austin Nichols
    Urban Institute
    Washington, DC, USA
    austinnichols@gmail.com
<p>
<b><u>Also see</u></b>
<p>
 Manual:  <b>[U] 23 </b> Estimation<b> and </b> post-estimation<b> commands</b>
          <b>[R] bootstrap</b>
          <b>[R] poisson</b>
          <b>[R] regress</b>
          <b>[R] ivregress</b>
<p>
 On-line: help for (if installed) ivreg2, overid, ivendog, ivhettest, ivreset, 
          xtivreg2, xtoverid, ranktest, condivreg; qvf.
</pre>