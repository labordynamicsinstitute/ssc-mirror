<pre>
-------------------------------------------------------------------------------
help for <b>ingap</b>                                                   (Roger Newson)
-------------------------------------------------------------------------------
<p>
<p>
<b><u>Insert gap observations in a data set</u></b>
<p>
        <b>ingap</b> [ <i>numlist</i> ] [ <b>if</b> <i>expression</i> ] [ <b>in</b> <i>range</i> ] [ , <b><u>af</u></b><b>ter</b>
                           <b><u>g</u></b><b>apindicator(</b><i>newvarname</i>{cmd:) <b><u>newo</u></b><b>rder(</b><i>newvarname</i><b>)</b>
                           <b><u>ro</u></b><b>wlabel(</b><i>string_varname</i><b>)</b> <b><u>gr</u></b><b>owlabels(</b><i>string_list</i><b>)</b>
                           <b><u>gre</u></b><b>xpression(</b><i>gap_row_label_expression</i><b>)</b>
                           <b><u>rs</u></b><b>tring(</b><i>string_replacement_option</i><b>)</b> ]
<p>
where <i>numlist</i> is an optional list of integers, <i>string_list</i> is a list of
strings, <i>gap_row_label_expression</i> is a string-valued expression, and
<i>string_replacement_option</i> can be <b>name</b>, <b>label</b> or <b>labname</b>.
<p>
<b>by </b><i>varlist</i><b> :</b> may be used with <b>ingap</b>; see help for by.
<p>
<p>
<b><u>Description</u></b>
<p>
<b>ingap</b> inserts gap observations into a list of positions in an existing data
set. All existing variables in the data set will have missing values in the gap
observations, unless the user specifies otherwise.  Often, the user specifies
non-missing values in the gap observations for one particular existing string
variable, known as the row label variable. This row label variable may then be
output with a list of other variables to form a publication-ready table using
the listtex package.  Alternatively, the row label variable may be encoded,
using the sencode package, to form a numeric variable with value labels, which
can then be plotted on one axis of a graph to define axis labels.  The sencode
and listtex packages are downloadable from SSC.
<p>
<b>ingap</b> inserts a gap observation next to (before or after) each of a list of
observations specified by the <i>numlist</i>. A positive number <b>i</b> in the <i>numlist</i>
specifies the <b>i</b>th existing observation in the data set, or in each by-group if
<b>by </b><i>varlist</i><b> :</b> is specified. A negative number <b>-i</b> in the <i>numlist</i> specifies the
<b>i</b>th existing observation, in reverse order, from the end of the data set, or
from the end of each by-group if <b>by </b><i>varlist</i><b> :</b> is specified.  A zero or
out-of-range number in the <i>numlist</i> is ignored. The <i>numlist</i> is set to 1 if not
specified by the user. <b>ingap</b> assumes that the data set in memory has up to 3
classes of variables.  These are the by-variables (which define by-groups
possibly representing the pages of a table), a row label variable (possibly
containing the row labels in the left margin of the table), and the remaining
variables (which may form the entries in the table rows).  A gap observation
inserted by <b>ingap</b> has the same values for the by-variables as the observation
next to which it was inserted, a row label value specified by the <b>growlabels()</b>
or <b>grexpression()</b> options, and missing values (or possibly column headings) in
the remaining variables. <b>ingap</b> may also generate new variables, indicating
whether the observation is a gap observation and/or the new order of the
observation in the data set (or by-group) after the gap observations have been
inserted.
<p>
<p>
<b><u>Options</u></b>
<p>
<b>after</b> specifies that each gap observation will be inserted after the
    corresponding existing observation in the data set or by-group specified in
    the <i>numlist</i>. If <b>after</b> is not specified, then each gap observation will be
    inserted before the corresponding existing observation.
<p>
<b>gapindicator(</b><i>newvarname</i><b>)</b> specifies the name of a new variable to be generated,
    equal to 1 for the newly-inserted gap observations and 0 for all other
    observations.
<p>
<b>neworder(</b><i>newvarname</i><b>)</b> specifies the name of a new variable to be generated,
    equal to the new sequential order of the observation within the data set
    (or within the by-group if <b>by </b><i>varlist</i><b> :</b> is specified), after the gap
    observations have been inserted.  The new variable has no missing values.
    After execution of <b>ingap</b>, the data set in memory is sorted primarily by the
    by-variables (if specified), and secondarily by the <b>neworder()</b> variable (if
    specified).
<p>
<b>rowlabel(</b><i>string_varname</i><b>)</b> specifies the name of an existing string variable,
    used as the row labels for a table whose rows are the observations. In the
    gap observations, this string variable is set to the value specified by the
    corresponding string listed in the <b>growlabels()</b> option if that option is
    specified (see below), or to a missing value otherwise.  The <b>rowlabel()</b>
    variable may not be a by-variable.
<p>
<b>growlabels(</b><i>string_list</i><b>)</b> specifies a string value for each of the row labels in
    the gap observations. The <b>j</b>th string in the <i>string_list</i> is written to the
    <b>rowlabel</b> variable in the newly-inserted gap observation inserted next to
    the <b>j</b>th observation mentioned in the <i>numlist</i>. If the <b>rowlabel</b> option is
    present and the <b>growlabel()</b> option is absent, then the <b>rowlabel()</b> variable
    is initialised to missing in the gap observations.
<p>
<b>grexpression(</b><i>gap_row_label_expression</i><b>)</b> specifies a string expression, to be
    evaluated in all gap observations to give the final values of the
    <b>rowlabel()</b> variables in these gap observations. If <b>grexpression()</b> and
    <b>growlabels()</b> are both specified, then the result of <b>grexpression()</b> replaces
    any values set by <b>growlabels()</b>. (However, the name of the <b>rowlabels()</b>
    variable may appear in the <b>grexpression()</b> expression, so that the values of
    the <b>rowlabels()</b> variable can be modified in ways depending on the original
    values set by the <b>growlabels()</b> list.) Note that, when the <b>grexpression()</b>
    expression is evaluated, all variables other than the <b>rowlabels()</b> variable
    have been set to their final values, which are missing for all variables
    except the by-variables and the <b>rowlabel()</b> variable, except if they have
    been set to other values by the <b>rstring()</b> option (see below). However, the
    <b>grexpression()</b> expression may access values of variables in adjacent
    observations using subscripting. If by-variables are present, then any
    subscripts in the expression specified by <b>grexpression()</b> are defined within
    by-groups, and are defined including the gap observations. For instance, if
    a gap observation is inserted at the beginning of each by-group, then the
    value of <b>_n</b> in these gap observations will be 1.
<p>
<b>rstring(</b><i>string_replacement_option</i><b>)</b> specifies a rule for replacing the values of
    string variables (other than the by-variables and row label variables)) in
    gap observations.  If <b>rstring()</b> is set to <b>name</b>, then string variables which
    are not by-variables or row label variables are reset to their variable
    names in by-gap observations. If <b>rstring()</b> is set to <b>label</b>, then string
    variables that are not by-variables or row label variables are set to their
    variable labels in by-gap observations, or to missing values if their
    variable labels are missing.  If <b>rstring()</b> is set to <b>labname</b>, then string
    variables that are not by-variables or row label variables are set to their
    variable labels in by-gap observations, or to their variable names if their
    variable labels are missing.  If <b>rstring()</b> is set to any other value, or
    not set, then string variables that are not by-variables or row label
    variables are set to missing values.  (Note that numeric variables that are
    not by-variables are always set to numeric missing values in gap
    observations.) The <b>rstring()</b> option allows the user to add a row of column
    headings to a data set of string variables, or to add a row of column
    headings to each by-group of a data set of string variables.  Note that
    numeric variables may be converted to string variables using the sdecode
    package, downloadable from SSC, before using <b>ingap</b> and listtex. This allows
    the user to use the <b>rstring</b> option, and also to format numeric variables in
    ways not possible using Stata formats alone, such as adding parentheses to
    confidence limits.
<p>
<p>
<b><u>Remarks</u></b>
<p>
<b>ingap</b> is typically used to convert a Stata data set to a form with 1
observation per table row (including gap rows), or 1 observation per graph axis
label (including gap axis labels).  The user can then list the data set as a
TeX, LaTeX, HTML or Microsoft Word table, using the listtex package
(downloadable from SSC).  Alternatively, for immediate impact, the user can use
the sencode package (downloadable from SSC) to encode the row labels to a
numeric variable, and then plot this numeric variable against other variables
using Stata graphics programs.  For instance, a user of Stata 8 or above might
use eclplot (downloadable from SSC) to produce horizontal confidence interval
plots, with the row labels on the vertical axis.  It is often advisable for the
user to type preserve before a sequence of commands including <b>ingap</b>, and to
type restore after a sequence of commands using <b>ingap</b>, because <b>ingap</b> modifies
the data set by adding new observations. It is often also advisable for the
user to place the whole sequence of commands in a do-file, and to execute this
do-file, rather than to type the sequence of commands one by one at the
terminal.
<p>
<p>
<b><u>Examples</u></b>
<p>
        . ingap, g(toprow)
<p>
        . ingap 1 53, g(toprow) row(make) grow("US cars" "Non-US cars")
<p>
        . by foreign: ingap, g(gind) row(make) grow("Car model")
<p>
        . sort foreign rep78 make
        . by foreign rep78: ingap
        . by foreign: ingap -1, after
        . by foreign: ingap, row(make) grow("Car model")
        . list
<p>
The following example works in the <b>auto</b> data if the user has installed the 
listtex package, downloadable from SSC. It outputs to the Results window a
generic ampersand-delimited text table, which can be cut and pasted into a
Microsoft Word document, and then converted to the rows of a table inside
Microsoft Word, using the menu sequence <b>Table-&gt;Convert-&gt;Text to Table</b>.  (Note
that the listtex command can alternatively create table rows suitable for input
into a TeX, LaTeX or HTML file.)
<p>
        . preserve
        . by foreign: ingap, row(make) grexp(cond(foreign,"Non-US cars","US
                cars"))
        . listtex make mpg weight, type
        . restore
<p>
The following example works in the <b>auto</b> data if the user has installed the 
listtex package, and also the sdecode package, both of which can be downloaded
from SSC.) It outputs to the Results window a generic ampersand-delimited text
table, which can be cut and pasted into a Microsoft Word document (as in the
previous example), and then converted into two tables, one for American cars
and one for non-American cars, each with a title line containing the variable
labels in the <b>auto</b> data. Note that, to do this, the user must convert the
numeric variables to string variables, and this is done using sdecode.
<p>
        . preserve
        . sdecode mpg, replace
        . sdecode weight, replace
        . sdecode price, replace
        . by foreign: ingap, rstring(labname)
        . listtex make mpg weight price, type
        . restore
<p>
The following example works in the <b>auto</b> data if the user has installed the 
sdecode and sencode packages, downloadable from SSC.  It produces a graph of
mileage by car type (US or non-US) and repair record.
<p>
        . preserve
        . sdecode rep78, gene(row) miss
        . by foreign: ingap, row(row) grexp(cond(foreign,"Others:","US cars:"))
                gap(gapind)
        . sencode row, replace many gs(foreign -gapind rep78)
        . lab var row "Repair record"
        . version 7: graph row mpg, yreverse ylab(1(1)12) yscale(0 13)
                xlab(0(10)50)
        . restore
<p>
Other examples of the use of <b>ingap</b>, together with other packages, can be found
in Newson (2003).
<p>
<p>
<b><u>Author</u></b>
<p>
Roger Newson, King's College, London, UK.  Email: roger.newson@kcl.ac.uk
<p>
<p>
<b><u>References</u></b>
<p>
Newson, R. 2003. Confidence intervals and <i>p</i>-values for delivery to the end
user.  <i>The Stata Journal</i> 3(3): 245-269. Also downloadable from Roger Newson's
website at http://www.kcl-phs.org.uk/rogernewson.
<p>
<p>
<b><u>Acknowledgement</u></b>
<p>
I would like to thank Nicholas J. Cox, of the University of Durham, U.K., for
writing the hplot package, downloadable from SSC. This package gave me a lot of
the ideas used in <b>ingap</b>, and was also my preferred package for producing
confidence interval plots under Stata Versions 6 and 7, before I had access to
the improved graphics of Stata Version 8.
<p>
<p>
<b><u>Also see</u></b>
<p>
 Manual:  <b>[U] 14.1.2 by varlist:</b>, <b>[U] 14.5 by varlist: construct</b>, <b>[U] 31.2 The</b>
                     <b>by construct</b>, <b>[R] by</b>, <b>[R] expand</b>
On-line:  help for by, byprog, ssc
          help for listtex, sencode, sdecode, hplot, eclplot if installed
</pre>