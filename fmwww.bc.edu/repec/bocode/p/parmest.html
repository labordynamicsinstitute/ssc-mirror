<pre>
-------------------------------------------------------------------------------
help for <b>parmest</b> and <b>parmby</b>        (STB-49: dm65; STB-58: dm65.1; Roger Newson)
-------------------------------------------------------------------------------
<p>
<p>
<b><u>Convert estimation results to a dataset with one observation per parameter</u></b>
<p>
    The <b>parmest</b> package includes 4 modules, which produce datasets with one
    observation for each of a set of estimated parameters, and variables
    containing estimates, confidence limits, <i>P</i>-values and other parameter
    attributes.  The modules are as follows:
<p>
   Module      Description
   --------------------------------------------------------------------------
   <b>parmest</b>     Create dataset from most recent estimation results
   <b>parmby</b>      Create dataset by calling an estimation command once for each
                 by-group
   <b>parmcip</b>     Confidence limits and <i>P</i>-values from estimates and standard
                 errors
   <b>metaparm</b>    Confidence limits and <i>P</i>-values for linear combinations of
                 independent parameters (as in a meta-analysis)
   --------------------------------------------------------------------------
<p>
<p>
    The modules <b>parmest</b> and <b>parmby</b> are documented here.  See <b>parmcip</b> and 
    <b>metaparm</b> for the other modules.
<p>
<p>
<b><u>Syntax for </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
        <b>parmest</b> <b>,</b> [ <i>parmest_outdest_opts</i> <i>parmest_ci_opts</i> <i>parmest_varadd_opts</i>
                     <i>parmest_varmod_opts</i> ]
<p>
        <b>parmby</b> [<b>`</b>]<b>"</b><i>command</i><b>"</b>[<b>'</b>] <b>,</b> [ <i>parmest_opts</i> <i>parmby_only_opts</i> ]
<p>
    where <i>parmest_opts</i> are options allowed by <b>parmest</b> and <i>command</i> is a Stata
    estimation command.
<p>
<p>
<b><u>Description</u></b>
<p>
    <b>parmest</b> takes, as input, the most recently calculated set of estimation
    results, created by the most recently executed estimation command.  It
    creates, as output, a new dataset, with one observation per estimated
    parameter, and variables containing parameter names, estimates, standard
    errors, <i>z</i>-test or <i>t</i>-test statistics, <i>P</i>-values, confidence limits, and
    other estimation results if requested by the user.  <b>parmby</b> executes an
    estimation command for each by-group specified by the by-variables, and
    then creates an output dataset with one observation for each estimated
    parameter in each by-group for which the command executed successfully,
    and all the variables in the <b>parmest</b> output dataset, together with the
    by-variables.  The output dataset created by <b>parmby</b> or <b>parmest</b> may be
    listed to the Stata log, or saved to a disk file, or written to the
    memory (overwriting any pre-existing dataset).
<p>
<p>
<b><u>Options for use with </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
    <b>parmest</b> and <b>parmby</b> have a large number of options, which fall into the
    following 5 groups:
<p>
   Option group          Description
   --------------------------------------------------------------------------
   <i>parmest_outdest_opts</i>  Output-destination options for <b>parmest</b> and <b>parmby</b>
   <i>parmest_ci_opts</i>       Confidence-interval options for <b>parmest</b> and <b>parmby</b>
   <i>parmest_varadd_opts</i>   Variable-adding options for <b>parmest</b> and <b>parmby</b>
   <i>parmest_varmod_opts</i>   Variable-modifying options for <b>parmest</b> and <b>parmby</b>
   <i>parmby_only_opts</i>      Options for <b>parmby</b> only
   --------------------------------------------------------------------------
<p>
<p>
<p>
<a name="parmest_outdest_opts"></a><b><u>Output-destination options for </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
<p>
    <i>options</i>                           Description
    -------------------------------------------------------------------------
    <b><u>li</u></b><b>st(</b><i>list_spec</i><b>)</b>                   List output dataset to Stata log and/or
                                        Results window
    <b><u>sa</u></b><b>ving(</b><i>filename</i>[<b>,replace</b>]<b>)</b>        Save output dataset to a disk file
    <b>no</b><b><u>re</u></b><b>store</b>                         Write output dataset to memory
    <b>fast</b>                              Write output dataset to memory without
                                        precautions
    <b><u>fl</u></b><b>ist(</b><i>global_macro_name</i><b>)</b>          Append output filename to a global
                                        macro
    -------------------------------------------------------------------------
<p>
    where <i>list_spec</i> is a specification of the form
<p>
    [<i>varlist</i>] [<i>if</i>] [<i>in</i>] [ , [<i>list_options</i> ] ]
<p>
    and <i>list_options</i> is a list of options used by the <b>list</b> command.
<p>
    See <i>parmest_outdest_opts</i> for details of these options.
<p>
<p>
<a name="parmest_ci_opts"></a><b><u>Confidence-interval options for </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
<p>
    <i>options</i>                   Description
    -------------------------------------------------------------------------
    <b><u>ef</u></b><b>orm</b>                     Exponentiate estimates and confidence limits
    <b><u>d</u></b><b>of(</b><i>#</i><b>)</b>                    Degrees of freedom for calculating confidence
                                limits
    <b><u>le</u></b><b>vel(</b><i>numlist</i><b>)</b>            Confidence level(s) for calculating confidence
                                limits
    <b><u>cln</u></b><b>umber(</b><i>numbering_rule</i><b>)</b>  Numbering rule for naming confidence limit
                                variables
    -------------------------------------------------------------------------
<p>
    where <i>numbering_rule</i> is
<p>
    <b>level</b> | <b>rank</b>
<p>
    See <i>parmest_ci_opts</i> for details of these options.
<p>
<p>
<a name="parmest_varadd_opts"></a><b><u>Variable-adding options for </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
<p>
    <i>options</i>           Description
    -------------------------------------------------------------------------
    <b><u>l</u></b><b>abel</b>             Variable containing <i>X</i>-variable labels
    <b><u>yl</u></b><b>abel</b>            Variable containing <i>Y</i>-variable labels
    <b><u>idn</u></b><b>um(</b><i>#</i><b>)</b>          Numeric dataset ID variable
    <b><u>ids</u></b><b>tr(</b><i>string</i><b>)</b>     String dataset ID variable
    <b><u>sta</u></b><b>rs(</b><i>numlist</i>)    Variable containing stars for the <i>P</i>-value
    <b><u>em</u></b><b>ac(</b><i>name_list</i><b>)</b>   Variables containing macro estimation results
    <b><u>es</u></b><b>cal(</b><i>name_list</i><b>)</b>  Variables containing scalar estimation results
    <b><u>er</u></b><b>ows(</b><i>name_list</i><b>)</b>  Variables containing rows of matrix estimation results
    <b><u>ec</u></b><b>ols(</b><i>name_list</i><b>)</b>  Variables containing columns of matrix estimation
                        results
    <b><u>ev</u></b><b>ec(</b><i>name_list</i><b>)</b>   Variables containing vectors extracted from matrix
                        estimation results
    -------------------------------------------------------------------------
<p>
    where <i>name_list</i> is a list of names of Stata estimation results.
<p>
    See <i>parmest_varadd_opts</i> for details of these options.
<p>
<p>
<a name="parmest_varmod_opts"></a><b><u>Variable-modifying options for </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
<p>
    <i>options</i>                   Description
    -------------------------------------------------------------------------
    <b><u>ren</u></b><b>ame(</b><i>renaming_list</i><b>)</b>     Rename variables in the output dataset
    <b><u>fo</u></b><b>rmat(</b><i>formatting_list</i><b>)</b>   Display formats for variables in the output
                                dataset
    <b>float</b>                     Set storage type of numeric variables to <b>float</b>
                                or lower precision
    <b>no</b><b><u>dou</u></b><b>ble</b>                  Alternative option for specifying <b>float</b>
    -------------------------------------------------------------------------
<p>
    where <i>renaming_list</i> is a list of  variable names of form
<p>
    <i>oldvarname_1 newvarname_1 ... oldvarname_n newvarname_n</i>
<p>
    and <i>formatting_list</i> is a list of form
<p>
    <i>varlist_1</i><i> </i><i>format_1</i><i> ... </i><i>varlist_n</i><i> </i><i>format_n</i>
<p>
    See <i>parmest_varmod_opts</i> for details of these options.
<p>
<p>
<a name="parmby_only_opts"></a><b><u>Options for </u></b><b><u>parmby</u></b><b><u> only</u></b>
<p>
<p>
    <i>options</i>                   Description
    -------------------------------------------------------------------------
    <b>by(</b><i>varlist</i><b>)</b>               Specify variables defining by-groups
    <b><u>com</u></b><b>mand</b>                   Add a variable containing the command called
    -------------------------------------------------------------------------
<p>
    See <i>parmby_only_opts</i> for details of these options.
<p>
<p>
<b><u>Output datasets created by </u></b><b><u>parmest</u></b><b><u> and </u></b><b><u>parmby</u></b>
<p>
    These output datasets (or resultssets) are described in detail in
    <i>parmest_resultssets</i>.
<p>
<p>
<b><u>Remarks</u></b>
<p>
    More information about the use of <b>parmest</b> and <b>parmby</b> can be found in
    Newson (2006), Newson (2004), Newson (2003) and Newson (2002).  <b>parmby</b> is
    similar to <b>statsby</b>, but it creates a data set with one observation per
    parameter per by-group, instead of a dataset with one observation per
    by-group.  Also, in default, <b>parmby</b> outputs the estimation output to the
    log, although the user can use <b>quietly</b> to suppress this.  <b>parmby</b> is a
    "quasi-byable" front end for <b>parmest</b>, and is the command usually used in
    any new programs for creating output datasets on disk or in memory.
    <b>parmest</b> is an older command, and is now mainly used when the user wishes
    simply to use the <b>list()</b> option to list the estimation output with
    user-specified formats or parameter labels, so that the results for each
    parameter are presented to a number of decimal places and/or significant
    figures specified by the user, and may also be labelled with the variable
    label corresponding to the parameter.  The <b>parmest</b> command is therefore
    used as an alternative version of <b>estimates table</b>, with the added power
    to calculate confidence limits.  With either <b>parmby</b> or <b>parmest</b>, the user
    must specify at least one of the four output-destination options <b>list()</b>,
    <b>saving()</b>, <b>norestore</b> and/or <b>fast</b>.  These options specify whether the
    output dataset is listed to the Stata log, saved to a disk file, or
    written to the memory (overwriting any pre-existing dataset).  <b>parmby</b>
    works by calling <b>parmest</b>, which in turn works by calling <b>parmcip</b>.
<p>
<p>
<b><u>Examples</u></b>
<p>
    The following examples use the <b>list()</b> option to list a subset of the
    output dataset to the Stata log.  After these examples are executed,
    there is no new dataset either in the memory or on disk.
<p>
                . regr price mpg weight,robust
                . parmest,format(estimate min95 max95 %8.2f p %8.1e) list(,)
<p>
                . glm mpg foreign,family(gamma) link(power -1)
                . parmest, label format(estimate min95 max95 p %8.2f p %8.1e)
                    list(parm label estimate min95 max95 p,clean noobs)
<p>
                . regress price mpg weight foreign displacement headroom,robust
                . parmest,label format(p %8.1e) stars(0.05 0.01 0.001 0.0001)
                    list(parm label estimate min95 max95 p stars,clean noobs)
<p>
                . parmby "qui logit foreign mpg, or", label eform for(estimate
                    min95 max95 %8.2f p %8.1e) li(parm label estimate min95
                    max95 p)
<p>
                . parmby "qui logit foreign mpg, or", level(95 97.5) cln(rank)
                    label eform for(estimate min* max* %8.2f p %8.1e) li(parm
                    label estimate min2 min1 max1 max2 p,clean noobs)
<p>
    The following examples use the <b>norestore</b> option to create an output
    dataset in the memory, overwriting any pre-existing dataset.
<p>
                . parmby "regr mpg weight,robust",by(foreign) label command
                    norestore
<p>
                . gene gpm=1/mpg
                . rename foreign t
                . parmby "regr gpm weight",by(t) label rename(t tstat p pvalue)
                    norestore
<p>
                . gene byte pw1=1
                . svyset [pwei=pw1],clear
                . parmby "svy :mean mpg weight price, over(foreign)", norestore
                    ylabel ev(_N _N_subp) es(N)
                . describe
                . list eq ylabel parm ev_* es_* estimate min95 max95
<p>
<p>
    The following examples use the <b>saving()</b> option to create an output
    dataset in a disk file.
<p>
                . parmby "logit foreign mpg, or", label eform
                    saving(parms1.dta)
<p>
                . parmby "regr mpg weight displ,robust",by(foreign) label
                    saving(myparms,replace)
<p>
    The following example demonstrates the use of <b>parmby</b> with multiple
    regression commands, saving the results in multiple temporary datasets.
    These datasets are then concatenated, using the <b>dsconcat</b> command, to form
    a single dataset in memory, with one observation for each parameter in
    each regression. The <b>dsconcat</b> command can be downloaded from SSC using
    the <b>ssc</b> command in Stata. This example uses the options <b>idnum()</b> and
    <b>idstr()</b> to create analysis identifier variables, which contain
    information identifying the regression model for each parameter.
<p>
                . gene gpm=1/mpg
                . tempfile tf1 tf2 tf3
                . parmby "regr gpm weight", lab saving(`tf1',replace) idn(1)
                    ids(Unadjusted)
                . parmby "regr gpm foreign", lab saving(`tf2',replace) idn(2)
                    ids(Unadjusted)
                . parmby "regr gpm weight foreign", lab saving(`tf3',replace)
                    idn(3) ids(Adjusted)
                . dsconcat `tf1' `tf2' `tf3'
                . list idnum idstr parm estimate min95 max95 p,noobs nodis
<p>
    The following advanced example demonstrates the use of the <b>flist()</b> option
    with the <b>dsconcat</b> command (downloadable from SSC). <b>parmby</b> is used to
    carry out multiple regression analyses, storing the results in temporary
    files, whose names are stored by the <b>flist()</b> option in a global macro
    <b>tflist</b>. The files are then concatenated using <b>dsconcat</b> to create a single
    long dataset in memory, with one observation for each parameter of each
    regression model.  For each parameter, the regression model for that
    parameter is identified by the string variable <b>command</b> and the numeric
    analysis identifier variable <b>idnum</b>. The dataset is sorted, described and
    listed.
<p>
                . global tflist ""
                . global modseq=0
                . foreach X of var headroom trunk weight length turn
                    displacement gear_ratio {
                .  global modseq=$modseq+1
                .  tempfile tf$modseq
                .  parmby "regr mpg `X'",by(foreign) label command
                    format(estimate min95 max95 %8.2f p %8.1e) idn($modseq)
                    saving(`tf$modseq',replace) flist(tflist)
                . }
                . dsconcat $tflist
                . sort idnum command foreign parmseq
                . describe
                . by idnum command:list foreign parm label estimate min95 max95
                    p,noobs
<p>
    More examples can be found in Newson (2006), Newson (2004), Newson (2003)
    and Newson (2002).
<p>
<p>
<b><u>Acknowledgments</u></b>
<p>
    I would like to thank Nicholas J. Cox of Durham University, UK, for
    coining the word resultsset to describe an output dataset created by a
    Stata program such as <b>parmest</b>.
<p>
<p>
<b><u>Author</u></b>
<p>
    Roger Newson, Imperial College London, UK.  Email:
    r.newson@imperial.ac.uk
<p>
<p>
<b><u>References</u></b>
<p>
    Newson, R. 2006. Resultssets, resultsspreadsheets, and resultsplots in
        Stata.  Presented at the 4th German Stata Users' Group Meeting,
        Mannheim, 31 March, 2006.  Also downloadable from Roger Newson's
        website at http://www.imperial.ac.uk/nhli/r.newson.
<p>
    Newson, R. 2004. From datasets to resultssets in Stata.  Presented at the
        10th United Kingdom Stata Users' Group Meeting, London, 29 June,
        2004.  Also downloadable from Roger Newson's website at
        http://www.imperial.ac.uk/nhli/r.newson.
<p>
    Newson, R. 2003. Confidence intervals and <i>p</i>-values for delivery to the
        end user.  <i>The Stata Journal</i> 3(3): 245-269. Pre-publication draft
        downloadable from Roger Newson's website at
        http://www.imperial.ac.uk/nhli/r.newson.
<p>
    Newson, R. 2002. Creating plots and tables of estimation results using
        <b>parmest</b> and friends.  Presented at the 8th UK Stata Users' Group
        Meeting, 20-21 May, 2002.  Also downloadable from Roger Newson's
        website at http://www.imperial.ac.uk/nhli/r.newson.
<p>
<p>
<b><u>Also see</u></b>
<p>
        STB: STB-49 dm65, STB-58 dm65.1
     Manual: <b>[U] 20 Estimation and post-estimation commands</b>,
             <b>[D] statsby</b>, <b>[D] append</b>
    On-line: help for <b>append</b>, <b>statsby</b>, estcom, postest, est_table
             help for <b>parmcip</b>, <b>metaparm</b>, <i>parmest_outdest_opts</i>, 
             <i>parmest_ci_opts</i>, <i>parmest_varadd_opts</i>, <i>parmest_varmod_opts</i>,
             <i>parmby_only_opts</i>, <i>parmest_resultssets</i>
             help for <b>dsconcat</b> and <b>lincomest</b> if installed
</pre>